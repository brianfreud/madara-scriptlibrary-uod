;==================================
; Script Name: Simple Stealth Trainer
; Author: LagHon
; Version: 1.00
; Client Tested with: 6.0.11.0 Patch 87
; EUO version tested with: 1.5 V144
; Shard OSI / FS: OSI
; Revision Date: 24/11/2008
; Public Release: 25/11/2008
; Global Variables Used: None
; Purpose: Trains Stealthing and Hiding in your house or anywhere you choose
;==================================
;
;==============================================
;Instructions
;Select a line of tiles where you wish to train (Recommend in your house)
;The line can be East west, North South, Diagonal or even offset
;Just ensure theres not too many obstacles to negotiate around
;Put your char on the start tile of the line and Click Set Start Button
;Move your char to the end tile of the line and click Set End Button
;Click Run Button, Your char will hide and walk up and down the line
;Whenever it reaches the end or start of the line and is not hidden it will rehide
;and start walking again
;NOTE* Script is oblivious to what your skill level is,
;what you are wearing or anything else or anything else around you.
;All it does is hides and walks.
; Scaletta:
; 0 - 30 NPC Teach
; 30.1 - 65 Vestiti normali (in cloth)
; 65.1 - 95 Armatura completa studded
; 95.1 - 100 Armatura completa studded e Close Helm
;===============================================

menu Button b_start 5 5 100 25 Set Start
menu Button b_end 5 35 100 25 Set End
menu Button b_run 5 65 100 25 Run
menu Button b_stop 5 95 100 25 Stop
menu Window Size 80 130
menu Show
set %x1 0
set %y1 0
set %x2 0
set %y2 0
event macro 1 0 Hello whats up?
Main:
set #menuButton N/A
while #menuButton = N/A
{
}
if #menuButton = b_start
   goto setstart
if #menubutton = b_end
   goto setend
if #menubutton = b_run
{
   set #menubutton N/A
   goto stealthinit
}
goto main

setstart:
set %x1 #charposx
set %y1 #charposy
event macro 1 0 I will start at %x1 %y1
goto main

setend:
set %x2 #charposx
set %y2 #charposy
event macro 1 0 I will turn back at %x2 %y2
goto main

stealthinit:
if %x1 = 0 || %y1 = 0
{
event macro 2 0 Scratches head
wait 10
event macro 1 0 I dont know where to start
goto main
}

if %x2 = 0 || %y2 = 0
{
event macro 2 0 Scratches head
wait 10
event macro 1 0 I dont know where to turn around
goto main
}
if %x1 = %x2 && %y1 = %y2
{
   event macro 2 0 Puzzled Look
   event macro 1 0 You want me to start and end on the same spot??
   goto Main
}
event macro 1 0 Starting my walk
goto stealth

stealth:
if #menubutton = b_stop
{
   event macro 1 0 Stopping now
   goto Main
}
If H notIn #charStatus
{
gosub hider
}
while #charposx <> %x1 || #charposy <> %y1
{
move %x1 %y1 0 4
}
if #menubutton = b_stop
{
   event macro 1 0 Stopping now
   goto Main
}
If H notIn #charStatus
gosub hider
while #charposx <> %x2 || #charposy <> %y2
{
move %x2 %y2 0 4
}
goto stealth

sub hider
hideloop:
event Macro 13 21
If H notIn #charStatus
{
wait 100
goto hideloop
}
return