;==================================================
; Name: Ticket_Exporter
; Author: Madara
; Shard FS: UODreams
; Client Tested With: 7.0.15 - 7.0.21 - 7.0.22
; EUO version tested with: 1.5 v.236
; Public Release: 01/06/2013
; Last Revision: 01/06/2013
; Purpose: Legge tutti i biglietti della lotteria
;          che avete nel backpack e li esporta in
;          txt nella cartella di easyuo.
;==================================================
ignoreitem reset ticket
if *reset_tikexp = N/A
   set *reset_tikexp #true
if *reset_tikexp = #false
{
   display yesnocancel Sovrascrivere il file "Ticket_Exporter_EXPORT"?
   if #DispRes = YES
      set *reset_tikexp #true
   if #DispRes = NO
      set *reset_tikexp #false
   if #DispRes = CANCEL
      halt
}

_main:
      finditem ETH C_ , #BackPackID
      if #findkind <> -1
      {
         str Left #date 2
         set %_a 20 , #strres
         str Mid #date 3 2
         set %_b #strres
         str Right #date 2
         set %_c #strres
         set #sysmsgcol 0
         event sysmessage Trovati #FindCnt Biglietti.
         set #sysmsgcol 2050
         gosub open 1 Ticket_Exporter_EXPORT.txt
         if *reset_tikexp = #true
         {
             gosub wipe 1
             set *reset_tikexp #false
         }
         gosub write 1 Totale Biglietti: #FindCnt
         gosub write 1 Data: %_c , / , %_b , / , %_a
         for %_exp 1 #FindCnt
         {
             event sysmessage Export in corso... , %_exp
             event Property #findid
             str Pos #Property [
             set %_1 ( #strres + 2 )
             str Pos #Property ]
             set %_2 #strres
             set %_3 ( %_2 - %_1 )
             str Mid #Property %_1 %_3
             gosub write 1 #strres
             ignoreitem #findid ticket
             finditem ETH C_ , #BackPackID
         }
      }
      else
      {
          display ok Non hai biglietti nel backpack.
             halt
      }
      gosub dump 1
      display yesnocancel Export Completato! , $
                +Grazie per aver usato il Ticket_Exporter, Madara.$$
                +Aprire il file "Ticket_Exporter_EXPORT.txt" ora?
                if #DispRes = YES
                   execute Ticket_Exporter_EXPORT.txt
             halt
goto _main

;==================================
; Script Name: filesubs.txt (import version)
; Author: Kal In Ex
; Version: 1.10
; Client Tested with: 4.0.6a
; EUO version tested with: 1.42.009d
; Shard OSI / FS: OSI
; Revision Date: April 11, 2005
; Public Release: November 16, 2004
; Global Variables Used:
; Purpose: writing to files in EUO
; Copyright 2004, 2005 Kal In Ex
;==================================

; subs open, GetOSFileCmd, wipe, fwipe, write and dump
; are all a part of filesubs.txt only use this script
; in its entirety and do not modify it!

sub open ; <identifier> <filename>
	namespace push
	namespace local _filebuffers
	set !_filename %1 , _filename
	set !_buffer %1 , _buffer
	set !_dirop %1 , _dirop
	set ! . !_filename %2
	set ! . !_buffer
	set ! . !_dirop >>
	if !_filesub = n/a
		{
		gosub GetOSFileCmd _
		if #result = command.com
			{
			set !_filesub _filesubsW1
			set !_filecmd command.com
			}
		if #result = cmd.exe
			{
			set !_filesub _filesubsW2
			set !_filecmd cmd.exe
			}
		}
	namespace pop
	return

;==================
;**
;* @name GetOSFileCmd
;* @ver 1.0 9May04
;* @author Roadkill
;* @purpose check the OS and set @%1 to cmd.exe or command.exe if win98/2000xp
;* @params %1 the varname to hold the return command
;* @returns @%1 and #result hold cmd.exe for win2k and xp, or command.exe for 98/me
;* @example call rksubs.txt GetOSFileCmd filecmd
;* @status done
sub GetOSFileCmd
	str Left #osver 1
	set % . %1 cmd.exe
	if #strRes = 1
		set % . %1 command.com
	return % . %1
;==================

sub wipe ; <identifier>
	namespace push
	namespace local _filebuffers
	set !_buffer %1 , _buffer
	set !_dirop %1 , _dirop
	set ! . !_buffer
	set ! . !_dirop >
	namespace pop
	return

sub fwipe ; <identifier>
	namespace push
	namespace local _filebuffers
	set !_filename %1 , _filename
	set !_buffer %1 , _buffer
	set ! . !_buffer
	execute !_filecmd /c type > ! . !_filename
	namespace pop
	return

sub write ; <identifier> <text> <text> <text...>
	namespace push
	namespace local _filebuffers
	if %0 > 2
		{
		for !_ 3 %0
			{
			set %2 %2 , #spc , % . !_
			}
		}
	if !_filesub = _filesubsw1 ; win98 file writing
		{
		str len %2
		for !_ #strres 1
			{
			str mid %2 !_ 1
			if #strres in _<>
				{
				str ins %2 ^ !_
				set %2 #strres
				}
			}
		set !_filename %1 , _filename
		set !_dirop %1 , _dirop
		execute !_filecmd /c echo %2 ! . !_dirop ! . !_filename
		set ! . !_dirop >>
		}
	if !_filesub = _filesubsw2 ; xp file writing
		{
		str len %2
		for !_ #strres 1
			{
			str mid %2 !_ 1
			if #strres in _&|()^<>
				{
				str ins %2 ^ !_
				set %2 #strres
				}
			}
		set !_filename %1 , _filename
		set !_buffer %1 , _buffer
		set !_dirop %1 , _dirop
		str len ! . !_buffer
		set !_ #strres
		str len %2
		if !_ + #strres >= 1900
			{
			execute !_filecmd /c ! . !_buffer
			set ! . !_buffer
			}
		str len ! . !_buffer
		if #strres = 0 || ! . !_dirop = >
			set ! . !_buffer echo , #spc , %2 , #spc , ! . !_dirop , #spc , ! . !_filename
		else
			set ! . !_buffer ! . !_buffer , #spc , && , #spc , echo , #spc , %2 , #spc , ! . !_dirop , #spc , ! . !_filename
		set ! . !_dirop >>
		}
	namespace pop
	return

sub dump ; <identifier>
	namespace push
	namespace local _filebuffers
	set !_filename %1 , _filename
	set !_buffer %1 , _buffer
	str len ! . !_buffer
	if #strres > 0
		{
		execute !_filecmd /c ! . !_buffer
		set ! . !_buffer
		}
	namespace pop
	return
