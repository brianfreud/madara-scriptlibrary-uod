;==================================
; Script Name: TURBOLOOTER
; Author: Tony
; Version: 1.3
; Client Tested with: 6.0.1.3
; EUO version tested with: 1.5 v142
; Shard OSI / FS: FS
; Revision Date: 25/12/08
; Public Release: 6/12/08
; Purpose: Loots corpses, saves previous preferences in system registry
; v1.3 adds: Avoid criminal status if choosen.
; All blade-type types added.
; Dragon scales, leather and wool types added.
; Double hotkey.
; General fixes.
;==================================
gosub readRegistry
gosub createMenu
gosub acquireTypes
while #menubutton <> start
{
if #menubutton = CLOSED
halt
wait 1
}
menu Hide
gosub writeRegistry
gosub getPreferences
gosub getRace
gosub openStatusBar
while #true
{
onhotkey %hotkey2 %hotkey1
{
exevent dropc #backpackid
if #weight < ( #maxweight - 10 + %racemod )
gosub loot
else
event exmsg #charid 3 30 Too Weight!
}
wait 1
}
stop

sub loot
finditem YFM_PNF_JNF_QNF_YNF_WNF_KNF_MNF_YNF_ G_2
if #findkind = -1 2
sound
return
set #lobjectid #findid
set %corpse #findid
ignoreitem #findid
event macro 17 0
set %init_loot #scnt
set %init_journal #jindex
while #contsize <> 144_212
{
wait 5
event macro 17 0
if #scnt - %init_loot > 2
return
}
wait 5
set %end_journal #jindex
menu get h21
if #menures = #false
{
for %i %init_journal %end_journal
{
scanjournal %i
if criminal in #journal
{
event sysmessage To avoid a criminal action this corpse will be skipped.
if #contsize = 144_212
click %closex %closey f r dmc
deletejournal
return
}
}
}
wait 10
if JJG in %to_loot || OFF in %to_loot || VLK in %to_loot ;resources looting
{
finditem %lame C_ , #backpackid
if #findkind = -1
finditem %lame C_ , #charid
if #findkind = -1
event sysmessage No bladed items were found in your backpack.
else
{
set #lobjectid #findid
event macro 17 0
target 2s
set #ltargetid %corpse
set #ltargetkind 1
event macro 22 0
wait 1
finditem JJG_OFF_VLK C_ , %corpse
set %find_init #scnt
while #findkind = -1
{
wait 1
finditem JJG_OFF_VLK C_ , %corpse
if #scnt - %find_init > 1
break
}
}
}
finditem %to_loot C_ , %corpse
for %j 1 #findcnt
{
set #findindex %j
exevent drag #findid #findstack
wait 8
if #weight > ( #maxweight - 10 + %racemod )
exevent dropc %corpse
else
exevent dropc #backpackid
wait 8
}
finditem %rawhide C_ , #backpackid
if #findkind <> -1
{
set %to_cut #findid
finditem %scissors C_ , #backpackid
if #findkind = -1 2
event sysmessage No scissors were found in your backpack.
break
else
{
set #lobjectid #findid
event macro 17 0
target 2s
set #ltargetid %to_cut
set #ltargetkind 1
event macro 22 0
wait 10
}
}
event exmsg #charid 3 30 Looted Everything!
set %closex 70 + #contposx
set %closey 110 + #contposy
if #contsize = 144_212
click %closex %closey f r dmc
return

sub writeRegistry
for %i 1 21
{
set %my_var h . %i
menu get %my_var
if #menures = #false
set *TURBO_LOOTER_P . %i #false
else
set *TURBO_LOOTER_P . %i #true
}
menu get hotkey1
set *TURBO_LOOTER_HOTKEY1 #menures
set %hotkey1 #menures
menu get hotkey2
set *TURBO_LOOTER_HOTKEY2 #menures
set %hotkey2 #menures
return

sub readRegistry
for %i 1 21
{
set %my_var *TURBO_LOOTER_P . %i
if %my_var = N/A || %my_var = #false
set %P . %i #false
else
set %P . %i #true
}
if *TURBO_LOOTER_HOTKEY1 = N/A
set *TURBO_LOOTER_HOTKEY1 ctrl
if *TURBO_LOOTER_HOTKEY2 = N/A
set *TURBO_LOOTER_HOTKEY2 L
return

sub CreateMenu
menu clear
set #MenuButton N/A
menu Font BGColor white
menu font color black
menu window color white
menu Window Crea Sacchette
set %x 100
set %y 100
menu Window Size 122 570
menu Font Size 8
menu Show %x %y
;POTIONS
menu Font Style b
menu text title0 1 2 Turbo Looter - v1.3
menu text title1 5 20 Potions
menu Font Style
menu check h1 8 40 120 15 %P1 Heal
menu check h2 8 60 120 15 %P2 Cure
menu check h3 8 80 120 15 %P3 Agility
menu check h4 8 100 120 15 %P4 Refresh
menu check h5 8 120 120 15 %P5 Poison
menu check h6 8 140 120 15 %P6 Strength
menu check h7 8 160 120 15 %P7 Explosion

;REAGENTS
menu Font Style b
menu text title2 5 180 Reagents
menu Font Style
menu check h8 8 200 120 15 %P8 Mage
menu check h9 8 220 120 15 %P9 Necromancer

;MISC
menu Font Style b
menu text title3 5 240 Miscellaneous
menu Font Style
menu check h10 8 260 120 15 %P10 Bandages
menu check h11 8 280 120 15 %P11 Bolas
menu check h12 8 300 120 15 %P12 Magic Apple
menu check h13 8 320 120 15 %P13 Arrows/Bolts
menu check h14 8 340 120 15 %P14 Teleport Scroll
menu check h15 8 360 120 15 %P15 Gold
menu check h16 8 380 120 15 %P16 Doom Bones
menu check h17 8 400 120 15 %P17 Feathers
menu check h18 8 420 120 15 %P18 Leather
menu check h19 8 440 120 15 %P19 Wool
menu check h20 8 460 120 15 %P20 Dragon Scales
;OPTIONS
menu Font Style b
menu text title4 5 480 Options
menu Font Style
menu check h21 8 500 120 15 %P21 Ignore Criminal Flag

menu edit hotkey1 8 520 40 *TURBO_LOOTER_HOTKEY1
menu edit hotkey2 41 520 20 *TURBO_LOOTER_HOTKEY2
menu text title4 65 525 HotKey
menu Font Style b
menu Button start 0 550 122 20 Start

return

sub getPreferences
set %to_loot #spc

menu get h1
if #menures = #true
set %to_loot %to_loot , _ , %heal
menu get h2
if #menures = #true
set %to_loot %to_loot , _ , %cure
menu get h3
if #menures = #true
set %to_loot %to_loot , _ , %agility
menu get h4
if #menures = #true
set %to_loot %to_loot , _ , %refresh
menu get h5
if #menures = #true
set %to_loot %to_loot , _ , %poison
menu get h6
if #menures = #true
set %to_loot %to_loot , _ , %strength
menu get h7
if #menures = #true
set %to_loot %to_loot , _ , %explosion
menu get h8
if #menures = #true
set %to_loot %to_loot , _ , %reagenti_mago
menu get h9
if #menures = #true
set %to_loot %to_loot , _ , %reagenti_necro
menu get h10
if #menures = #true
set %to_loot %to_loot , _ , %bende
menu get h11
if #menures = #true
set %to_loot %to_loot , _ , %bolas
menu get h12
if #menures = #true
set %to_loot %to_loot , _ , %melette
menu get h13
if #menures = #true
set %to_loot %to_loot , _ , %frecce
menu get h14
if #menures = #true
set %to_loot %to_loot , _ , %scroll
menu get h15
if #menures = #true
set %to_loot %to_loot , _ , %gold
menu get h16
if #menures = #true
set %to_loot %to_loot , _ , %bones
menu get h17
if #menures = #true
set %to_loot %to_loot , _ , %feathers
menu get h18
if #menures = #true
set %to_loot %to_loot , _ , %rawhide
menu get h19
if #menures = #true
set %to_loot %to_loot , _ , %wool
menu get h20
if #menures = #true
set %to_loot %to_loot , _ , %scales
;display ok %to_loot
return

sub acquireTypes
;POZIONI
set %agility YUF
set %cure NUF
set %refresh ZUF
set %heal UUF
set %poison AVF
set %strength XUF
set %explosion TUF

;REAGENTI
set %reagenti_mago RZF_SZF_WZF_MZF_JZF_JUF_KUF_KZF
set %reagenti_necro TZF_IUF_UZF_DUF_YZF

;MISC
set %bende ZLF
set %bolas KTO
set %melette CBS
set %frecce RWF_LNK
set %scroll UTL
set %gold POF
set %bones OZF
set %feathers VLK
set %leather JJG
set %wool OFF
set %rawhide DEG
set %scissors KAG
set %scales STO

;BLADED ITEMS
set %lame YPO_BNF_CPH_INF_FSF_OSF_ASF_HNF_FMH_CNF_LSF_NSF_ISF_SMH_RMH_CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_SFR_
set %lame %lame , CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_JDP_EDP_YCP_KDP_
set %lame %lame , RFR_NFR_SFR_OFR_EFR_YFR_XFR_DFR_
return

sub getRace
finditem #charid
if #findtype in IS_HS
set %racemod 50
else
set %racemod 0
return

sub openStatusBar
if #charname = N/A 2
event macro 8 2
wait 1s
return
