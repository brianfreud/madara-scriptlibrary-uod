; By Morhua
;
; Info:
; In questi giorni ho macrato provocation da 0, avendo però già music 
; a 120. Ho seguito il metodo qui riportato http://forum.gamesnet.it/f500/traini...
; cation-564362/ senza però ottenere grossi risultati, in quanto i Cu Sidhe tamati 
; e successivamente rilasciati hanno una barding difficulty troppo bassa (82-84) per
; permettere di arrivare a 120.
;
; Sono riuscito a trovare un metodo alternativo che necessita solamente di:
; - magery (sufficientemente alta per non flizzare troppo i vortex, io con 110 andavo + che bene)
; - lrc 100%
; - una barca
; - alcune bagball
; - strumenti musicali meglio se riuscite a procurarvene exceptional, normal e 5/6 arachnid slayer
;
; Poichè sono rimasto soddisfatto di come funziona (come pure un amico a cui l'ho fatto provare) ho deciso di postare lo script che mi sono creato.
; Essenzialmente il metodo si basa su due step distinti:
; 1- (Da 40 a 85) Provocare vortex contro vortex
; 2- (Da 85 in su) Provocare vortex contro Yamandon
; Il primo lo fate comodamente a casa vostra, piazzando delle bagball e bloccandole a terra, in modo da impedire ai vortex di muoversi o di venire in contatto.
; Il secondo richiede un po' + di preparazione, in quanto dovete trovare uno Yamadon, attirarlo vicino alla riva per poi salire velocemente sulla barca che avrete precedentemente piazzato. Nulla di difficile, basta non attaccarlo e stare attenti ai mostri castanti che spawnano in zona (Rune Beetle dove sono andato io).
;
; Ecco uno specchietto riassuntivo con musicianship a 120.
; Se avete music a 100 occorre aggiungere 5 punti ai valori riportati:
; (Grazie a matteomassone e Blakkina per la scaletta)
;
; 0-28 : train della skill dall' NPC
; 28-40 : provocate polar bear, white wolf e snow leopard
; 40-75 : provocate vortex con strumenti exe
; 75-85 : provocate vortex con strumenti normali
; 85-105 : provocate Yamadon con strumenti exe
; 105-115 : provocate Yamadon con strumenti normali
; 115-120 : provocate Yamadon con strumenti arachnid (sono opposite slayer)
;
; Qui di seguito riporto lo script, è facile da usare, basta seguire le istruzioni. Ricordate che lo script non fa distinzione tra gli strumenti e usa tutti quelli che trova nel backpack. Per separare normal/exe/arachnid usate un qualsiasi contenitore, lasciando nel livello principale dello zaino solo gli strumenti che volete usare. 
; Se riscontrate qualche problema dovuto a miei errori  e me lo riportate, cercherò di rimediare. Buon macraggio.

set %target3 N/A
set %x1 N/A
set %y1 N/A
set %x2 N/A
set %y2 N/A
finditem NH G_8     ;yamadon type
if #findkind <> -1
{
   set %target3 #findid
   event SysMessage Yamadon trovato!
}
set %instruments KRF_LQF_QRF_PRF_MQF_OQF_NRF_PGP
chooseskill provo
set #TARGCURS 1
if %target3 = N/A
{
   event SysMessage Posiziona il mirino sul tile dove evocare il primo vortex e premi ESC
   while %x1 = N/A
   {
      onhotkey ESC
      {
         set %x1 #cursorx
         set %y1 #cursory
         event SysMessage OK!
      }
      wait 2
   }
   set #TARGCURS 1
   event SysMessage Posiziona il mirino sul tile dove evocare il secondo vortex e premi ESC
   while %x2 = N/A
   {
      onhotkey ESC
      {
         set %x2 #cursorx
         set %y2 #cursory
         event SysMessage OK!
      }
      wait 2
   }
}
else
{
   event SysMessage Posiziona il mirino sul tile dove evocare i vortex e premi ESC
   while %x1 = N/A
   {
      onhotkey ESC
      {
         set %x1 #cursorx
         set %y1 #cursory
         set %x2 #cursorx
         set %y2 #cursory
         event SysMessage OK!
      }
      wait 2
   }
}
set #TARGCURS 0
gosub find_instrument
;**************************************
;**************************************
;**************************************
begin:
if #skill = #skillcap
{
    display Hai raggiunto il cap nella skill!
    halt
}
if #FOLLOWERS < 2
   gosub cast_vortex
set #LTARGETKIND 1
if %target3 <> N/A
{
   for %i 1 3
   {
      event macro 13 22
      set #ltargetid %target . %i
      target 20
      event macro 22
      if %i = 3
         set #ltargetid %target1
      else
         set #ltargetid %target3
      target 20
      set %jindex #jindex
      event macro 22
      gosub delay %jindex
   }
}
else
{
   for %i 1 2
   {
      event macro 13 22
      set #ltargetid %target . %i
      target 20
      event macro 22
      if %i = 1
         set #ltargetid %target2
      else
         set #ltargetid %target1
      target 20
      set %jindex #jindex
      event macro 22
      gosub delay %jindex
   }
}
goto begin
;**************************************
;**************************************
;**************************************
sub delay
wait 10
for %counter %1 #jindex
{
   scanjournal %counter
   if your_music_fails in #journal || You_play_poorly in #journal
      wait 90
   if your_music_succeeds in #journal
      wait 190
   if the_instrument_played_its_last_tune in #journal || too_far_away in #journal
      gosub find_instrument
}
return
;**************************************
sub cast_vortex
while #FOLLOWERS < 2
{
   event macro 15 57
   target 100
   set %time #SCNT + 5
   while #TARGCURS = 0
   {
      if %time > #SCNT
         break
      wait 1
   }
   while #TARGCURS = 1
   {
      click %x1 %y1
      wait 1
   }
   wait 3
}
while #FOLLOWERS < 4
{
   event macro 15 57
   target 100
   set %time #SCNT + 5
   while #TARGCURS = 0
   {
      if %time > #SCNT
         break
      wait 1
   }
   while #TARGCURS = 1
   {
      click %x2 %y2
      wait 1
   }
   wait 3
}
finditem YI_EG G_6
if #findkind <> -1
{
   for #findindex 1 #findcnt
      set %target . #findindex #findid
}
return
;**************************************
sub find_instrument
finditem %instruments C_ , #backpackid
if #findkind = -1
   halt
set #LOBJECTID #findid
wait 1
event macro 17
wait 10
return