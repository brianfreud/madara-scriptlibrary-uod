;===================================
;PVP UTILITIES by Vash ver. 1.5
;===================================
;Qual è il vantaggio di questo script? Un solo tasto per il scendere e salire su qualsiasi tuo pet!!
;Aggiunte inoltre tante utility per i target, ovviamente per i maghi.
;Puoi salvare un target, targettare tranquillamente un'altra persona e poi ricaricare il vecchio target!
;Hai una macro per scrivere /accept e una per scrivere /add al volo.
;Setta qui i seriali dei tuoi pet e delle petball (puoi aggiungere anche il terzo pet/petball)
;(per ottenere il seriale, casta una magia a caso e targetta l'oggetto e il pet e guarda la variabile #ltargetid)
set %pet1 HYEJ
set %pet2 AUCH
set %curpet  %pet1 ; current pet
set %ball1 BCLIOMD
set %ball2 BCEBKMD
set %lallo FC_QC_GC_PC_ZGB_MHB_BK_JG_MG_SG_OF_SF_YJ_EC_TJ_UJ_SC

;Ricorda che il current pet è il primo quando avvii lo script.
;Se cambi pet manualmente, per settare il nuovo current pet, usa la petball mentre sei sopra di esso.
;Ovviamente ogni volta che usi una petball, viene settato il relativo pet come current pet.
;Qundi se appena apri lo script ad esempio premendo il tasto per il remount sali sul 
_mainloop:
onhotkey Q ALT ; Con questo tasto chiami con petball 1
     GoSub PetBall 1
onhotkey W ALT ; Con questo tasto chiami con petball 2
     GoSub PetBall 2
onhotkey SPACE CTRL ; Con questo tasto sali o scendi sul pet
     GoSub Remount
onhotkey F5
     GoSub SaveTarget ; Con questo memorizzi l'ultimo target
onhotkey F6
     GoSub GetTarget ; Con questo riprendi l'ultimo target memorizzato
onhotkey 8 ALT
    GoSub Party 1 ; Say /add
onhotkey 9 ALT
    GoSub Party 2 ; Say /accept
goto _mainloop

;Inutile specificare che i tasti son modificabili.
;Ma non toccate nulla da qui in giu.
;Ci sono altre sub utili per chi le volesse provare ma sembrano buggate.
;Intedo per poisonare lalli nemici o targettare il proprio.
;Ma come già detto sembrano non funzionare al meglio, quindi adesso faccio a mano.

Sub PetBall
set %i %1
set %var %ball . %i
set #lobjectid %var
event macro 17 0
wait 8
event macro 1 0 All Come
wait 1
set %var %pet . %i
set #lobjectid %var
event macro 17 0
set %curpet %var
return

Sub Remount
ignoreitem reset
finditem %curpet G
if #findkind = -1 ; sei a cavallo
     {
     set #lobjectid #charid
     event macro 17 0
     return
     }
event macro 1 0 All Come
wait 1
set #lobjectid %curpet
event macro 17 0
return

Sub SaveTarget
wait 1
event sysmessage Last Target Salvato
set %savedtarget #ltargetid
return

Sub GetTarget
wait 1
event sysmessage Last Target Ripristinato
set #ltargetid %savedtarget
target 3s
event macro 22 0
return

Sub TargetPet
GoSub SaveTarget
set #ltargetid %curpet
target 3s
event macro 22 0
return

Sub InNoxPet
_findloop:
finditem %lallo G_16
if #findkind = -1
     {
     event sysmessage Nessun Pet Nemico Trovato
     ignoreitem reset
     return
     }
if #findrep <> 5
     {
     ignoreitem #findid
     goto _findloop
     }
event sysmessage Pet Nemico Trovato! Attendo target...
set #ltargetid #findid
target 3s
event macro 22 0
ignoreitem reset
return

Sub Party
if %1 = 1
     {
     msg /
     wait 2
     msg add$
     }
if %1 = 2
    {
    msg /
    wait 2
    msg accept$
    }
return