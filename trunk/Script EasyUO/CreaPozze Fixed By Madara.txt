; ############################################
; Script Name: Guadah's Alchemist Assistant
; Author: Guadah, Fixed By Madara
; Version: 1.3.0
; Client Tested with: 7.0.22
; EUO version tested with: 1.5 V 236
; Revision Date: 23/04/2013
; Public Release: 7-11-2008
; Purpose: To Craft Potions
; Special Thanks:
; * Trailmyx Persistant Variable Sub, advice & support - www.scriptuo.com
; * Freddy for advice & support - www.winuo.org
; ############################################
set %alch_tool RQF
set %alch_tools_needed 1
set %tink_tool GTL_KTL_JTL
set %tink_tools_needed 2
set %bottle WUF
set %ingot_type ENK
set %board TLK
set %size1 160
set %size2 130
set %size3 ( %size2 + 3 )
set %item_to_make 100
set %amount_to_make 200
set %currently_crafting 300
set %resources WUF_ENK_TLK_XWS_YWS_YZF_KZF_KUF_RZF_MZF_JUF_WZF_SZF_JZF_TZF_YZF_AFZ_VBM
set %finished_items ZUF_AVF_NUF_UUF_XUF_YUF_TUF_OUF_AFZ
set %catposx 30
set %selposx 230
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXXXXXXXXXXXX MENU DESIGN XXXXXXXXXXXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
menu hideeuo
menu window title Guadah's Crafting Assistant
menu window color silver
menu window size 290 %size1
menu font bgcolor gray
menu shape potionbg 5 5 280 65 3 7 1 Black 7 Gray
menu font align right
menu text title 80 12 Resources:
menu text title 80 42 Potions:
menu font align left
menu font bgcolor silver
menu font size 10
menu button target_resource_bag 175 10 30 24 Set
menu button target_dump_bag 175 40 30 24 Set
menu edit resource_bag 85 10 85 %resource_bag
menu edit dump_bag 85 40 85 %dump_bag
menu button start 230 40 50 25 Start
;menu button quit 230 40 50 25 Quit
menu font size 9
menu font color black
menu text titles 10 110 Select Item to Make
menu text titles 172 110 Amount
menu button add_more 235 105 20 20 +
menu button remove 264 105 20 20 -
; Counter Box
menu shape %counter_box 235 %size2 50 22 3 7 1 Black 7 Gray
menu font bgcolor gray
menu text %currently_crafting 240 %size3 %finished_count

; Status Box
menu shape potionbg 5 75 280 25 3 7 1 Black 7 Gray
menu text status_title 10 80 Status:
menu font size 8
menu font color black
 menu check tools 210 6 74 25 #true Make Tools
menu font bgcolor silver
menu combo create %item_to_make 5 %size2 150
; #######      ALCHEMY ITEMS      #######
menu combo add %item_to_make Black Powder
menu combo add %item_to_make Darkglow
menu combo add %item_to_make Deadly Poison
menu combo add %item_to_make Fuse Cord
menu combo add %item_to_make Greater Agility
menu combo add %item_to_make Greater Conflagration
menu combo add %item_to_make Greater Confusion Blast
menu combo add %item_to_make Greater Cure
menu combo add %item_to_make Greater Heal
menu combo add %item_to_make Greater Explosion
menu combo add %item_to_make Greater Poison
menu combo add %item_to_make Greater Strength
menu combo add %item_to_make Invisibility
menu combo add %item_to_make Match Cord
menu combo add %item_to_make Night Sight
menu combo add %item_to_make Normal Poison
menu combo add %item_to_make Parasitic
menu combo add %item_to_make Potash
menu combo add %item_to_make Scouring Toxin
menu combo add %item_to_make Total Refreshment
menu edit %amount_to_make 170 %size2 50 0
menu font bgcolor gray
menu font color maroon
menu font size 9
menu text status 60 80 Please wait, Initializing Script...
menu show
check_container_status_loop:
gosub TM_GetPersistantVariable resource_bag
menu set resource_bag #result
menu get resource_bag
set #menubutton = N/A
if #menures = N/A
   {
   menu set status Please set your Resource Chest
   }
if #menures <> N/A
   {
   set %resource_bag #menures
   set #lobjectid %resource_bag
   event macro 17 0
    wait 10
   contpos 575 400
    wait 10
   }
gosub TM_GetPersistantVariable dump_bag
menu set dump_bag #result
menu get dump_bag
if #menures = N/A && %resource_bag <> N/A
   {
   menu set status Please set your bag for Finished Pots
   }
if #menures <> N/A
   {
   set %dump_bag #menures
   set #lobjectid %dump_bag
   }
if %resource_bag <> N/A && %dump_bag <> N/A
   {
   menu set status Select item to make and click "Start"
   }
menuloop:
if #menuButton = target_resource_bag
   {
   set #targCurs 1
	 set_resource_bag:
    if #targCurs = 1
     goto set_resource_bag
   Set %resource_bag #ltargetid
   Set #lobjectid %resource_bag
   Event Macro 17 0
    wait 10
   contpos 610 440
    wait 10
   menu font size 10
   menu set resource_bag %resource_bag
   gosub TM_SetPersistantVariable resource_bag %resource_bag
   set #menubutton N/A
   goto check_container_status_loop
   }
if #menuButton = target_dump_bag
   {
   set #targCurs 1
	 set_dump_bag:
    if #targCurs = 1
     goto set_dump_bag
   Set %dump_bag #ltargetid
   menu font size 10
   menu set dump_bag %dump_bag
   gosub TM_SetPersistantVariable dump_bag %dump_bag
   set #menubutton N/A
   }
if #menubutton = add_more
   {
   gosub add_more
   set #menubutton N/A
   }
if #menubutton = remove
   {
   gosub remove
   set #menubutton N/A
   }
if #menubutton = Start
   {
   set #menubutton N/A
   set %size2 130
   set %item_to_make 100
   set %finished %amount_to_make
   set %amount_to_make 200
   set %currently_crafting 300
   set %old_stack 0
   menu delete Start
   menu font bgcolor silver
   menu font size 10
   menu font color black
   menu button Pause 230 40 50 25 Pause
   gosub loop
   }
if #menubutton = Quit
   {
   set #menubutton N/A
   display ok Thank you for using my script.$
           +$
           +Script is Halting$
   menu hide
   halt
   }
if #menubutton = Closed
   {
   halt
   }
goto menuloop
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXXXXX SUB TO ADD ANOTHER OPTION XXXXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub add_more
set %size1 ( %size1 + 25 )
set %size2 ( %size2 + 25 )
set %size3 ( %size3 + 25 )
set %item_to_make ( %item_to_make + 1 )
set %amount_to_make ( %amount_to_make + 1 )
set %counter_box ( %counter_box + 1 )
set %currently_crafting ( %currently_crafting + 1 )
menu window size 290 %size1
menu font size 8
menu font color black
menu font bgcolor silver
menu combo create %item_to_make 5 %size2 150
menu combo add %item_to_make Black Powder
menu combo add %item_to_make Darkglow
menu combo add %item_to_make Deadly Poison
menu combo add %item_to_make Fuse Cord
menu combo add %item_to_make Greater Agility
menu combo add %item_to_make Greater Conflagration
menu combo add %item_to_make Greater Confusion Blast
menu combo add %item_to_make Greater Cure
menu combo add %item_to_make Greater Heal
menu combo add %item_to_make Greater Explosion
menu combo add %item_to_make Greater Poison
menu combo add %item_to_make Greater Strength
menu combo add %item_to_make Invisibility
menu combo add %item_to_make Match Cord
menu combo add %item_to_make Night Sight
menu combo add %item_to_make Normal Poison
menu combo add %item_to_make Parasitic
menu combo add %item_to_make Potash
menu combo add %item_to_make Scouring Toxin
menu combo add %item_to_make Total Refreshment
menu edit %amount_to_make 170 %size2 50 0
menu shape %counter_box 235 %size2 50 22 3 7 1 Black 7 Gray
menu font bgcolor gray
menu font size 9
menu text %currently_crafting 240 %size3 %finished_count
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXXXXXX SUB TO REMOVE AN OPTION XXXXXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub remove
if %item_to_make = 100
   return
set %size1 ( %size1 - 25 )
set %size2 ( %size2 - 25 )
menu window size 290 %size1
menu delete %item_to_make
menu delete %amount_to_make
menu delete %counter_box
set %item_to_make ( %item_to_make - 1 )
set %amount_to_make ( %amount_to_make - 1 )
set %counter_box ( %counter_box - 1 )
set %currently_crafting ( %currently_crafting - 1 )
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXXXXXXXX MAIN LOOP FOR SCRIPT XXXXXXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub loop
repeat
gosub clear_pack %resources
gosub clear_finished
set %finished_count 0
set %old_count 0
gosub resource_init
gosub check_tool %required_tool %required_tool_needed %required_tool_catposy %required_tool_selposy
gosub grab_resources %resource1 %resource2 %resource3 %resource4
gosub craft_item
until #menubutton = CLOSED
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXX SUB TO FIND AND RETURN RESOURCES XXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub clear_pack
gosub CheckPause
menu set status Clearing pack of resources...
finditem %1 C_ , #backpackid
if #findcnt < 1
   return
for %i 1 #findcnt
    {
    finditem %1 C_ , #backpackid
    exevent drag #findid #findstack
     wait 10
    exevent dropc %resource_bag
     wait 15
    }
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXXX SUB TO CHECK AND CRAFT TOOLS XXXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub check_tool
gosub CheckPause
menu set status Checking for Tools
finditem %1 C_ , #backpackid
if #findcnt >= %2
   return
menu get tools
if #menures = #false
   {
   finditem %1 C_ , %resource_bag
   if #findcnt < 1
      {
      display ok You are out of %5 $
              +You will need Alch Tools in your Resource Chest$
              +Please get more and restart the script.$
              +Script has halted$
      halt
      }
   exevent drag #findid
    wait 10
   exevent dropc #backpackid
    wait 15
   }
menu set status Crafting required Tools
set %to_make ( %2 - #findcnt ) ; Sets a number of tools to make
set #lobjectid #findid
; --------------------------------
; This section looks for ingots, then pulls 100
finditem %ingot_type C_ , %resource_bag
for #findindex 1 #findcnt
    {
    event property #findid
    if copper || shadow || bronze || agapite || gold || verite || valorite in #property
     ignoreitem #findid
    if ingots in #property && shadow notin #property
    set %ingots #findid    ; Finds only Iron Ingots
    }
finditem %ingots C_ , %resource_bag
if #findstack < 20
   {
   display ok You are out of ingots.$
           +Please get more and resetart script.
   gosub clear_pack
   halt
   }
finditem %ingots C_ , #backpackid
if #findstack < 10
   {
   finditem %ingots C_ , %resource_bag
   exevent drag #findid 100
    wait 10
   exevent dropc #backpackid
    wait 15
   }
; --------------------------------
; Makes tool required, and loops %to_make (number of tools needed) times
finditem %alch_tool C_ , #backpackid
if #findcnt < 1
{
 menu get tools
 finditem %alch_tool C_ , %resource_bag
 if #menures = #false && #findid = X
 {
   display ok You are out of %5 $
   +You will need Alch Tools in your Resource Chest$
   +Please get more and restart the script.$
   +Script has halted$
   halt
 }
 if #findcnt > 0
 {
  exevent drag #findid
  wait 10
  exevent dropc #backpackid
  wait 15
 }
 finditem %tink_tool C_ , #backpackid
 set %tinkerq #findcnt
 if #findcnt < 1
 {
  finditem %tink_tool C_ , %resource_bag
  set %tinkerq #findcnt
  if #findcnt < 1
  {
   display ok You need at least one tinker tool to begin$
   +Script has halted.$
   halt
  }
  else
  {
   exevent drag #findid
   wait 10
   exevent dropc #backpackid
   wait 15
  }
 }
 if %tinkerq < %tink_tools_needed
 {
  set %tq ( %tink_tools_needed - %tinkerq )
  set #lobjectid #findid
  set #ltargetkind 1
  event macro 17 0
  gosub gumpwait
  set %x #contposx + 26
  set %y #contposy + 109
  click %x %y ; Selects Category
  gosub gumpwait
  for %i 1 %tq
  {
   set %x #contposx + 233
   set %y #contposy + 131
   click %x %y ; Selects Tool
   gosub gumpwait
  }
  finditem %1 C_ , #backpackid
  if #findcnt < %2
   gosub check_tool
 }
 set #lobjectid #findid
 set #ltargetkind 1
 event macro 17 0
 gosub gumpwait
 set %x #contposx + 26
 set %y #contposy + 109
 click %x %y ; Selects Category
 gosub gumpwait
 for %i 1 %alch_tools_needed
 {
  set %x #contposx + 233
  set %y #contposy + 89
  click %x %y ; Selects Tool
  gosub gumpwait
 }
 finditem %alch_tool C , #backpackid , _ , %resource_bag
 if #findcnt < %2
  gosub check_tool
 gosub clear_pack %ingot_type
 gosub clear_pack %tink_tool
}
set %makefirst #TRUE
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXX SETS ITEMS NEEDED FOR EACH POTION XXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub resource_init
gosub CheckPause
menu set status Checking potion requirements
menu get %item_to_make
if #menuRes = 5 ; Greater Agility
   {
   set %item_name Greater , #spc , Agility
   set %resource1 WUF ; Empty Bottles
   set %resource2 JUF ; Bloodmoss
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Bloodmoss
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 5
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 110
   set %selposy 90
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   }
menu get %item_to_make
if #menuRes = 1 ; Black Powder
   {
   finditem AFZ C_ , %resource_bag
   for #findindex 1 #findcnt
       {
       event property #findid
       if charcoal in #property
       set %resource1 #findid
       }
   set %item_name Black , #spc , Powder
   finditem AFZ C_ , %resource_bag
   for #findindex 1 #findcnt
       {
       event property #findid
       if saltpeter in #property
       set %resource2 #findid
       }
   set %resource3 SZF ; sulfurous ash
   set %resource2_name Charcoal
   set %resource1_name Saltpeter
   set %resource3_name Sulfurous , #spc , Ash
   set %amount_to_grab1 600
   set %amount_to_grab2 100
   set %amount_to_grab3 100
   set %minimum_required_resource1 6
   set %minimum_required_resource2 1
   set %minimum_required_resource3 1
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 150
   set %selposy 210
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 6 ; Greater Conflagration
   {
   set %item_name Greater , #spc , Conflagration
   set %resource1 WUF ; Empty Bottles
   set %resource2 TZF ; Grave Dust
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Grave , #spc , Dust
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 10
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 250
   set %selposy 90
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 7 ; Greater Confusion Blast
   {
   set %item_name Greater , #spc , Confusion , #spc , Blast
   set %resource1 WUF ; Empty Bottles
   set %resource2 YZF ; Pig Iron
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Pig , #spc , Iron
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 10
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 250
   set %selposy 130
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 8 ; Greater Cure
   {
   set %item_name Greater , #spc , Cure
   set %resource1 WUF ; Empty Bottles
   set %resource2 KZF ; Garlic
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Garlic
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 6
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 210
   set %selposy 110
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 2 ; Darkglow
   {
   set %item_name Darkglow
   set %resource1 WUF ; Empty Bottles
   set %resource2 XWS ; Luminescent Fungi
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Luminescent , #spc , Fungi
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 5
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 270
   set %selposy 110
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 10 ; Greater Explosion
   {
   set %item_name Greater , #spc , Explosion
   set %resource1 WUF ; Empty Bottles
   set %resource2 SZF ; Sulfurous Ash
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Sulfurous , #spc , Ash
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 10
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 230
   set %selposy 110
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 4 ; Fuse Cord
   {
   set %item_name Fuse , #spc , Cord
   finditem AFZ C_ , %resource_bag
   for #findindex 1 #findcnt
       {
       event property #findid
       if black in #property && powder in #property
       set %resource1 #findid
       }
   set %resource2 NLF_PLF_QLF ; Ball of Yarn
   set %resource3 VBM ; Water (ID of a Pitcher of Water)
   finditem AFZ C_ , %resource_bag
   for #findindex 1 #findcnt
       {
       event property #findid
       if potash in #property
       set %resource4 #findid
       }
   set %resource1_name Black , #spc , Powder
   set %resource2_name Ball , #spc , of , #spc , Yarn
   set %resource3_name Water
   set %resource4_name Potash
   set %amount_to_grab1 20
   set %amount_to_grab2 20
   set %amount_to_grab3 20
   set %amount_to_grab4 20
   set %minimum_required_resource1 1
   set %minimum_required_resource2 1
   set %minimum_required_resource3 1
   set %minimum_required_resource4 1
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 150
   set %selposy 250
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 9 ; Greater Heal
   {
   set %item_name Greater , #spc , Heal
   set %resource1 WUF ; Empty Bottles
   set %resource2 JZF ; Ginseng
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Garlic
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 5
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 150
   set %selposy 110
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 13 ; Invisibility
   {
   set %item_name Invisibility
   set %resource1 WUF ; Empty Bottles
   set %resource2 WZF ; Nightshade
   set %resource3 JUF ; Bloodmoss
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Nightshade
   set %resource3_name Bloodmoss
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %amount_to_grab3 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 4
   set %minimum_required_resource3 3
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 270
   set %selposy 70
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 14 ; Match Cord
   {
   set %item_name Match , #spc , Cord
   set %resource1 AFZ ; Saltpeter
   for #findindex 1 #findcnt
       {
       event property #findid
       if saltpeter in #property
       set %resource4 #findid
       }
   set %resource2 NLF_PLF_QLF ; Ball of Yarn
   set %resource3 VBM ; Water (ID of a Pitcher of Water)
   finditem AFZ C_ , %resource_bag
   for #findindex 1 #findcnt
       {
       event property #findid
       if potash in #property
       set %resource4 #findid
       }
   set %resource1_name Saltpeter
   set %resource2_name Ball , #spc , of , #spc , Yarn
   set %resource3_name Water
   set %resource4_name Potash
   set %amount_to_grab1 20
   set %amount_to_grab2 20
   set %amount_to_grab3 20
   set %amount_to_grab4 20
   set %minimum_required_resource1 1
   set %minimum_required_resource2 1
   set %minimum_required_resource3 1
   set %minimum_required_resource4 1
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 150
   set %selposy 230
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 15 ; Night Sight
   {
   set %item_name Night , #spc , Sight
   set %resource1 WUF ; Empty Bottles
   set %resource2 RZF ; Spiders' Silk
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Spiders' , #spc , Silk
   set %amount_to_grab1 100
   set %amount_to_grab2 100
   set %minimum_required_resource1 1
   set %minimum_required_resource2 1
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 130
   set %selposy 70
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 17 ; Parasitic
   {
   set %item_name Parasitic
   set %resource1 WUF ; Empty Bottles
   set %resource2 YWS ; Parasitic Plant
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Parasitic , #spc , Plant
   set %amount_to_grab1 100
   set %amount_to_grab2 100
   set %minimum_required_resource1 1
   set %minimum_required_resource2 5
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 270
   set %selposy 90
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 16 ; Normal Poison
   {
   set %item_name Greater , #spc , Poison
   set %resource1 WUF ; Empty Bottles
   set %resource2 WZF ; Nightshade
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Nightshade
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 2
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 190
   set %selposy 90
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 11 ; Greater Poison
   {
   set %item_name Greater , #spc , Poison
   set %resource1 WUF ; Empty Bottles
   set %resource2 WZF ; Nightshade
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Nightshade
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 4
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 190
   set %selposy 110
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 3 ; Deadly Poison
   {
   set %item_name Deadly , #spc , Poison
   set %resource1 WUF ; Empty Bottles
   set %resource2 WZF ; Nightshade
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Nightshade
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 8
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 190
   set %selposy 130
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 18 ; Potash
   {
   set %item_name Potash
   set %resource1 VBM ; Water
   set %resource2 TLK_ZLK ; Boards & Logs
   set %resource1_name Water
   set %resource2_name Boards , #spc , (Wood)
   set %amount_to_grab1 10
   set %amount_to_grab2 10
   set %minimum_required_resource1 1
   set %minimum_required_resource2 1
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 190
   set %selposy 210
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 20 ; Total Refreshment
   {
   set %item_name Deadly , #spc , Poison
   set %resource1 WUF ; Empty Bottles
   set %resource2 KUF ; Black Pearl
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Black , #spc , Pearl
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 5
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 90
   set %selposy 90
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 19 ; Scouring Toxin
   {
   set %item_name Scouring , #spc , Toxin
   set %resource1 WUF ; Empty Bottles
   set %resource2 XIY ; Toxic Venom Sac
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Toxic , #spc , Venom , #spc , Sac
   set %amount_to_grab1 10
   set %amount_to_grab2 10
   set %minimum_required_resource1 1
   set %minimum_required_resource2 1
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 130
   set %selposy 190
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
menu get %item_to_make
if #menuRes = 12 ; Greater Strength
   {
   set %item_name Greater , #spc , Strength
   set %resource1 WUF ; Empty Bottles
   set %resource2 MZF ; Mandrake Root
   set %resource1_name Empty , #spc , Bottles
   set %resource2_name Mandrake , #spc , Root
   set %amount_to_grab1 100
   set %amount_to_grab2 500
   set %minimum_required_resource1 1
   set %minimum_required_resource2 5
   set %required_tool RQF
   set %required_tool_needed 1
   set %required_tool_catposy 130
   set %required_tool_selposy 90
   set %catposy 170
   set %selposy 90
   menu getnum %amount_to_make
   set %amount #menures
   if #menures = 0
      gosub if_menures_is_zero
   set %amount #menures
   }
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXX SUB TO FIND AND GRAB RESOURCES XXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub grab_resources
gosub CheckPause
; Finds and grabs Resource #1
finditem %1 C_ , #backpackid
if #findstack < %minimum_required_resource1
   {
   menu set status Grabbing %resource1_name
   finditem %1 C_ , %resource_bag
   if #findstack < %amount_to_grab1
      {
      display ok You do not have enough %resource1_name in your resource bag$
              +Script is halting
      menu hide
      halt
      }
   exevent drag #findid %amount_to_grab1
    wait 10
   exevent dropc #backpackid
    wait 15
   }

; Finds and grabs Resource #2
if %2 = N/A
   return
finditem %2 C_ , #backpackid
if #findstack < %minimum_required_resource2
   {
   menu set status Grabbing %resource2_name
   finditem %2 C_ , %resource_bag
   if #findstack < %amount_to_grab2
      {
      display ok You do not have enough %resource2_name in your resource bag$
              +Script is halting
      menu hide
      halt
      }
   exevent drag #findid %amount_to_grab2
    wait 10
   exevent dropc #backpackid
    wait 15
   }

 ; Finds and grabs Resource #3
if %3 = N/A
   return
finditem %3 C_ , #backpackid
if #findstack < %minimum_required_resource3
   {
   menu set status Grabbing %resource3_name
   finditem %3 C_ , %resource_bag
   if #findstack < %amount_to_grab3
      {
      display ok You do not have enough %resource3_name in your resource bag$
              +Script is halting
      menu hide
      halt
      }
   exevent drag #findid %amount_to_grab3
    wait 10
   exevent dropc #backpackid
    wait 15
   }
 ; Finds and grabs Resource #4
if %4 = N/A
   return
finditem %4 C_ , #backpackid
if #findstack < %minimum_required_resource2
   {
   menu set status Grabbing %resource4_name
   finditem %4 C_ , %resource_bag
   if #findstack < %amount_to_grab4
      {
      display ok You do not have enough %resource4_name in your resource bag$
              +Script is halting
      menu hide
      halt
      }
   exevent drag #findid %amount_to_grab4
    wait 10
   exevent dropc #backpackid
    wait 15
   }
 ; Finds and grabs Resource #5
if %5 = N/A
   return
finditem %5 C_ , #backpackid
if #findstack < %minimum_required_resource2
   {
   menu set status Grabbing %resource5_name
   finditem %5 C_ , %resource_bag
   if #findstack < %amount_to_grab5
      {
      display ok You do not have enough %resource5_name in your resource bag$
              +Script is halting
      menu hide
      halt
      }
   exevent drag #findid %amount_to_grab5
    wait 10
   exevent dropc #backpackid
    wait 15
   }
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXXXXX SUB FOR MAKING THE POTIONS XXXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub craft_item
set %makefirst #TRUE
  _looppot:
  gosub CheckPause
  menu set status Crafting %item_name
  gosub check_resources %resource1 %resource2 %resource3 %resource4
  finditem %alch_tool C_ , #backpackid
  if #findcnt < 1
     {
     menu set status Out of Tools!
      wait 5
     gosub check_tool %required_tool %required_tools_needed %required_tool_catposy %required_tool_selposy
     finditem %alch_tool C_ , #backpackid
     }
  set #lobjectid #findid
  if %makefirst = #TRUE
     {
     event macro 17 0
       gosub gumpwait
     set %x #contposx + %catposx
     set %y #contposy + %catposy
     click %x %y ; Category
       gosub gumpwait
     set %x #contposx + %selposx
     set %y #contposy + %selposy
     click %x %y ; Selection (Potion)
       gosub gumpwait
     set %makefirst #FALSE
     }
   else ; Make Last
     {
     set %x #contposx + 282
     set %y #contposy + 410
     click %x %y ; Category
       gosub gumpwait
     }
set %finished_count ( %finished_count + 1 )
;set %check_finished %finished_count
;finditem %finished_items C_ , #backpackid
;wait 1
;set %finished_count #findstack
menu set %currently_crafting %finished_count
;if %check_finished = %finished_count
; set %finished_count #findstack
if %finished_count >= %amount
 gosub set_finished_item
else
 goto _looppot
return

; ###########################################################################
; ##########     Checks for resources and refills if needed   ###############
; ###########################################################################
sub check_resources
finditem %1 C_ , #backpackid
if #findstack < %minimum_required_resource1
   {
   gosub clear_finished
   gosub grab_resources %1
   }
if %2 = N/A
   return
finditem %2 C_ , #backpackid
if #findstack < %minimum_required_resource2
   gosub grab_resources %2
if %3 = N/A
   return
finditem %3 C_ , #backpackid
if #findstack < %minimum_required_resource3
   gosub grab_resources %3
if %4 = N/A
   return
finditem %4 C_ , #backpackid
if #findstack < %minimum_required_resource4
   gosub grab_resources %4
if %5 = N/A
   return
finditem %5 C_ , #backpackid
if #findstack < %minimum_required_resource4
   gosub grab_resources %5
return

; ############################################################################
; ##########     Clears finished potions out of the backpack   ###############
; ############################################################################
sub clear_finished
gosub CheckPause
menu set status Clearing pack of finished items...
finditem %finished_items C_ , #backpackid
if #findcnt < 1
   return
set %old_count ( #findstack + %old_count )

finditem %finished_items C_ , #backpackid
exevent drag #findid #findstack
 wait 10
exevent dropc %dump_bag
 wait 15

return

; ##############################################################################
; ##########     Sets the Finished Potion as Done and moves on   ###############
; ##############################################################################
sub set_finished_item
   menu delete %amount_to_make
   menu font color red
   menu font bgcolor silver
   menu font size 10
   menu text done 175 %size2 Done!
   menu set status Finished with %potion_type
   set %item_to_make ( %item_to_make + 1 )
   wait 5
   gosub clear_pack %resources
   gosub clear_finished
   set %size2 ( %size2 + 25 )
   if %amount_to_make = %finished
      gosub finished
   set %amount_to_make ( %amount_to_make + 1 )
   set %currently_crafting ( %currently_crafting + 1 )
   set %old_count 0
   set %makefirst #TRUE
return
; #######################################################################
; ##########     SUB TO COMBINE AND MAKE SCRIPT SMALLER   ###############
; #######################################################################
sub if_menures_is_zero
menu delete %amount_to_make
menu font color red
menu font bgcolor silver
menu text done 175 %size2 Done!
set %size2 ( %size2 + 25 )
set %item_to_make ( %item_to_make + 1 )
if %amount_to_make = %finished
   gosub finished
set %amount_to_make ( %amount_to_make + 1 )
set %currently_crafting ( %currently_crafting + 1 )
gosub potion_init
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXXXXXXXXXX FINISHED MESSAGE XXXXXXXXXXXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub finished
menu set status Script is Finished!
display ok Script has finished working!$
        +All potions should now be crafted and put away.$
        +$
        +Thank you for using Guadah's Alchimest Assistant.$
        +$
        +Script is Halting$
menu hide
halt
return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXXX GUMP WAIT SUB, ENSURES GUMP IS UP XXXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub gumpwait
set %timer #SCNT + 5
 repeat
  until #CONTNAME = generic_gump || #SCNT > %timer
if #contname <> generic_gump
   {
   event macro 17 0
   wait 1s
   }
Return
; #################################################
; ########            PAUSE SUB            ########
; #################################################
Sub CheckPause
if #menubutton = pause
   {
   set #menubutton N/A
   menu delete pause
   menu font color maroon
   menu font bgcolor silver
   menu button resume 230 10 50 25 Resume
   menu set status Paused!  Click "Resume" when ready.
   gosub PauseLoop
   }
if #menubutton = quit
   {
   display ok Thank you for using Guadah's Alchemist Assistant.$
          +$
          +Script is Halting$
   menu hide
   halt
   }
return

Sub PauseLoop
Pause_Loop:
If #menubutton = resume
   {
   set #menubutton N/A
   Menu Delete Resume
   Menu Font Color Black
   menu font bgcolor silver
   Menu Button Pause 230 10 50 25 Pause
   Return
   }
if #menuButton = quit
   {
   Display ok Thank you for using Guadah's Alchemist Assistant.$
            +$
            +Script is Halting$
   Menu Hide
   Halt
   }
Goto Pause_Loop
Return
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; XXX TRAILMYX'S PERSISTANT VARIABLE SUBS XXX
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
sub TM_GetPersistantVariable
  set #RESULT %1 , _ , #CHARID
  set #RESULT * . #RESULT
return #RESULT
sub TM_SetPersistantVariable
  set #RESULT %1 , _ , #CHARID
  set * . #RESULT %2
return

; FIN