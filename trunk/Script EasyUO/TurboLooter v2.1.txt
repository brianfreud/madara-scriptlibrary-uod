;==================================
; Script Name: TURBOLOOTER
; Author: Tony
; Version: 2.1
; Client Tested with: 6.0.13
; EUO version tested with: 1.5 v152
; Shard OSI / FS: FS
; Revision Date: 01/09/09
; Public Release: 10/05/09
; Purpose: Loots corpses, saves previous preferences in system registry
; v1.3 adds: Avoid criminal status if choosen.
; All blade-type types added.
; Dragon scales, leather and wool types added.
; Double hotkey.
; General fixes.
; v1.4 adds: More checks on status bar
; Option to avoid the script to close the corpses after loot
; Option to enable auto looting
; v2.0 adds: Plain bones
; Jewels
; Optimized leather gathering
; Optimized weight handling
; Various stuff
;
;==================================
set %title Turbo , #spc , Looter , #spc , - , #spc , v2.0 , #spc , by , #spc , Tony , #spc , for , #spc , UODreams , #spc , - , #spc , Last , #spc , update , #spc , 10/05/09
gosub readRegistry
gosub createMenu
gosub acquireTypes
set %h %P23
set #lpc 211
while #menubutton <> start
{
if #menubutton = CLOSED
halt
menu get h23
if %h <> #menures
{
if #menures = #true
{
menu delete hotkey1
menu delete hotkey2
menu delete titleh
}
else
{
menu Font Style
menu edit hotkey1 8 340 40 *TURBO_LOOTER_HOTKEY1
menu edit hotkey2 41 340 20 *TURBO_LOOTER_HOTKEY2
menu text titleh 65 343 HotKey
}
}
set %h #menures
wait 1
}
menu Hide
gosub writeRegistry
gosub getPreferences
gosub getRace
gosub openStatusBar
gosub hasScissors
while #true
{
if ! %auto_looting
{
onhotkey %hotkey2 %hotkey1
set %hotkey_pressed #true
}
if %auto_looting || %hotkey_pressed
{
set %hotkey_pressed #false
finditem YFM_PNF_JNF_QNF_YNF_WNF_KNF_MNF_YNF_ G_2
if #findkind = -1
{
if ! %auto_looting
sound
}
else
{
ignoreitem #findid
gosub openStatusBar
if #weight < ( %totalweight - 10 )
gosub loot
else
event exmsg #charid 3 30 Too Weight!
}
}
wait 1
}
stop

sub loot
set #lobjectid #findid
set %corpse #findid
ignoreitem #findid
event macro 17 0
set %init_loot #scnt
set %init_journal #jindex
while #contsize <> 144_212
{
set %jindex #jindex
event macro 17 0
wait 7
while %jindex < #jindex
{
set %jindex %jindex + 1
scanjournal %jindex
if You_did_not_earn in #journal 3
deletejournal
ignoreitem %corpse
return
}
if #scnt - %init_loot > 2 2
ignoreitem %corpse
return
}
wait 5
set %end_journal #jindex
if %ignore_criminal_flagment = #false
{
for %i %init_journal %end_journal
{
scanjournal %i
if criminal in #journal
{
event sysmessage To avoid a criminal action this corpse will be skipped.
if #contsize = 144_212
click %closex %closey f r dmc
deletejournal
return
}
}
}
;wait 10
if JJG in %to_loot || OFF in %to_loot || VLK in %to_loot || STO in %to_loot ;resources looting
{

finditem %lame C_ , #backpackid
if #findkind = -1
finditem %lame C_ , #charid
if #findkind = -1
event sysmessage No bladed items were found in your backpack.
else
{
set #lobjectid #findid
event macro 17 0
target 2s
set #ltargetid %corpse
set #ltargetkind 1
event macro 22 0
finditem JJG_OFF_VLK C_ , %corpse
set %find_init #scnt
while #findkind = -1
{
wait 1
finditem JJG_OFF_VLK C_ , %corpse
if ( #scnt - %find_init > 1 ) || useful_to_carve in #journal
break
}
}
}
set %stuff %rawhide , _ , %random_bones
finditem %to_loot C_ , %corpse
if #findkind <> -1
{
for #findindex 1 #findcnt
{
set %w #weight
exevent drag #findid #findstack
wait 8
gosub openStatusBar
set %hidemod 0
if #findtype = DEG && %hasscissors && %woverride
{
set %a %w + #findstack
set %b %totalweight - 10
if %a < %b
set %hidemod 1000
}
if #weight > ( %totalweight - 10 + %hidemod )
{
exevent dropc %corpse
wait 10
gosub getStones #findid #findstack #findtype
set %max ( %totalweight - %w - 10 ) / #result
if %max > 0
{
event exmsg #charid 3 30 Almost overweight!
exevent drag #findid %max
wait 8
exevent dropc #backpackid
wait 8
}
else
event exmsg #charid 3 30 Too Weight!
}
else
exevent dropc #backpackid
if #findtype in %stuff
gosub cutItem #findid
wait 8
}
}
finditem %stuff C_ , #backpackid
if #findkind <> -1
{
for #findindex 1 #findcnt
{
gosub cutItem #findid
}
}
event exmsg #charid 3 30 Looted Everything!
ignoreitem %corpse
if %auto_close_corpse
{
set %closex 70 + #contposx
set %closey 110 + #contposy
if #contsize = 144_212 &&
click %closex %closey f r dmc
}
return

sub writeRegistry
for %i 1 26
{
set %my_var h . %i
menu get %my_var
if #menures = #false
set *TURBO_LOOTER_P . %i #false
else
set *TURBO_LOOTER_P . %i #true
}
menu get hotkey1
set *TURBO_LOOTER_HOTKEY1 #menures
set %hotkey1 #menures
menu get hotkey2
set *TURBO_LOOTER_HOTKEY2 #menures
set %hotkey2 #menures

for %j 1 21
{
set %my_var q . %j
menu get %my_var
if #menures = N/A
set *TURBO_LOOTER_Q . %j 0
else
set *TURBO_LOOTER_Q . %j #menures
}
return

sub readRegistry
for %i 1 26
{
set %my_var *TURBO_LOOTER_P . %i
if %my_var = N/A || %my_var = #false
set %P . %i #false
else
set %P . %i #true
}
if *TURBO_LOOTER_HOTKEY1 = N/A
set *TURBO_LOOTER_HOTKEY1 SHIFT
if *TURBO_LOOTER_HOTKEY2 = N/A
set *TURBO_LOOTER_HOTKEY2 A

for %j 1 21
{
set %my_var *TURBO_LOOTER_Q . %j
if %my_var = N/A
set %Q . %j 0
else
set %Q . %j %my_var
}
return

sub CreateMenu
menu clear
set #MenuButton N/A
menu Font BGColor white
menu font color black
menu window color white
menu Window Crea Sacchette
set %x 100
set %y 100
menu Window Size 355 365
menu Font Size 8
menu Show %x %y
;POTIONS
menu Font Style b
menu text title0 2 0 %title
menu text title1 5 20 Potions
menu Font Style
menu check h1 8 40 120 15 %P1 Heal
menu check h2 8 60 120 15 %P2 Cure
menu check h3 8 80 120 15 %P3 Agility
menu check h4 8 100 120 15 %P4 Refresh
menu check h5 8 120 120 15 %P5 Poison
menu check h6 8 140 120 15 %P6 Strength
menu check h7 8 160 120 15 %P7 Explosion

;REAGENTS
menu Font Style b
menu text title2 5 180 Reagents
menu Font Style
menu check h8 8 200 120 15 %P8 Mage
menu check h9 8 220 120 15 %P9 Necromancer

;MISC
menu Font Style b
menu text title3 175 20 Miscellaneous
menu Font Style
menu check h10 178 40 120 15 %P10 Bandages
menu check h11 178 60 120 15 %P11 Bolas
menu check h12 178 80 120 15 %P12 Magic Apple
menu check h13 178 100 120 15 %P13 Arrows/Bolts
menu check h14 178 120 120 15 %P14 Teleport Scroll
menu check h15 178 140 120 15 %P15 Gold
menu check h16 178 160 120 15 %P16 Doom Bones
menu check h17 178 180 120 15 %P17 Feathers
menu check h18 178 200 120 15 %P18 Leather
menu check h19 178 220 120 15 %P19 Wool
menu check h20 178 240 120 15 %P20 Dragon Scales
menu check h24 178 260 120 15 %P24 Plain Bones
menu check h25 178 280 120 15 %P25 Jewels
;OPTIONS
menu Font Style b
menu text title4 5 240 Options
menu Font Style
menu check h21 8 260 120 15 %P21 Ignore Criminal Flag
menu check h22 8 280 120 15 %P22 Close Corpses
menu check h23 8 320 120 15 %P23 Enable Autolooting
menu check h26 8 300 120 15 %P26 Weight override
if ! %P23
{
menu edit hotkey1 8 340 40 *TURBO_LOOTER_HOTKEY1
menu edit hotkey2 41 340 20 *TURBO_LOOTER_HOTKEY2
menu text titleh 65 343 HotKey
}
menu Font Style b
menu Button start 178 340 122 20 Start

if #false
{
menu text mstatic1 136 20 MAX
menu Edit q1 130 36 40 %Q1
menu Edit q2 130 56 40 %Q2
menu Edit q3 130 76 40 %Q3
menu Edit q4 130 96 40 %Q4
menu Edit q5 130 116 40 %Q5
menu Edit q6 130 136 40 %Q6
menu Edit q7 130 156 40 %Q7

menu Edit q8 130 196 40 %Q8
menu Edit q9 130 216 40 %Q9

menu text mstatic2 306 20 MAX
menu Edit q10 300 36 40 %Q10
menu Edit q11 300 56 40 %Q11
menu Edit q12 300 76 40 %Q12
menu Edit q13 300 96 40 %Q13
menu Edit q14 300 116 40 %Q14
menu Edit q15 300 136 40 %Q15
menu Edit q16 300 156 40 %Q16
menu Edit q17 300 176 40 %Q17
menu Edit q18 300 196 40 %Q18
menu Edit q19 300 216 40 %Q19
menu Edit q20 300 236 40 %Q20
menu Edit q21 300 256 40 %Q21

}

return

sub getPreferences
set %to_loot #spc

menu get h1
if #menures = #true
set %to_loot %to_loot , _ , %heal
menu get h2
if #menures = #true
set %to_loot %to_loot , _ , %cure
menu get h3
if #menures = #true
set %to_loot %to_loot , _ , %agility
menu get h4
if #menures = #true
set %to_loot %to_loot , _ , %refresh
menu get h5
if #menures = #true
set %to_loot %to_loot , _ , %poison
menu get h6
if #menures = #true
set %to_loot %to_loot , _ , %strength
menu get h7
if #menures = #true
set %to_loot %to_loot , _ , %explosion
menu get h8
if #menures = #true
set %to_loot %to_loot , _ , %reagenti_mago
menu get h9
if #menures = #true
set %to_loot %to_loot , _ , %reagenti_necro
menu get h10
if #menures = #true
set %to_loot %to_loot , _ , %bende
menu get h11
if #menures = #true
set %to_loot %to_loot , _ , %bolas
menu get h12
if #menures = #true
set %to_loot %to_loot , _ , %melette
menu get h13
if #menures = #true
set %to_loot %to_loot , _ , %frecce
menu get h14
if #menures = #true
set %to_loot %to_loot , _ , %scroll
menu get h15
if #menures = #true
set %to_loot %to_loot , _ , %gold
menu get h16
if #menures = #true
set %to_loot %to_loot , _ , %daemon_bones
menu get h17
if #menures = #true
set %to_loot %to_loot , _ , %feathers
menu get h18
if #menures = #true
set %to_loot %to_loot , _ , %rawhide , _ , %leather
menu get h19
if #menures = #true
set %to_loot %to_loot , _ , %wool
menu get h20
if #menures = #true
set %to_loot %to_loot , _ , %scales
;display ok %to_loot
menu get h21
if #menures = #true
set %ignore_criminal_flagment #true
menu get h22
if #menures = #true
set %auto_close_corpse #true
menu get h23
if #menures = #true
set %auto_looting #true
menu get h24
if #menures = #true
set %to_loot %to_loot , _ , %random_bones , _ , %bones
menu get h25
if #menures = #true
set %to_loot %to_loot , _ , %jewels
menu get h26
if #menures = #true
set %woverride #true
return

sub acquireTypes
;POZIONI
set %agility YUF
set %cure NUF
set %refresh ZUF
set %heal UUF
set %poison AVF
set %strength XUF
set %explosion TUF

;REAGENTI
set %reagenti_mago RZF_SZF_WZF_MZF_JZF_JUF_KUF_KZF
set %reagenti_necro TZF_IUF_UZF_DUF_YZF

;MISC
set %bende ZLF
set %bolas KTO
set %melette CBS
set %frecce RWF_LNK
set %scroll UTL
set %gold POF
set %daemon_bones OZF
set %feathers VLK
set %leather JJG
set %wool OFF
set %rawhide DEG
set %scissors KAG
set %scales STO
set %random_bones YIK_EJK_SJK_HJK_AJK_DJK_HIK_KJK_ZIK_XIK_BJK
set %bones GUF
SET %jewels HVF_UVF_FVF_EVF_OVF_VUF_GVF_RVF_BVF_VVF_NVF_ZVF

;BLADED ITEMS
set %lame YPO_BNF_CPH_INF_FSF_OSF_ASF_HNF_FMH_CNF_LSF_NSF_ISF_SMH_RMH_CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_SFR_
set %lame %lame , CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_JDP_EDP_YCP_KDP_
set %lame %lame , RFR_NFR_SFR_OFR_EFR_YFR_XFR_DFR_HFR_
return

sub getRace
finditem #charid
if #findtype in IS_HS
set %totalweight #maxweight + 60
else
set %totalweight #maxweight
return

sub openStatusBar
while #charname = N/A
{
set #nextcposx 800
set #nextcposy 300
event macro 8 2
wait 1s
}
return

sub globalCount
if %0 < 2
%gwhere = #backpackid
else
%gwhere = %2
finditem %1 C_ , %gwhere
set %tot 0
if #findkind <> -1
{
for #findindex 1 #findcnt
{
set %tot %tot + #findstack
}
}
return %tot

sub cutItem
set #lobjectid %scissorsid
event macro 17 0
target 2s
set #ltargetid %1
set #ltargetkind 1
event macro 22 0
wait 10
return

sub hasScissors
finditem %scissors C_ , #backpackid
if #findkind = -1 2
event sysmessage No scissors were found in your backpack, will take lesser hides!
set %hasscissors #false
else
{
set %hasscissors #true
set %scissorsid #findid
}
return

sub getStones
set %id %1
set %amount %2
set %type %3
if %type = DEG && %woverride
return 1
event property %id
str pos #property Weight:
set %init #strres + 8
str pos #property Stones
set %end #strres - 1
set %len %end - %init
str mid #property %init %len
set %stones #strres
set %unitweight %stones / %amount
if %unitweight < 1
set %unitweight 1
;display ok %stones %unitweight
return %unitweight