;===================================================
; Script Name: s7UOBodExporter
; Author: snicker7
; Version: 1.2a
; Client Tested with: 4.0.11b
; EUO version tested with:  1.5 build 0051
; Shard OSI / FS:  OSI / FS
; Revision Date:  8/16/05
; Public Release:  7/18/05
; Global Variables Used: N/A
; Purpose:  Exports all BODs in selected BOD books to a format importable by the program UOBod
;====================================================
;/CH4NG3L0G/////////////////////////////////////////=
;====================================================
; 082605: v1.2a:
;	*mmorning reporting problems with cmd window
;	 still hanging over too long causing OCR
;	 problems. Added 4 new pixel checks to 
;	 s7CheckGumpVisibility.
;	*s7CheckPixelVisibility had no timeout and
;	 would very very rarely get hung up while
;	 waiting with no alert on.
; 081605: v1.2:
;	*fixed the LBOD/SBOD mix problem. Turns out I
;	 had already fixed it but reversed the order of
;	 my sub calls and it wasn't working.
;	*simple fix accounting for people running the
;	 retarded bubble windows xp style as it was 
;	 covering part of the BOD book gump and trigged
;	 a visibility checker until you moved it
;	 yourself.
;	*menu now says the name of the BOD it is
;	 scanning
;	*fixed an error due to BOD books not opening if
;	 you already had one open when the script
;	 starts
;	*removed an illegal character check from Kal's
;	 file writing subs, it wasn't needed and a 
;	 significant speed increase was gained!
; 080805: v1.1a:
;	*fixed a problem causing platemail large bods
;	 to show up as "studded set" on certain
;	 shards, thanks go to menoka for the bug
;	 report
; 072605: v1.1:
;	*fixed a check on the filter scan for the
;	 wrong contsize for bod books in containers
;	*wrote and implemented s7CheckPixelVisibility
;	 and s7CheckGumpVisibility, which is a
;	 wrapper for s7CPV which scans 5 spots on the
;	 currently open gump, making sure the window
;	 is visible. Should correct problems on
;	 slower computers where the file writing cmd
;	 window was hanging around too long, and
;	 causing pixel scan errors.
;	*fancied up the code and organized everything
; 071805: v1.0b
;	*official release, added to PSL.
; 071705: v1.0b
;	*designed a filter checker to automatically
;	 remove the filters from bod books before
;	 attempting to scan them.
;	*inserted a version check sub to ensure users
;	 are running EUO1.5.
;	*fixed major error in #cont scanning page
;	 turning subroutine that would sometimes 
;	 completely skip whole books except the first
;	 page.
; 041905: v1.0a
;	*first official working release, not released
;	 to public.
;====================================================
;/SCR1P7 B3G1N//////////////////////////////////////=
;====================================================
set %bodbooktype DYM
set %debug #false

gosub initMenu
gosub checkVersion
menuloop:
if #menubutton = btnAddBook 2
	set #menubutton N/A
	gosub AddBook
if #menubutton = btnDelBook 2
	set #menubutton N/A
	gosub DelBook
if #menubutton = btnClrBook 2
	set #menubutton N/A
	gosub ClrBook
if #menubutton = btnAddAll 2
	set #menubutton N/A
	gosub AllBooks
menu get chkLbod
if #menures && ! %ExpLBods
{
	set %ExpLBods #true
	menu edit txtLbodFile 18 62 112 LBoDs , #date , #dot , txt
	menu delete status
	menu text status %statusx %statusy Enter filename to save$large bod data in.
}
if ! #menures && %ExpLBods
{
	set %ExpLBods #false
	menu delete txtLbodFile
}
menu get chkSbod
if #menures && ! %ExpSBods
{
	set %ExpSBods #true
	menu edit txtSbodFile 18 105 112 SBoDs , #date , #dot , txt
	menu delete status
	menu text status %statusx %statusy Enter filename to save$small bod data in.
}
if ! #menures && %ExpSBods
{
	set %ExpSBods #false
	menu delete txtSbodFile
}
if ! %startButtonDisp && %bodBookCount > 0 && ( %ExpSBods || %ExpLBods )
{
	set %startButtonDisp #true
	menu button btnStart 2 162 60 20 Start
}
if %startButtonDisp && ( %bodBookCount = 0 || ( ! %ExpSBods && ! %ExpLBods ) )
{
	set %startButtonDisp #false
	menu delete btnStart
}
if #menubutton = btnStart
{
	set #menubutton N/A
	if %ExpSBods 2
		menu get txtSbodFile
		set %SbodFilename #menures
	if %ExpLBods 2
		menu get txtLbodFile
		set %LBodFilename #menures
	goto loop
}
wait 1
goto menuloop

loop:
gosub initScanMenu
if %ExpLBods
{
	gosub Open 2 %LbodFilename
	gosub Wipe 2
}
if %ExpSBods
{
	gosub Open 1 %SbodFilename
	gosub Wipe 1
}
set %finishedBods 0
set %bodScanStart #systime
for %BBI 1 %bodBookCount
{
	if %bodBookDeedCount . %BBI < 1
		continue
	gosub OpenBodBook %bodBookID . %BBI
	menu set lblCbook Current book: %bodBookName . %BBI (book %BBI of %bodBookCount selected)
	set %menuposy #contposy + 485
	set %menuposx #contposx + 10
	menu show %menuposx %menuposy
	gosub CheckFilter
	for %BLoop 0 1
	{
		set %BLoop 0
		set %bodI 1
		while %bodI <= 10
		{
			set #lpc 250
			set %currbod %finishedbods + 1
			gosub s7CheckGumpVisibility #true
			call kalocr.txt getBodInfo %bodI #contposx #contposy
			if !gbi_count = N/A
				break
			gosub Size
			gosub Exceptions
			if ( %size = lbod && %ExpLbods ) || ( %size = sbod && %ExpSBods )
			{
				gosub Material
				set %bodstring %bodstring , !gbi_quality , #smc
				gosub LbodSet
				str pos !gbi_amount #spc , / , #spc
				set %pos #strRes - 1
				str left !gbi_amount %pos
				set %filled #strRes
				set %pos %pos + 4
				str mid !gbi_amount %pos 2
				set %amount #strRes
				if %size = sbod
				{
					set %FileNumber 1
					gosub Parts
					menu set status Scanning BOD %currbod ( , %parts , )
					set %bodstring %bodstring , %amount , #smc , %filled , #smc
					gosub ToolCat
				}
				if %size = lbod
				{
					set %FileNumber 2
					set %bodstring %bodstring , %amount , #smc
					gosub Parts
					menu set status Scanning BOD %currbod ( , %lbod LBOD)
				}
				str len !gbi_price
				if #strRes = 0
				set !gbi_price 0
				set %bodstring %bodstring , !gbi_price , #smc , %bodbookName . %BBI , #spc , ( , %bodBookID . %bbi , )
				gosub Write %FileNumber %bodstring
			}
			if !gbi_count = 0
				set !gbi_count 1
			set %bodI %bodI + !gbi_count
			set %finishedBods %finishedBods + 1
			set %bodRm %totalDeedCount - %finishedBods
			menu set lblBRm BODs remaining: %bodRm
			gosub SetETA
			gosub setProgressBar
		}
		set %_cx #contposx + 240
		set %_cy #contposy + 430
		savepix %_cx %_cy 1
		if #pixcol = 526344
		{
			menu set status Turning page...
			click %_cx %_cy
			gosub waitForPageTurn
		}
		if #pixcol <> 526344
		{
			set %_cx #contposx + 30
			set %_cy #contposy + 30
			click %_cx %_cy r
			set %BLoop 1
		}
		gosub setProgressBar
		set #lpc 50
	}
}
if %expSBods
{
	gosub dump 1
	menu set status Writing SBOD file...
}
if %expLBods
{
	gosub dump 2
	menu set status Writing LBOD file...
}
set %totalTime ( #systime - %bodScanStart ) / 1000
set %totaltime_m %totaltime / 60
set %totaltime_s %totaltime % 60
menu set status Done in %totaltime_m minutes and %totalTime_s seconds!!
if %bodRM > 0
	menu set lblCBook %bodRM errors.
if %bodRM = 0
	menu delete lblCBook
halt

;====================================================
;/SUBR0U71N3S///////////////////////////////////////=
;====================================================

;====================================================
;/B0D B00K SUBR0U71N3S//////////////////////////////=
;====================================================

sub OpenBodBook
	set #lobjectid %1
	menu set status Opening BOD book " , %bodBookName . %bbi , "...
	for %obb 0 1
	{
		if #contsize = 615_454 || #contsize = 531_454
		{
			set %obb 0
      set %_cx #contposx + 30
			set %_cy #contposy + 30
			click %_cx %_cy r
		}
	}
	gosub s7WaitForAction 13
	event macro 17 0
	set %obbTimeout #scnt
	for %OBB 0 1
	{
		set %obb 0
		if %obbTimeout + 4 < #scnt
			return #false
		if #contsize = 615_454 || #contsize = 531_454
			set %obb 1
	}
	wait 10
	;contpos 50 50
return #true

sub CheckFilter
	set %_sx #contposx + 532
	set %_sy #contposy + 36
	if #contsize = 531_454 2
		set %_sx %_sx - 84
	gosub s7CheckPixelVisibility #true %_sx %_sy
	savepix %_sx %_sy 1
	if #pixcol <> 16745604
	{
		set %_cx #contposx + 50
		set %_cy #contposy + 44
		click %_cx %_cy
		wait 10
		set %_cx #contposx + 380
		set %_cy #contposy + 430
		click %_cx %_cy
		wait 10
		set %_cx #contposx + 520
		set %_cy #contposy + 430
		click %_cx %_cy
		wait 10
	}
return

sub waitForPage
	set %wfp 0
	for %wfpi 0 1
	{
		if ( #contsize = 531_454 || #contsize = 615_454 ) && %wfp <= 20
		{
			wait 1
			gosub SetETA
			set %wfp %wfp + 1
			set %wfpi 0
		}
	}
	if #contsize = 615_454 || #contsize = 531_454
		return #false
	for %wfp 0 1
	{
		if #contsize <> 615_454 && #contsize <> 531_454 3
			gosub SetETA
			set %wfp 0
			wait 1
	}
	set %wfpT #scnt2
	for %wfp 0 1
	{
		wait 1
		gosub setETA
		if %wfpt + 5 < #scnt2
			set %wfp 0
	}
return #true

sub waitForPageTurn
	for %wfp 0 1
	{
		if #contsize <> 615_454 && #contsize <> 531_454 3
			gosub SetETA
			set %wfp 0
			wait 1
	}
	set %wfpT #scnt2
	for %wfp 0 1
	{
		wait 1
		gosub setETA
		if %wfpt + 5 < #scnt2
			set %wfp 0
	}
return

;====================================================
;/P4RS1NG SUBR0U71N3S///////////////////////////////=
;====================================================

sub ToolCat
	if platemail in !gbi_item1
		set %toolcat Platemail
	if chainmail in !gbi_item1
		set %toolcat Chainmail
	if ringmail in !gbi_item1
		set %toolcat Ringmail
	if helm in !gbi_item1 || bascinet in !gbi_item1
		set %toolcat Helmets
	if shield in !gbi_item1 || buckler in !gbi_item1
		set %toolcat Shields
	if axe in !gbi_item1
		set %toolcat Axes
	if sword in !gbi_item1 || cutlass in !gbi_item1 || dagger in !gbi_item1 || scimitar in !gbi_item1 || katana in !gbi_item1 || kryss in !gbi_item1
		set %toolcat Bladed
	if mace in !gbi_item1 || hammer in !gbi_item1 || maul in !gbi_item1
		set %toolcat Bashing
	if bard in !gbi_item1 || halb in !gbi_item1 || war , #spc , f in !gbi_item1
		set %toolcat Polearms
	if skirt notin !gbi_item1 && shorts notin !gbi_item1 && bustier notin !gbi_item1 && leather in !gbi_item1
		set %toolcat Leather , #spc , Armor
	if bustier notin !gbi_item1 && armor notin !gbi_item1 && studded in !gbi_item1
		set %toolcat Studded , #spc , Armor
	if bone notin !gbi_item1 && ( shorts in !gbi_item1 || ( skirt in !gbi_item1 && !gbi_material <> cloth ) || armor in !gbi_item1 || bustier in !gbi_item1 )
		set %toolcat Female , #spc , Armor
	if bone in !gbi_item1
		set %toolcat Bone , #spc , Armor
	if shoes in !gbi_item1 || boots in !gbi_item1 || sandals in !gbi_item1
		set %toolcat Footwear
	if hat in !gbi_item1 || ( cap in !gbi_item1 && !gbi_material = cloth ) || bandana in !gbi_item1 || bonnet in !gbi_item1
		set %toolcat Hats
	if shirt in !gbi_item1 || dress in !gbi_item1 || ( tunic in !gbi_item1 && !gbi_material = cloth ) || doublet in !gbi_item1 || cloak in !gbi_item1 || robe in !gbi_item1 || surcoat in !gbi_item1 || suit in !gbi_item1
		set %toolcat Shirts
	if pants in !gbi_item1 || kilt in !gbi_item1 || ( skirt in !gbi_item1 && !gbi_material = cloth )
		set %toolcat Pants
	if apron in !gbi_item1 || sash in !gbi_item1
		set %toolcat Miscellaneous
	set %bodstring %bodstring , %toolcat , #smc
return

sub LbodSet
	if ( female , #spc , plate in !gbi_item1 || bascinet in !gbi_item1 || shield in !gbi_item1 || buckler in !gbi_item1 ) || ( bone notin !gbi_item1 && plate notin !gbi_item1 && helm in !gbi_item1 )
	{
		set %bodstring %bodstring , #smc
		return
	}
	if %size = sbod
	{
		if leather notin !gbi_material && cloth notin !gbi_material
		{
			if cutlass in !gbi_item1 || katana in !gbi_item1 || scimitar in !gbi_item1 || broad in !gbi_item1 || longsword in !gbi_item1 || viking in !gbi_item1
				set %lbod Swords
			if kryss in !gbi_item1 || spear in !gbi_item1 || war , #spc , fork in !gbi_item1 || dagger in !gbi_item1
				set %lbod Piercing
			if axe in !gbi_item1 && war notin !gbi_item1
				set %lbod Axes
			if mace in !gbi_item1 || maul in !gbi_item1 || hammer in !gbi_item1 || war , #spc , axe in !gbi_item1
				set %lbod Bashing
			if bard in !gbi_item1 || halb in !gbi_item1
				set %lbod Polearms
			if chainmail in !gbi_item1
				set %lbod Chainmail
			if ringmail in !gbi_item1
				set %lbod Ringmail
			if plate in !gbi_item1
				set %lbod Platemail
		}
		if leather in !gbi_material || cloth in !gbi_material
		{
			if shoes in !gbi_item1 || boots in !gbi_item1 || sandals in !gbi_item1
				set %lbod Footwear , #spc , Set
			if female notin !gbi_item1 && skirt notin !gbi_item1 && shorts notin !gbi_item1 && leather in !gbi_item1
				set %lbod Leather , #spc , Set
			if bustier notin !gbi_item1 && studded in !gbi_item1
				set %lbod Studded , #spc , Set
			if female in !gbi_item1 || bustier in !gbi_item1 || ( skirt in !gbi_item1 && !gbi_material <> cloth ) || shorts in !gbi_item1 || studded , #spc , armor in !gbi_item1
				set %lbod Female , #spc , Set
			if bone in !gbi_item1
				set %lbod Bone , #spc , AR
			if bandana in !gbi_item1 || shirt in !gbi_item1 || ( skirt in !gbi_item1 && !gbi_material = cloth )
				set %lbod The , #spc , Gypsy
			if skullcap in !gbi_item1 || doublet in !gbi_item1 || kilt in !gbi_item1
				set %lbod The , #spc , Pirate
			if ( straw in !gbi_item1 && tall notin !gbi_item1 ) || ( tunic in !gbi_item1 && !gbi_material = cloth ) || long , #spc , pants in !gbi_item1
				set %lbod The , #spc , Farmer
			if wizard in !gbi_item1 || sash in !gbi_item1 || robe in !gbi_item1
				set %lbod The , #spc , Wizard
			if floppy in !gbi_item1 || plain , #spc , dress in !gbi_item1 || full , #spc , apron in !gbi_item1
				set %lbod Fishergirl
			if feathered in !gbi_item1 || surcoat in !gbi_item1 || fancy , #spc , shirt in !gbi_item1 || short , #spc , pants in !gbi_item1
				set %lbod Town , #spc , Crier
			if bonnet in !gbi_item1 || half , #spc , apron in !gbi_item1 || fancy , #spc , dress in !gbi_item1
				set %lbod The , #spc , Lady
			if jester in !gbi_item1 || cloak in !gbi_item1
				set %lbod The , #spc , Jester
			if tricorne in !gbi_item1 || ( skull notin !gbi_item1 && cap in !gbi_item1 && !gbi_material = cloth ) || tall , #spc , straw in !gbi_item1 || wide-brim in !gbi_item1
				set %lbod The , #spc , Hat-Set
		}
	}
	if %size = lbod
	{
		if cutlass in !gbi_item1 || katana in !gbi_item1 || scimitar in !gbi_item1 || broad in !gbi_item1 || longsword in !gbi_item1 || viking in !gbi_item1
			set %lbod Swords
		if kryss in !gbi_item1 || spear in !gbi_item1 || warfork in !gbi_item1 || dagger in !gbi_item1
			set %lbod Piercing
		if axe in !gbi_item1 && war notin !gbi_item1
			set %lbod Axes
		if mace in !gbi_item1 || maul in !gbi_item1 || hammer in !gbi_item1 || war , #spc , axe in !gbi_item1
			set %lbod Bashing
		if bardiche in !gbi_item1 || halberd in !gbi_item1
			set %lbod Polearms
		if chainmail in !gbi_item1
			set %lbod Chainmail
		if ringmail in !gbi_item1
			set %lbod Ringmail
		if gorget in !gbi_item1 && gloves in !gbi_item2
			set %lbod Studded , #spc , Set
		if plate in !gbi_item1 || plate in !gbi_item 
			set %lbod Platemail
		if sandals in !gbi_item1 && shoes in !gbi_item2
			set %lbod Footwear , #spc , Set
		if gorget in !gbi_item && !gbi_item1 = N/A 
			set %lbod Leather , #spc , Set
		if skirt in !gbi_item1 && bustier in !gbi_item2
			set %lbod Female , #spc , Set
		if bone in !gbi_item1
			set %lbod Bone , #spc , AR
		if bandana in !gbi_item1 && shirt in !gbi_item2
			set %lbod The , #spc , Gypsy
		if skullcap in !gbi_item1 && doublet in !gbi_item2
			set %lbod The , #spc , Pirate
		if straw in !gbi_item1 && tunic in !gbi_item2
			set %lbod The , #spc , Farmer
		if wizard in !gbi_item1 && sash in !gbi_item2
			set %lbod The , #spc , Wizard
		if floppy in !gbi_item1 && apron in !gbi_item2
			set %lbod Fishergirl
		if feathered in !gbi_item1 && surcoat in !gbi_item2
			set %lbod Town , #spc , Crier
		if bonnet in !gbi_item1 && apron in !gbi_item2
			set %lbod The , #spc , Lady
		if jester in !gbi_item1 && jester in !gbi_item2
			set %lbod The , #spc , Jester
		if tricorne in !gbi_item1 && cap in !gbi_item2
			set %lbod The , #spc , Hat-Set
	}
	set %bodstring %bodstring , %lbod , #smc
return

sub Exceptions
	if %size = lbod
	{
		if plate in !gbi_item1 || plate in !gbi_item
			set !gbi_count 6
		if gorget in !gbi_item && !gbi_item1 = N/A
			set !gbi_count 6	
	}
return

sub Parts
	if %size = lbod
	{
		if %lbod = Swords
			set %parts Cut , #smc , 0 , #smc , VSw , #smc , 0 , #smc , LSw , #smc , 0 , #smc , Sci , #smc , 0 , #smc , BSw , #smc , 0 , #smc , Kat , #smc , 0
		if %lbod = Piercing
			set %parts Dag , #smc , 0 , #smc , Kry , #smc , 0 , #smc , WFo , #smc , 0 , #smc , SSp , #smc , 0 , #smc , Spe , #smc , 0 , #smc , #smc , 0
		if %lbod = Axes
			set %parts LBA , #smc , 0 , #smc , DAx , #smc , 0 , #smc , BAx , #smc , 0 , #smc , THA , #smc , 0 , #smc , Axe , #smc , 0 , #smc , EAx , #smc , 0
		if %lbod = Bashing
			set %parts Mac , #smc , 0 , #smc , Mau , #smc , 0 , #smc , WMa , #smc , 0 , #smc , WHa , #smc , 0 , #smc , HPi , #smc , 0 , #smc , WAx , #smc , 0
		if %lbod = Polearms
			set %parts Bar , #smc , 0 , #smc , Hal , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = Chainmail
			set %parts Coi , #smc , 0 , #smc , Leg , #smc , 0 , #smc , Tun , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = Ringmail
			set %parts Glo , #smc , 0 , #smc , Sle , #smc , 0 , #smc , Leg , #smc , 0 , #smc , Tun , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = Platemail
			set %parts Gor , #smc , 0 , #smc , Glo , #smc , 0 , #smc , PHe , #smc , 0 , #smc , Arm , #smc , 0 , #smc , Leg , #smc , 0 , #smc , Tun , #smc , 0
		if %lbod = Footwear , #spc , Set
			set %parts San , #smc , 0 , #smc , Sho , #smc , 0 , #smc , Boo , #smc , 0 , #smc , ThB , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = Leather , #spc , Set
			set %parts Cap , #smc , 0 , #smc , Gor , #smc , 0 , #smc , Glo , #smc , 0 , #smc , Sle , #smc , 0 , #smc , Leg , #smc , 0 , #smc , Tun , #smc , 0
		if %lbod = Studded , #spc , Set
			set %parts Gor , #smc , 0 , #smc , Glo , #smc , 0 , #smc , Sle , #smc , 0 , #smc , Leg , #smc , 0 , #smc , Tun , #smc , 0 , #smc , #smc , 0
		if %lbod = Female , #spc , Set
			set %parts Ski , #smc , 0 , #smc , Bus , #smc , 0 , #smc , Sht , #smc , 0 , #smc , LAr , #smc , 0 , #smc , SBu , #smc , 0 , #smc , SAr , #smc , 0
		if %lbod = Bone , #spc , AR
			set %parts BHe , #smc , 0 , #smc , BGl , #smc , 0 , #smc , BAr , #smc , 0 , #smc , BLe , #smc , 0 , #smc , BAo , #smc , 0 , #smc , #smc , 0
		if %lbod = The , #spc , Gypsy
			set %parts Ban , #smc , 0 , #smc , Shi , #smc , 0 , #smc , Ski , #smc , 0 , #smc , ThB , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = The , #spc , Pirate
			set %parts SkC , #smc , 0 , #smc , Dou , #smc , 0 , #smc , Kil , #smc , 0 , #smc , Sho , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = The , #spc , Farmer
			set %parts StH , #smc , 0 , #smc , Tun , #smc , 0 , #smc , LoP , #smc , 0 , #smc , Boo , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = The , #spc , Wizard
			set %parts WiH , #smc , 0 , #smc , BoS , #smc , 0 , #smc , Rob , #smc , 0 , #smc , Boo , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = Fishergirl
			set %parts FlH , #smc , 0 , #smc , FuA , #smc , 0 , #smc , PlD , #smc , 0 , #smc , San , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = Town , #spc , Crier
			set %parts FeH , #smc , 0 , #smc , Sur , #smc , 0 , #smc , FaS , #smc , 0 , #smc , ShP , #smc , 0 , #smc , ThB , #smc , 0 , #smc , #smc , 0
		if %lbod = The , #spc , Lady
			set %parts Bon , #smc , 0 , #smc , HaA , #smc , 0 , #smc , FaD , #smc , 0 , #smc , San , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = The , #spc , Jester
			set %parts JeH , #smc , 0 , #smc , JeS , #smc , 0 , #smc , Clo , #smc , 0 , #smc , Sho , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
		if %lbod = The , #spc , Hat-Set
			set %parts TrH , #smc , 0 , #smc , Cap , #smc , 0 , #smc , WBH , #smc , 0 , #smc , TSH , #smc , 0 , #smc , #smc , 0 , #smc , #smc , 0
	}
	if %size = sbod
	{
		if !gbi_item1 = axe
			set %parts Axe
		if bandana in !gbi_item1
			set %parts Bandana
		if bardiche in !gbi_item1
			set %parts Bardiche
		if bascinet in !gbi_item1
			set %parts Bascinet
		if battle in !gbi_item1 && large notin !gbi_item1
			set %parts Battle , #spc , Axe
		if sash in !gbi_item1
			set %parts Body , #spc , Sash
		if sleeves in !gbi_item1
			set %parts Sleeves
		if gorget in !gbi_item1
			set %parts Gorget
		if gloves in !gbi_item1
			set %parts Gloves
		if arms in !gbi_item1
			set %parts Arms
		if legs in !gbi_item1
			set %parts Legs
		if tunic in !gbi_item1
			set %parts Tunic
		if leggings in !gbi_item1
			set %parts Leggings
		if bone in !gbi_item1
		{
			if armor in !gbi_item1
				set %parts Bone , #spc , Armor
			if arms in !gbi_item1
				set %parts Bone , #spc , Arms
			if gloves in !gbi_item1
				set %parts Bone , #spc , Gloves
			if helm in !gbi_item1
				set %parts Bone , #spc , Helmet
			if leg in !gbi_item1
				set %parts Bone , #spc , Leggings
		}
		if bonnet in !gbi_item1
			set %parts Bonnet
		if !gbi_item1 = boots
			set %parts Boots
		if broadsword in !gbi_item1
			set %parts Broadsword
		if bronze in !gbi_item1
			set %parts Bronze , #spc , Shield
		if buckler in !gbi_item1
			set %parts Buckler
		if !gbi_item1 = cap
			set %parts Cap
		if cloak in !gbi_item1
			set %parts Cloak
		if close in !gbi_item1
			set %parts Close , #spc , Helmet
		if coif in !gbi_item1
			set %parts Chainmail , #spc , Coif
		if cutlass in !gbi_item1
			set %parts Cutlass
		if !gbi_item1 = coif
			set %parts Coif
		if !gbi_item1 = dagger
			set %parts Dagger
		if double in !gbi_item1
			set %parts Double , #spc , Axe
		if doublet in !gbi_item1
			set %parts Doublet
		if Executioner in !gbi_item1
			set %parts Executioner's , #spc , Axe
		if fancy , #spc , d in !gbi_item1
			set %parts Fancy , #spc , Dress
		if fancy , #spc , s in !gbi_item1
			set %parts Fancy , #spc , Shirt
		if feathered in !gbi_item1
			set %parts Feathered , #spc , Hat
		if female , #spc , tu in !gbi_item1
			set %parts Female , #spc , Tunic
		if female , #spc , pl in !gbi_item1
			set %parts Female , #spc , Tunic
		if floppy in !gbi_item1
			set %parts Floppy , #spc , Hat
		if l , #spc , apron in !gbi_item1
			set %parts Full , #spc , Apron
		if halberd in !gbi_item1
			set %parts Halberd
		if f , #spc , apron in !gbi_item1
			set %parts Half , #spc , Apron
		if hammer , #spc , p in !gbi_item1
			set %parts Hammer , #spc , Pick
		if heater in !gbi_item1
			set %parts Heater , #spc , Shield
		if !gbi_item1 = helmet
			set %parts Helmet
		if jester , #spc , h in !gbi_item1
			set %parts Jester , #spc , Hat
		if jester , #spc , s in !gbi_item1
			set %parts Jester , #spc , Suit
		if katana in !gbi_item1
			set %parts Katana
		if kilt in !gbi_item1
			set %parts Kilt
		if kryss in !gbi_item1
			set %parts Kryss
		if large , #spc , battle in !gbi_item1
			set %parts Large , #spc , Battle , #spc , Axe
		if leather in !gbi_item1
		{
			if armor in !gbi_item1
				set %parts Leather , #spc , Armor
			if bustier in !gbi_item1
				set %parts Leather , #spc , Bustier
			if shorts in !gbi_item1
				set %parts Leather , #spc , Shorts
			if skirt in !gbi_item1
				set %parts Leather , #spc , Skirt
			if cap in !gbi_item1
				set %parts Leather , #spc , Cap
		}
		if long , #spc , p in !gbi_item1
			set %parts Long , #spc , Pants
		if longsword in !gbi_item1
			set %parts Longsword
		if !gbi_item1 = mace
			set %parts Mace
		if !gbi_item1 = maul
			set %parts Maul
		if metal , #spc , Kite in !gbi_item1
			set %parts Metal , #spc , Kite , #spc , Shield
		if metal , #spc , shield in !gbi_item1
			set %parts Metal , #spc , Shield
		if norse in !gbi_item1
			set %parts Norse , #spc , Helm
		if plain in !gbi_item1
			set %parts Plain , #spc , Dress
		if plate , #spc , h in !gbi_item1
			set %parts Plate , #spc , Helm
		if wide in !gbi_item1
			set %parts Wide-Brim , #spc , Hat
		if robe in !gbi_item1
			set %parts Robe
		if sandals in !gbi_item1
			set %parts Sandals
		if scimitar in !gbi_item1
			set %parts Scimitar
		if shirt in !gbi_item1 && fancy notin !gbi_item1
			set %parts Shirt
		if shoes in !gbi_item1
			set %parts Shoes
		if short , #spc , p in !gbi_item1
			set %parts Short , #spc , Pants
		if spear in !gbi_item1
			set %parts Spear
		if short , #spc , s in !gbi_item1
			set %parts Short , #spc , Spear
		if skirt in !gbi_item1 && !gbi_material = cloth
			set %parts Skirt
		if skullcap in !gbi_item1
			set %parts Skullcap
		if straw in !gbi_item1
			set %parts Straw , #spc , Hat
		if studded , #spc , ar in !gbi_item1
			set %parts Studded , #spc , female , #spc , Armor
		if tall , #spc , st in !gbi_item1
			set %parts Tall , #spc , Straw , #spc , Hat
		if studded , #spc , bust in !gbi_item1
			set %parts Studded , #spc , Bustier
		if studded , #spc , female in !gbi_item1
			set %parts Studded , #spc , female
		if surcoat in !gbi_item1
			set %parts Surcoat
		if tear , #spc , kite in !gbi_item1
			set %parts Tear , #spc , Kite , #spc , Shield
		if thigh in !gbi_item1
			set %parts Thigh , #spc , Boots
		if tricorne in !gbi_item1
			set %parts Tricorne , #spc , Hat
		if two , #spc , handed in !gbi_item1
			set %parts Two , #spc , Handed , #spc , Axe
		if viking in !gbi_item1
			set %parts Viking , #spc , Sword
		if war , #spc , a in !gbi_item1
			set %parts War , #spc , Axe
		if war , #spc , f in !gbi_item1
			set %parts War , #spc , Fork
		if war , #spc , h in !gbi_item1
			set %parts War , #spc , Hammer
		if war , #spc , m in !gbi_item1
			set %parts War , #spc , Mace
		if wizard in !gbi_item1
			set %parts Wizard's , #spc , Hat
	}
	set %bodstring %bodstring , %parts , #smc
return

sub Material
	if normal , #spc , leather in !gbi_material
		 set %material Plain , #spc , Leather
	if spined in !gbi_material
		 set %material Spined , #spc , Leather
	if horned in !gbi_material
		 set %material Horned , #spc , Leather
	if barbed in !gbi_material
		 set %material Barbed , #spc , Leather
	if cloth in !gbi_material
		 set %material Cloth
	if iron in !gbi_material
		 set %material Iron
	if dull in !gbi_material
		 set %material Dull
	if shadow in !gbi_material
		 set %material Shadow
	if dull notin !gbi_material && copper in !gbi_material
		 set %material Copper
	if bronze in !gbi_material
		 set %material Bronze
	if gold in !gbi_material
		 set %material Gold
	if agapite in !gbi_material
		 set %material Agapite
	if verite in !gbi_material
		 set %material Verite
	if valorite in !gbi_material
		 set %material Valorite
	set %bodstring %bodstring , %material , #smc
return

sub Size
	set %size SBod
	if !gbi_type <> small
		 set %size LBod
	set %bodstring %size , #smc
return

;====================================================
;/M3NU SUBR0U71N3S//////////////////////////////////=
;====================================================

sub initScanMenu
	menu clear
	menu window color black
	menu window size 600 120
	menu window title s7UOBodExporter Scanning...
	menu font transparent #true
	menu font name Verdana
	menu font color navy
	menu font size 10
	menu text Title 397 2 S N I C K E R 7 ' S
	menu text Title 417 16 U O B o d   E X P O R T E R .
	menu shape Title 394 -5 300 38 3 5 1 navy 2 0
	menu shape ProgressBarOutline 20 80 560 20 3 5 1 green 2 0
	menu font size 8
	menu font color green
	menu font bgcolor black
	menu font style bu
	menu text lblStatus 2 2 Status:
	menu font style
	menu text Status 2 15 Initializing...
	menu text lblCbook 2 28 Current book:
	menu text lblETA 394 33 Time remaining: ???
	menu text lblBRm 394 45 BODs remaining: ???
return

sub setProgressBar
	set #lpc 1000
	set %pBarSize ( ( 557000 * %finishedBods ) / %totalDeedCount ) / 1000
	set %percentTxt ( %finishedBods * 100 ) / %totalDeedCount
	set %textlocx ( %pbarsize / 2 ) + 12
	menu delete ProgressBar
	menu shape ProgressBar 22 82 %pBarSize 17 3 1 1 green 7 green
	menu font color black
	menu text ProgressBar %textlocx 83 %percentTxt , %
	menu font color green
	set #lpc 500
return

sub initMenu
	menu clear
	menu window color black
	menu window size 300 184
	menu window title s7UOBodExporter version %version
	menu font transparent #true
	menu font name Verdana
	menu font color navy
	menu font size 14
	menu text Title 2 2 S N I C K E R 7 ' S
	menu text Title 22 22 U O B o d   E X P O R T E R .
	menu font size 8
	menu font color green
	menu font bgcolor black
	menu check chkLbod 2 44 130 14 #false Export Large BODs
	menu check chkSbod 2 87 130 14 #false Export Small BODs
	menu text lblBookList 140 44 Books To Export:
	menu list create lstBookList 140 58 156 69
	menu button btnAddBook 141 130 37 20 Add
	menu button btnAddAll 180 130 37 20 All
	menu button btnDelBook 219 130 37 20 Del
	menu button btnClrBook 258 130 37 20 Clr
	set %statusx 140
	set %statusy 153
	if ! %debug
	   menu hideeuo
	menu show
return

sub ClrBook
	set %bodBookCount 0
	menu delete lstBookList
	menu list create lstBookList 140 58 157 69
	menu set lblBookList Books To Export:
	set %totalDeedCount 0
return

sub DelBook
	menu get lstBookList
	for %bbi #menures %bodBookCount
	{
			set %offset %bbi + 1
			set %bodBookID . %bbi %bodBookID . %offset
			set %bodBookName . %bbi %bodBookName . %offset
			set %bodBookDeedCount . %bbi %bodBookDeedCount . %offset
	}
	set %bodBookCount %bodBookCount - 1
	if %bodBookCount < 0
		set %bodBookCount 0
	menu delete lstBookList
	menu list create lstBookList 140 58 157 69
	set %totalDeedCount 0
	if %bodBookCount > 0
	{
		for %bbi 1 %bodBookCount
		{
			set %totalDeedCount %totalDeedCount + %bodBookDeedCount . %bbi
			menu list add lstBookList %bodBookName . %bbi , #spc , ( , %bodBookID . %bbi , )
		}
	}
	if %totalDeedCount > 0
		menu set lblBookList Books To Export: ( , %totalDeedCount , )
	if %totalDeedCount = 0
		menu set lblBookList Books To Export:
return

sub AllBooks
	finditem %bodbooktype C
	if #findkind = -1 3
		menu delete status
		menu text status %statusx %statusy No books found on$screen.
		return
	for %findi 0 1
	{
		finditem %bodbooktype C
		if #findkind <> -1
		{
			gosub AddBook #findid
			ignoreitem #findid BookIgnore
			set %findi 0
		}
	}
	ignoreitem reset BookIgnore
return		

sub AddBook
	if %0 = 0
	{
		menu delete status
		menu text status %statusx %statusy Please target the BOD$book to add...
		gosub s7GetTarget
	}
	if %0 > 0
		set #result %1
	finditem #result
	if #findtype <> %bodbooktype
	{
		menu delete status
		menu text status %statusx %statusy That is not a BoD book.
		return
	}
	if %bodBookCount <> 0
	{
		for %BBI 1 %bodBookCount
		{
			if #findid = %bodBookID . %BBI
			{
				menu delete status
				menu text status %statusx %statusy That book is already in$the list.
				return	
			}
		}
	}
	set %bodbookcount %bodbookcount + 1
	set %bodBookID . %bodbookcount #findid
	event property #findid
	if name: in #property
	{
		str len #property
		set %len #strRes
		str pos #property name:
		set %pos #strRes + 6
		set %len %len - %pos
		str mid #property %pos %len
		set %bodBookName . %bodBookCount #strRes
	}
	if name: notin #property
		set %bodBookName . %bodBookCount Unnamed
	str pos #property $book
	set %len #strRes
	str pos #property book:
	set %pos #strRes + 6
	set %len %len - %pos
	str mid #property %pos %len
	set %bodBookDeedCount . %bodBookCount #strRes
	set %totalDeedCount %totalDeedCount + #strRes
	menu set lblBookList Books To Export: ( , %totalDeedCount , )
	menu list add lstBookList %bodBookName . %bodbookcount , #spc , ( , %bodBookID . %bodBookCount , )
	menu delete status
return

sub SetETA
	if ( ( ( %finishedBods * 100 ) / %totalDeedCount ) >= 25 ) || %finishedBods >= 100
	{
		set %timeremaining ( ( %totalDeedCount - %finishedbods ) * ( ( #systime - %bodscanstart ) / %finishedbods ) ) / 1000
		set %timeremaining_m %timeremaining / 60
		set %timeremaining_s %timeremaining % 60
		if %timeremaining_s < 10 2
      str ins %timeremaining_s 0 0
      set %timeremaining_s #strRes
		menu set lblETA Time remaining: %timeremaining_m , : , %timeremaining_s
	}
return

;====================================================
;/0TH3R CR4P ///////////////////////////////////////=
;====================================================
	
sub checkVersion
	str left #euover 1
	if #strRes > 1
		return
	str pos #euover _
	set #strRes #strRes + 1
	str mid #euover #strRes 1
	if #strRes < 5
	{
		display ok HALT!$$You must be using EasyUO version 1.5 or greater$
				+in order to use this script! This script depends on$
				+functions which are not avaliable in earlier versions!
		menu hide
		halt
	}
return

; ===================================================================== ;
; ///////////////////////////////////////////////////////////////////// ;
; // 3XC3RP7S FR0M S7SUBS.EUO ///////////////////////////////////////// ;
; ///////////////////////////////////////////////////////////////////// ;
; // Any reproduction of the subroutines below is strictly pro- /////// ;
; // hibited without express written permission of the author ///////// ;
; // (snicker7). Violation of the above will result in malicious ////// ;
; // actions by the aforementioned author performed upon your person // ;
; // including but not limited to: bodily harm, slander, maternal ///// ;
; // insults, possible legal action, and callin' the five-oh. Thank /// ;
; // you. ///////////////////////////////////////////////////////////// ;
; ///////////////////////////////////////////////////////////////////// ;
; ===================================================================== ;

;----
;* @name s7CheckPixelVisibility
;* @author snicker7
;* @ver 1.0 26Jul05
;* @purpose	Checks multiple pixels on the screen
;*		to determine whether they are covered 
;*		by another window or not.
;* @params 	%1	#true/#false: display an 
;*			alert window if the gump is
;*			covered or simply continue 
;*			looping until it isn't.
;*		%n*2	the x coordinates of the pixels
;*			you wish to check.
;*		%n*2+1	the y coordinates of the pixels
;*			you wish to check. Multiple
;*			sets of pixels may be checked
;*			for visibility at the same time.
;*			See example.
;* @returns 	#false	if you did not pass the correct
;*			parameters.
;* @notes 	the more pixels that are checked, the
;*		greater the execution time of the sub
;*		will be.
;* @example: gosub s7CheckPixelVisibility <true/false> <x1> <y1> <x2> <y2> ... <xN> <yN>
;* @status done
sub s7CheckPixelVisibility
	if %0 < 3
		return #false
	if ( ( %0 - 1 ) % 2 ) > 0
		return #false
	namespace push
	namespace local _s7CPVis
	set !alert %1
	set !checkCnt ( %0 - 1 ) / 2
	set !to #scnt
	for !checki 1 !checkCnt
	{
		set !_x ( !checki * 2 )
		set !_y ( ( !checki * 2 ) + 1 )
		set !_x % . !_x
		set !_y % . !_y
		savepix !_x !_y 779
		if #pixcol = 4294967295
		{
			if !alert 2
				display ok Attention, something is covering the Ultima Online window,$and it needs to be visible in order for the script to continue.$Please make sure the window is uncovered and press OK.$You will have 3 seconds to make the window visible.
				wait 3s
			if ! !alert && !to + 30 < #scnt
			{
				display yesno Something has caused the visibility scanner to hang for 30 seconds or more.$Please make sure that the Ultima Online window is visible and unconvered, and that the gump is in plain sight.$$Would you like to continue the script anyway?
				if #dispres = yes
					return
				set !to #scnt
			}
			set !checki 0
		}
	}
	namespace pop
return

;----
;* @name s7CheckGumpVisibility
;* @author snicker7
;* @ver 1.0 26Jul05
;* @purpose	Checks 5 key positions on currently 
;*		open gump to ensure that it is not
;*		covered by another window for pixel
;*		scanning.
;* @params 	%1	#true/#false: display an 
;*			alert window if the gump is
;*			covered or simply continue 
;*			looping until it isn't.
;* @returns N/A
;* @notes execution time is ~.08seconds at 50 LPC.
;* @example: gosub s7CheckGumpVisibility #true
;* @status done
sub s7CheckGumpVisibility 
	namespace push
	namespace local _s7CGVis
	set !alert #false
	if %0 > 0
		set !alert %1
	set !contsize #contsize
	set !x1 #contposx
	set !y1 #contposy
	str len !contsize
	set !len #strRes
	str pos !contsize _
	set !pos #strRes - 1
	str left !contsize !pos
	set !xsize #strRes
	set !len !len - !pos
	set !pos !pos + 2
	str mid !contsize !pos !len
	set !ysize #strRes
	set !x2 !x1 + !xsize
	set !y2 !y1 + !ysize
	set !xmid ( !x1 + !xsize ) / 2
	set !ymid ( !y1 + !ysize ) / 2
	gosub s7CheckPixelVisibility !alert !x1 !y1 !x1 !y2 !x2 !y1 !x2 !y2 !xmid !ymid !xmid !y1 !xmid !y2 !x1 !ymid !x2 !ymid
	namespace pop
return

;----
;* @name s7GetTarget
;* @author snicker7
;* @ver 1.0 27Dec04
;* @purpose Returns a target using #targcurs, objects only.
;* @params 	none
;* @returns Item id of the targeted item.
;* @notes none
;* @example: gosub getTarget
;* @status tested good
sub s7GetTarget
	set #targcurs 1
	_waitfortargcurs:
	if #targcurs = 1 2
		wait 1
		goto _waitfortargcurs
return #ltargetid

;====================================================
;/0TH3R SCR1P73R'S SUBS/////////////////////////////=
;====================================================

;================================== 
; Script Name: filesubs.txt (import version)
; Author: Kal In Ex
; Version: 1.09
; Client Tested with: 4.0.6a
; EUO version tested with: 1.42.009d
; Shard OSI / FS: OSI
; Revision Date: December 23, 2004
; Public Release: November 16, 2004
; Global Variables Used: 
; Purpose: writing to files in EUO
; Copyright 2004 Kal In Ex
;==================================

; subs open, GetOSFileCmd, wipe, fwipe, write and dump
; are all a part of filesubs.txt only use this script
; in its entirety and do not modify it!

sub open ; <identifier> <filename>
	namespace push
	namespace local _filebuffers
	set !_filename %1 , _filename
	set !_buffer %1 , _buffer
	set !_dirop %1 , _dirop
	set ! . !_filename %2
	set ! . !_buffer
	set ! . !_dirop >>
	if !_filesub = n/a
		{
		gosub GetOSFileCmd _
		if #result = command.com
			{
			set !_filesub _filesubsW1
			set !_filecmd command.com
			}
		if #result = cmd.exe
			{
			set !_filesub _filesubsW2
			set !_filecmd cmd.exe
			}
		}
	namespace pop
	return
	
;================== 
;** 
;* @name GetOSFileCmd 
;* @ver 1.0 9May04 
;* @author Roadkill 
;* @purpose check the OS and set @%1 to cmd.exe or command.exe if win98/2000xp 
;* @params %1 the varname to hold the return command 
;* @returns @%1 and #result hold cmd.exe for win2k and xp, or command.exe for 98/me 
;* @example call rksubs.txt GetOSFileCmd filecmd 
;* @status done 
sub GetOSFileCmd
	str Left #osver 1
	set % . %1 cmd.exe
	if #strRes = 1
		set % . %1 command.com
	return % . %1
;==================

sub wipe ; <identifier>
	namespace push
	namespace local _filebuffers
	set !_buffer %1 , _buffer
	set !_dirop %1 , _dirop
	set ! . !_buffer
	set ! . !_dirop >
	namespace pop
	return

sub fwipe ; <identifier>
	namespace push
	namespace local _filebuffers
	set !_filename %1 , _filename
	set !_buffer %1 , _buffer
	set ! . !_buffer
	execute !_filecmd /c type > ! . !_filename
	namespace pop
	return

sub write ; <identifier> <text> <text> <text...>
	namespace push
	namespace local _filebuffers
	if %0 > 2
		{
		for !_ 3 %0
			{
			set %2 %2 , #spc , % . !_
			}
		}
	goto !_filesub
	namespace pop
	return

_filesubsW1:
	str len %2
	for !_ #strres 1
		{
		str mid %2 !_ 1
		if #strres in <>
			{
			str ins %2 ^ !_
			set %2 #strres
			}
		}
	set !_filename %1 , _filename
	set !_dirop %1 , _dirop
	menu set status Writing BOD %currbod to file...
	execute !_filecmd /c echo %2 ! . !_dirop ! . !_filename
	gosub s7CheckGumpVisibility #false
	set ! . !_dirop >>
	namespace pop
	return

_filesubsW2:
	set !2 %2
	str len !2
;	for !_ #strres 1
;		{
;		str mid !2 !_ 1
;		if #strres in &|()^<>
;			{
;			str ins !2 ^ !_
;			set !2 #strres
;			}
;		}
	set !_filename %1 , _filename
	set !_buffer %1 , _buffer
	set !_dirop %1 , _dirop
	str len ! . !_buffer
	set !_ #strres
	str len !2
	if !_ + #strres >= 1900
		{
		menu set status Write buffer full... writing to file...
		execute !_filecmd /c ! . !_buffer
		gosub s7CheckGumpVisibility #false
		set ! . !_buffer
		}
	str len ! . !_buffer
	if #strres = 0 || ! . !_dirop = >
		set ! . !_buffer echo , #spc , !2 , #spc , ! . !_dirop , #spc , ! . !_filename
	else
		set ! . !_buffer ! . !_buffer , #spc , && , #spc , echo , #spc , !2 , #spc , ! . !_dirop , #spc , ! . !_filename
	set ! . !_dirop >>
	namespace pop
	return

sub dump ; <identifier>
	namespace push
	namespace local _filebuffers
	set !_filename %1 , _filename
	set !_buffer %1 , _buffer
	str len ! . !_buffer
	if #strres > 0
		{
		execute !_filecmd /c ! . !_buffer
		gosub s7CheckGumpVisibility #false
		set ! . !_buffer
		}
	namespace pop
	return
;================================== 
; end of filesubs.txt
;================================== 
