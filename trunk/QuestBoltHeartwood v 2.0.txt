;==================================
; Script Name: TurboQuester for Bowcrafter
; Version: 2.0
; Author: Tony
; Client Tested with: 6.0.6
; EUO version tested with: 1.5 v132
; Shard OSI / FS: FS
; Revision Date: 14/04/09
; Public Release: 26/02/08
; Purpose: ENG Obtains and completes Heartwood 'Lethal Darts' bowcrafting quest,
; grabs and insures runic tools, trashes talismanes, recipes and other garbage,
; restocks bolts from bankbox and back again.
; ITA Ottiene e completa la quest da bowcrafter 'Lethal Darts' a Heartwood,
; raccoglie e insura i kit runici ricevuti, butta via le recipe, i talismani e
; e l'altra robaccia, restocka le bolts in banca e torna indietro.
; NEW 2.0: items evaluating, statistics window
;==================================
; Per iniziare:
;  -una runa marcata a yew, vicino al portale per entrare a heartwood
;  -questa runa deve essere chiamata 'elfi'
;  -una runa marcata in banca
;  -questa runa deve essere chiamata 'bank' o 'banca'
;  -entrambe le rune vanno posizionate dentro un runebook
;    OPPURE
;   dentro runebook diversi
;  -più bolts (frecce per la balestra) avete in banca, meglio è
;  -press play
; E' possibile impostare un valore per la raccolta dei talismani
; Se ad esempio imposto 40 verranno conservati tutti i talismani
; la cui somma dei bonus nelle skill lavorative è superiore a 40
; Es: Blacksmith bonus 15 , Blacksmith Exceptional bonus 25
; Se non voglio conservare nessun talismano, è sufficiente impostare
; un numero molto alto. I talismani Slayer verranno comunque conservati.
;==================================
set %talismani 50 ;talismans value
set %min_item_value 25 ;items value
; #NON MODIFICARE NIENTE QUI SOTTO#
set %last_time #scnt
gosub getTypes
gosub creaMenu
set %first_time #true
gosub metodoRecall
gosub getMyBag
gosub trovaRune
gosub acquisisciPosArrayPerElfi
gosub acquisisciPosArrayPerBanca
gosub ignoraBackpack
gosub controllaRisorse ;first_time
gosub trovaQuester
set %first_time #false

main_loop:
  gosub CheckForWorldSave
  gosub antiBlock
  gosub get_quest
  gosub check_quest
  gosub toggle_quest_item
  gosub complete_quest
  gosub premio
  gosub controllaRisorse
goto main_loop

sub metodoRecall
  chooseskill chiv
  set %chivalry #skill
  chooseskill mage
  set %magery #skill
  if %chivalry > %magery
    set %recallMethod C
  else
    set %recallMethod M
return
sub ignoraBackpack
  finditem ZJF C_ , #backpackid
  for %i 0 #findcnt
  {
    set #findindex %i
    ignoreitem #findid
  }
return

sub get_quest
  gosub CheckForWorldSave
  gosub antiBlock
  set #lobjectid %npc
  event macro 17 0
  wait 5
  while #contsize <> 507_436
  {
    event macro 17 0
    wait 1s
  }
return

sub check_quest
  gosub CheckForWorldSave
  gosub antiBlock
  wait 5
  set %rx 202 + #contposx
  set %ry 236 + #contposy
  savepix %rx %ry 1
  if #pixcol = 8710077
  {
    set %c_x 132 + #contposx
    set %c_y 402 + #contposy
    click %c_x %c_y
    return
  }
  else
    goto main_loop
halt

sub toggle_quest_item
  gosub CheckForWorldSave
  wait 5
  toggle_loop:

  if europa in #shard
  {
    set %x #clixres / 2
    set %y #cliyres / 2
    set %absx #clileft + %x
    set %absy #clitop + %y - 60
    click %absx %absy mc
    wait 1s
    set %x 100 + #contposx
    set %y 110 + #contposy
    click %x %y
  }
  else
  {
    exevent popup #charid 6
  }
    target 1s
    finditem LNK C_ , #backpackid
    set #ltargetid #findid
    set #ltargetkind 1
    event macro 22 0
    wait 20
    set #targcurs 0
    finditem #ltargetid C_ , #backpackid
    event property #findid
    if quest notin #property 2
      set #targcurs 0
      goto toggle_loop
return

sub complete_quest
  gosub CheckForWorldSave
  gosub antiBlock
  set #lobjectid %npc
  event macro 17 0
  wait 5
  while #contsize <> 507_436
  {
    event macro 17 0
    wait 1s
  }
  set %x #contposx + 132
  set %y #contposy + 402
  click %x %y
  event exmsg #charid 3 30 Quest completata!
  wait 1s
  if europa in #shard
  {
    set %x #contposx + 132
    set %y #contposy + 402
    click %x %y
    wait 1s
  }

return

sub premio
  gosub CheckForWorldSave
  gosub antiBlock
  wait 5
  finditem ZJF C_ , #backpackid
  if #findkind <> -1
  {
    set %a_pack #findid
    set #lobjectid %a_pack
    event macro 17 0
    wait 1s
    while #contid <> %a_pack
    {
      event macro 17 0
      wait 1s
    }
    gosub refreshValue quest 1
    set %time #scnt - %last_time
    set %last_time #scnt
    gosub refreshValue time %time
    finditem * C_ , %a_pack
    for #findindex 1 #findcnt
    {
      event property #findid
      if Oak in #property || Yew in #property || Ash in #property || Heartwood in #property
      {
        gosub refreshValue #findcol 1
        sound
        exevent drag #findid 1
        wait 10
        exevent dropc #backpackid
        wait 10
        if Oak notin #property
          gosub insure #findid
      }
    }
    finditem %loot C_ , %a_pack
    if #findkind <> -1
    {
      event sysmessage Checking , #spc , #findcnt , #spc , items...
      for #findindex 1 #findcnt
      {
         str Pos #property $
         set %cut #strres - 1
         str Mid #property 1 %cut
         set %nome_completo #strres
         call BMItemEval.euo #findid
         event sysmessage %nome_completo , #spc , value: , #spc , #result
         if #result > %min_item_value
         {
           exevent drag #findid
           wait 10
           exevent dropc %my_bag
           wait 10
         }
      }
    }
    finditem EWR_DWR_FWR_GWR C_ , %a_pack
    if #findkind <> -1
      gosub valutaTalismano #findid
    if #result = -1
    {
        exevent drag #findid 1
        wait 10
        exevent dropc %my_bag
        wait 10
    }
     finditem JKF G_2
     set %trashcan #findid
     butta_loop:
       finditem ZJF C_ , #backpackid
       if #findkind <> -1
       {
         exevent drag %a_pack 1
         wait 10
         exevent dropc %trashcan
         wait 10
         goto butta_loop
       }
     finditem EWH_TVH C_ , %a_pack
     event property #findid
     if Aquarium in #property
     {
     exevent drag #findid
     wait 10
     exevent dropc %my_bag
     wait 10
     }

  }
return

sub insure
  gosub CheckForWorldSave
  set %target %1
  exevent popup #charid 3
  target 2s
  set #ltargetid %target
  set #ltargetkind 1
  event macro 22 0
  key esc
  if #targcurs = 1
    set #targcurs 0
return

sub trovaQuester
  trova_npc:
  finditem HS_IS_XU
  event property #findid
  if Bowcrafter in #property
    set %npc #findid
  else
  {
    ignoreitem #findid
    goto trova_npc
  }
return

sub restock
  gosub CheckForWorldSave
  ;if first_time notin %1 2
  ;  gosub esciDagliElfi
  ;  gosub clickRunebookLocation %libro_banca %numero_runa_banca m #true
  gosub prendiRoba
  gosub clickRunebookLocation %libro_elfi %numero_runa_elfi %recallMethod #true
  gosub entraDagliElfi
return

sub prendiRoba
  gosub CheckForWorldSave
  wait 2s
  msg $
  msg bank $
  wait 2s
  finditem UFG C_ , #backpackid
  for %i 0 #findcnt
  {
    set #findindex %i
    exevent drag #findid 1
    wait 10
    exevent dropc #contid
    wait 10
  }
  set %wmod 0
  finditem #charid
  if #findtype in IS_HS
    set %wmod 50
  finditem  LNK C_ , #contid
  if #findkind = -1
  {
    ;chooseskill Bowc
    ;if #skill < 200
    ;{
      display ok Sei a corto di bolts, lo script si è messo in pausa.
      pause
    ;}
    ;else
     ;gosub craftaBolts
  }
  set %q ( ( #maxweight - #weight + %wmod ) * 10 )
  exevent drag #findid %q
  wait 10
  exevent dropc #backpackid
  wait 10
return

sub esciDagliElfi
set #lpc 200
wait 1s
  for %i 1 6
  {
    gosub CheckForWorldSave
    set %x %Bxpos . %i
    set %y %Bypos . %i
    event pathfind %x %y
    while #charposx <> %x || #charposy <> %y
      wait 1
  }
set #lpc 20
return

sub entraDagliElfi
set #lpc 200
wait 1s
  for %i 1 5
  {
    gosub CheckForWorldSave
    set %x %Expos . %i
    set %y %Eypos . %i
    event pathfind %x %y
    wait 3s
  }
  if europa in #shard
  {
    event pathfind %Expos6 %Eypos6
    wait 3s
    event pathfind 7050 380
  }
set #lpc 20
return

sub controllaRisorse
  finditem LNK C_ , #backpackid
  set %tot 0
  for %y 1 #findcnt
  {
    set #findindex %y
    set %tot %tot + #findstack
  }
  if %tot < 20 || #findindex = -1
  {
    if %first_time = #false
      gosub esciDagliElfi
    event sysmessage vado in banca
    gosub clickRunebookLocation %libro_banca %numero_runa_banca %recallMethod #true
    gosub restock %var
  }
  else
  {
    if %first_time 2
      gosub clickRunebookLocation %libro_elfi %numero_runa_elfi %recallMethod #true
      gosub entraDagliElfi
  }
return

sub trovaRune
  finditem ZBN C_ , #backpackid
  for %i 1 #findcnt
  {
    event sysmessage Scansione runebook , #spc , %i , #spc , in corso...
    set #findindex %i
    set #lobjectid #findid
    event macro 17 0
    wait 5
    while #contsize <> 452_236
    {
      wait 7
      event macro 17 0
    }
    for %_ 1 16
    {
      contpos 0 50
      wait 5
      call kalocr getRuneName %_ 0 50
      if %1 = N/A
        display ok Non hai messo il file kalocr.txt nella cartella di EUOX.
      if bank in %1 || banca in %1 3
        set %libro_banca #findid
        set %numero_runa_banca %_
        event sysmessage Runa per banca trovata , #spc , %_
      if elfi in %1 || home in %1 3
        set %libro_elfi #findid
        set %numero_runa_elfi %_
        event sysmessage Runa per Heartwood trovata , #spc , %_
    }
  }
  if %libro_elfi = N/A || %libro_banca = N/A || %numero_runa_banca = N/A || %numero_runa_elfi = N/A 2
    display ok Non hai le rune necessarie per proseguire.
    halt
return

sub acquisisciPosArrayPerElfi
  set %Expos1 535
  set %Eypos1 992
  set %Expos2 6994
  set %Eypos2 347
  set %Expos3 7011
  set %Eypos3 363
  set %Expos4 7028
  set %Eypos4 380
  set %Expos5 7039
  set %Eypos5 379
  if europa in #shard
  {
    set %Expos6 7049
    set %Eypos6 379
  }
return

sub acquisisciPosArrayPerBanca
  set %Bxpos1 7024
  set %Bypos1 375
  set %Bxpos2 7014
  set %Bypos2 365
  set %Bxpos3 7004
  set %Bypos3 357
  set %Bxpos4 6994
  set %Bypos4 349
  set %Bxpos5 6989
  set %Bypos5 343
  set %Bxpos6 6984
  set %Bypos6 338
return

;Fail Override Option...
;#TRUE = Enable / #FALSE = Disable
;If you fizzle will continue to try until successful
;If your recall option becomes unavailable, will try new one based on skill and supplies

; Place this at the bottom of your script... or use it as a called sub.
;==================================
; Script Name: subClickRunebookLocation
; Author: Tecmo, Bad_Maniac
; Version: 2.41
; Client Tested with: 4.0.11b
; EUO version tested with: 1.50 build 0057
; Shard OSI / FS: OSI & FS
; Revision Date: 2005-Oct-08
; Public Release: 2004-Nov-25
; Copyright: Tecmo, Bad_Maniac
;* @packageversion   2.41
;* @purpose   Click specified location in runebook (even when book gets moved)
;* @author   Tecmo, Bad_Maniac
;* @param   %1 BookID      req   #LobjectId of runebook you wish to use
;*      %2 RuneNumber      req   Rune in which to recall from (1 to 16) 1-8 Left, 9-16 Right
;*      %3 RecallMethod    req   Method of recalling; S = Scroll, M = Magery, C = Sacred Journey
;*      %4 Fail Override   req   #TRUE = Enable / #FALSE = Disable (Fizzle, location blocked, Method change if current unavailable
;*      %5 Default         opt   Recalls off the default rune via the selected method, %2 is ignored
;* @return      Returns the value True or False
;* @example    (inside script) gosub clickRunebookLocation [BookID] [RuneNumber] [RecallMethod] [FailOverride] [*default*]
;*       (outside calling) call subClickRuneBookLocation.txt [BookID] [RuneNumber] [RecallMethod] [FailOverride] [*default*]
;==================================
SUB clickRunebookLocation
{
  namespace PUSH
  NAMESPACE LOCAL ClickRunebook
  ;___________USER SETUP_________
  SET !_locationBlockLimit 12 ;Recall attempts on location block before recall fail
  SET !_fizzleLimit 12 ;Recall retrys using magery / scroll before recall fail
  ;___________END  SETUP_________
	;DO NOT EDIT BELOW HERE
	SET !_oldPos #CHARPOSX , #CHARPOSY , #CURSKIND ;Marks your current location
	SET #RESULT ParamError
	IF %0 < 4 || %0 > 5 ;If too many or too few parameters were passed
		GOTO runebooklocation_end ;fail
	IF #TRUE NOTIN %4 && #FALSE NOTIN %4 ;Fail Override On/Off Verifier
		GOTO runebooklocation_end
	FOR !i 0 %0
	{
	  SET ! . !i % . !i
	}
	SET #RESULT #TRUE
	SET !_failed
	SET !_fizzle 0
	SET !_locationBlock 0
_recall:
	SET !_jStart #jIndex
	SET !_fail null
	SET !_oldTP #TP
	FINDITEM !1 C_ , #BACKPACKID
	IF #FINDKIND = -1
	{
	  SET #RESULT No_RuneBook
	  GOTO runebooklocation_end
	}
	IF !0 = 5 && !5 = default ;Recall using Scroll on book default rune
	{
	  SET #LTARGETKIND 1
	  SET #LTARGETID #FINDID
	  IF !3 = S ;Recall using Scroll on book default rune
	  {
		  FINDITEM WTL C_ , #BACKPACKID
		  IF #FINDKIND = -1
		  {
			  SET !_fail Scroll
			  GOTO _failOverride
			}
			SET #LOBJECTID #FINDID
			EVENT macro 17 0
		}
		IF !3 = M ;Recall using Magery on book default rune
		  EVENT macro 15 31
		IF !3 = C ;Recall using Sacred Journey on book default rune
		  EVENT macro 15 210
		TARGET 5s
		EVENT macro 22 0
		GOTO _locationCheck
	}
	SET !_rune !2
	SET #LOBJECTID #FINDID
	EVENT macro 17 0
	SET !runebooktimeout #SCNT2 + 30
_runeBookWait:
	IF #SCNT2 > !runebooktimeout
	  GOTO _Recall
	IF #CONTSIZE <> 452_236 && generic_gump NOTIN #CONTNAME
	  GOTO _runeBookWait
	IF !3 = S  ;Recall using Recall Scrolls
	{
	  IF !2 <= 8
		{
		  SET !_rX 135
		  SET !_rY 55 + ( !_rune * 15 )
		}
		IF !2 > 8
		{
		  SET !_rX 295
		  SET !_rY -65 + ( !_rune * 15 )
		}
		SET !_clickX #contPosX + !_rX
		SET !_clickY #contPosY + !_rY
		CLICK !_clickX !_clickY
		GOTO _locationCheck
	}
	; Recall using Magery or Chivalry (opens the book and then turns to the page)
	IF !3 = M || !3 = C
	{
    wait 3s
		; Determining the click offsets and opening to the correct page
	  SET !_pageY #CONTPOSY + 195
	  SET !_pageX #CONTPOSX + ( ( !2 - 1 ) / 2 ) * 35 + 140
	  IF !2 > 8
		  SET !_pageX !_pageX + 30
   	CLICK !_pageX !_pageY ;Click desired page
		; Determing the click offsets and clicking the gem
		SET !_odd 1_3_5_7_9_11_13_15 ;Odd rune numbers
		SET !_even 2_4_6_8_10_12_14_16 ;Even rune numbers
		IF !3 = M ;Magery
		  SET !_rY #CONTPOSY + 145
		IF !3 = C ;Chivalry
		  SET !_rY #CONTPOSY + 180
		IF !2 IN !_even
		  SET !_rX #CONTPOSX + 300
		IF !2 IN !_odd
		  SET !_rX #CONTPOSX + 140
		CLICK !_rX !_rY ;click selected rune with chosen recall option
	}
_locationCheck:
  SET !_jEnd !_jStart + 8
	SET !_timeout #SCNT2 + 60
_locationCheckLoop:
	FOR !i 1 1
	{
	  IF !_oldPos = #CHARPOSX , #CHARPOSY , #CURSKIND && #SCNT2 < !_timeout
		{
      FOR !_jIndex !_jStart #JINDEX
      {
        WAIT 1
        SCANJOURNAL 1
		    IF reagents IN #JOURNAL
			    SET !_fail Reagents
  			IF there_are_no_charges_ IN #JOURNAL && !3 = S
				  SET !_fail NoCharges
			  IF fizzles IN #JOURNAL
				  SET !_fail Fizzles
			  IF something_is_blocking_ IN #JOURNAL || that_location_is_blocked IN #JOURNAL
				  SET !_fail LocationBlock
			  IF this_spell_has_been IN #JOURNAL
				  SET !_fail TithingPoints
			  IF !_fail <> null
				  GOTO _failOverride
			  IF you_have_not_yet IN #JOURNAL
				  GOTO _recall
			  SET !i 0
			  SET !_jStart !_jStart + 1
      }
		}
	}
	IF !3 = C && #TP = !_oldTP && !_fail <> LocationBlock
		SET !_fail TithingPoints
_failOverride:
	IF !_fail IN Sroll_NoCharges_Reagents_Fizzles_TithingPoints_LocationBlock && !4 = #TRUE
	{
    IF !_fail NOTIN !_failed
    	 SET !_failed !_failed , _ , !_fail
		IF Fizzles = !_fail
		{
			 IF !_fizzle >= !_fizzleLimit
			 {
			 	 SET #RESULT You_fizzled_ , !_fizzle , _ , times
				 GOTO runebooklocation_end
			 }
		   SET !_fizzle !_fizzle + 1
		   GOTO _recall
	  }
		SET !_fail null
		IF LocationBlock IN !_failed
		{
			IF !_locationBlock >= !_locationBlockLimit
			{
				SET #RESULT location_blocked
				GOTO runebooklocation_end
			}
			SET !_locationBlock !_locationBlock + 1
			WAIT 2s
    	GOTO _recall
		}
		IF TithingPoints NOTIN !_failed
		{
			CHOOSESKILL CHIVALRY
			IF #SKILL >= 15 && #TP >= 15 && #MANA >= 10
      {
      	SET !3 C
			  GOTO _recall
      }
		}
		IF Scroll NOTIN !_failed && !5 = DEFUALT || NoCharges NOTIN !_failed
		{
			SET !3 S
			GOTO _recall
		}
		IF Reagents NOTIN !_failed
		{
			CHOOSESKILL MAGERY
			IF #SKILL >= 15 && #MANA >= 11
			{
			  SET !3 M
			  GOTO _recall
      }
		}
	}
runebooklocation_end:
	IF !_oldPos = #CHARPOSX , #CHARPOSY , #CURSKIND
		SET #RESULT !_failed
	IF !4 = #FALSE && !_fail <> null
		SET #RESULT !_fail
	NAMESPACE CLEAR
	NAMESPACE POP
	RETURN #RESULT
}
;End of subClickRuneBookLocation
;==================================

;==================
;**
;* @name CheckForWorldSave
;* @ver 1.0 20May05
;* @author  Boydon
;* @purpose To detect world saves and resources cleaning of RunUO Shards
;* @params
;* @returns
;* @notes the sub keep track of the last scanned journal line using
;*					the variable %Saveindex: every time the sub is called the script will
;*					start to scan from that line.
;* @dependencies sub WaitForSaveEnd
;* @example gosub CheckForWorldSave
;* @status Tested and working fine

sub CheckForWorldSave
	if %SaveIndex = N/A
		set %SaveIndex #jindex

	set !Save #false
	set !Jstart %SaveIndex
	for !i !Jstart #jindex
	{
		scanjournal !i
		if #jcolor = 53
		{
			if THE_WORLD_WILL_SAVE_IN in #journal || WORLD_IS_SAVING in #journal
			{
				set !Save #true
				Event ExMsg #charid 3 33 SAVE Detected
				wait 40s
				set %SaveIndex #jindex
				set !i #jindex
			}
			if CLEANING in #journal
			{
				set !Save #true
				Event ExMsg #charid 3 33 CLEANING Detected
				;gosub WaitForSaveEnd CLEANED %CleaningTimeout !i CLEANING
				wait 25s
				set %SaveIndex #jindex
				set !i #jindex
			}
		}
		set %SaveIndex !i
	}

	nameSpace Clear
	nameSpace Pop

return

sub valutaTalismano
set %talismano %1
finditem %talismano
event property #findid
;display ok #property
if bonus in #property
{
  str count #property $
  set %righe #strres - 1
  set %left 0
  set %somma 0
  for %i 0 %righe
  {
    str pos #property $ %i
    set %left #strres
    set %j %i + 1
    str pos #property $ %j
    set %right #strres - %left
    str mid #property %left %right
    set %riga . %i #strres
    if bonus in %riga . %i
    {
      set %stringa %riga . %i
      str pos %stringa :
      set %left #strres + 1
      str pos %stringa %
      set %right #strres - %left
      str mid %stringa %left %right
      set %valore #strres
      set %somma %somma + %valore
      ;event sysmessage %valore
    }
  }
  if %somma > %talismani || summoning in #property || slayer in #property || Yamandon , #spc , Killer: , #spc , +9 in #property
    return #true
  else
  ;  return #false
  event sysmessage La somma dei valori è , #spc , %somma
}
return #false

sub craftaBolts
set %legna TLK
set %piume VLK
msg $
msg bank $
wait 10
set %bank #contid
finditem %legna C_ , %bank
if #findkind = -1 2
  display ok Legna finita.
  stop
set %suml 0
for %_i 0 #findcnt
{
  set #findindex %_i
  set %suml %suml + #findstack
}
finditem %piume C_ , %bank
if #findkind = -1 2
  display ok Piume finite.
  stop
set %sump 0
for %_i 0 #findcnt
{
  set #findindex %_i
  set %sump %sump + #findstack
}
if %suml < %sump
  set %sum %suml
else
  set %sum %sump
if %sum > 5000
  set %sum 5000
set %q ( #maxweight - #weight - 30 )
set %_1 ( %sum / %q )
set %_0 0
if %_1 > 20
  set %_1 20
for %_0 1 %_1
{
  crafting_loop:
  finditem %legna C_ , %bank
  if #findkind = -1 2
    display ok Legna finita.
    stop
  set %q ( #maxweight - #weight - 30 )
  exevent drag #findid %q
  wait 8
  exevent dropc #backpackid
  wait 8
  exevent dropc #backpackid
  finditem %piume C_ , %bank
  if #findkind = -1 2
    display ok Piume finite.
    stop
  exevent drag #findid %q
  wait 8
  exevent dropc #backpackid
  wait 8
  exevent dropc #backpackid
  finditem UFG C_ , #backpackid
  if #findkind = -1
  {
    cerca_tool:
    finditem UFG C_ , %bank
    event property #findid
    if #findkind = -1
    {
      display ok Fletching tools finiti.
      stop
    }
    else
    {
      if runic notin #property
      {
        exevent drag #findid 1
        wait 8
        exevent dropc #backpackid
        wait 8
        goto continue_crafting
      }
      else
      {
        ignoreitem #findid
        goto cerca_tool
      }
    }
  }
  continue_crafting:
  set #lobjectid #findid
  event macro 17 0
  wait 10
  set %x #contposx + 30
  set %y #contposy + 90
  click %x %y
  wait 10
  set %x #contposx + 234
  set %y #contposy + 90
  click %x %y
  wait 50
  set %x #contposx + 30
  set %y #contposy + 110
  click %x %y
  wait 10
  set %x #contposx + 230
  set %y #contposy + 90
  click %x %y
  wait 50
  finditem LNK C_ , #backpackid
  if #findkind <> -1
  {
    exevent drag #findid #findstack
    wait 8
    exevent dropc %bank
    wait 8
    exevent dropc #backpackid
  }
  else
  {
    display ok Bolts finiti.
    stop
  }
  wait 10
  exevent dropc #backpackid
}
return

sub antiBlock
  set %rx 321 + #contposx
  set %ry 403 + #contposy
  savepix %rx %ry 2
  if #pixcol = 4866626 || #contsize = 348_262
  {
    set %rx 350 + #contposx
    set %ry 400 + #contposy
    click %rx %ry
    wait 10
    event macro 8 1
    wait 10
    set %rx 216 + #contposx
    set %ry 133 + #contposy
    click %rx %ry
    wait 10
    set %rx 380 + #contposx
    set %ry 150 + #contposy
    click %rx %ry
    wait 10
    set %rx 130 + #contposx
    set %ry 400 + #contposy
    click %rx %ry
    wait 10
    set %rx 300 + #contposx
    set %ry 230 + #contposy
    click %rx %ry
    wait 10
  }
return

sub creaMenu
menu clear
set #MenuButton N/A
menu Font BGColor white
menu font color black
menu window color white
menu Window Guild Scanner
set %x #clileft + #clixres
set %y 50
menu Window Size 260 180
menu Font Size 10
menu Show %x %y
menu Font Style b
menu text t0 5 0 TurboQuesterBowcrafting2.0 - Statistics
menu text t01 10 20 Runic kits collected
menu Font Style
menu text t1 15 40 Oak
menu text t2 15 60 Ash
menu text t3 15 80 Yew
menu text t4 15 100 Heartwood
menu Font Style b
menu text t5 10 130 Total time
menu text t6 10 150 Quests completed
menu Font Style
menu text c1 160 40 0
menu text c2 160 60 0
menu text c3 160 80 0
menu text c4 160 100 0
menu text c5 160 130 00:00:00
menu text c6 160 150 0
;menu Button reset 1 180 150 20 Reset

if *TURBO_QUESTER_C1 = N/A
  set *TURBO_QUESTER_C1 0
if *TURBO_QUESTER_C2 = N/A
  set *TURBO_QUESTER_C2 0
if *TURBO_QUESTER_C3 = N/A
  set *TURBO_QUESTER_C3 0
if *TURBO_QUESTER_C4 = N/A
  set *TURBO_QUESTER_C4 0
if *TURBO_QUESTER_C5 = N/A
  set *TURBO_QUESTER_C5 0
if *TURBO_QUESTER_C6 = N/A
  set *TURBO_QUESTER_C6 0
  
  
 ; MENU SET c1 *TURBO_QUESTER_C1
  ;MENU SET c2 *TURBO_QUESTER_C2
  ;MENU SET c3 *TURBO_QUESTER_C3
  ;MENU SET c4 *TURBO_QUESTER_C4
 ; MENU SET c5 *TURBO_QUESTER_C5
 ; MENU SET c6 *TURBO_QUESTER_C6
  
  gosub refreshValue
return

sub refreshValue
  if %1 = 2010
    set *TURBO_QUESTER_C1 *TURBO_QUESTER_C1 + %2
  if %1 = 1191
    set *TURBO_QUESTER_C2 *TURBO_QUESTER_C2 + %2
  if %1 = 1192
    set *TURBO_QUESTER_C3 *TURBO_QUESTER_C3 + %2
  if %1 = 1193
    set *TURBO_QUESTER_C4 *TURBO_QUESTER_C4 + %2
  if %1 = time
    set *TURBO_QUESTER_C5 *TURBO_QUESTER_C5 + %2
  if %1 = quest
    set *TURBO_QUESTER_C6 *TURBO_QUESTER_C6 + %2

  set %minuti *TURBO_QUESTER_C5 / 60
  set %secondi *TURBO_QUESTER_C5 % 60
  set %ore %minuti / 60
  set %minuti %minuti % 60
  str len %secondi
  if #strres = 1
    set %secondi 0 , %secondi
  str len %minuti
  if #strres = 1
    set %minuti 0 , %minuti
  str len %ore
  if #strres = 1
    set %ore 0 , %ore

  set %my_time %ore , : , %minuti , : , %secondi
  MENU SET c1 *TURBO_QUESTER_C1
  MENU SET c2 *TURBO_QUESTER_C2
  MENU SET c3 *TURBO_QUESTER_C3
  MENU SET c4 *TURBO_QUESTER_C4
  MENU SET c5 %my_time
  MENU SET c6 *TURBO_QUESTER_C6
return

sub getMyBag
  finditem CKF C_ , #backpackid
  if #findkind = -1 2
    display ok No bag founded, items will be placed in your backpack.
    set %my_bag #backpackid
  else
    set %my_bag #findid
return
    
sub getTypes
	;WEAPON TYPES=============================================================
	SET %sword YPO_BNF_CPH_INF_FSF_OSF_ASF_HNF_FMH_CNF_LSF_NSF_ISF_SMH_RMH_CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_SFR_
	SET %fencing SRH_AQF_PPF_TSF_WSF_BUO_VPO_WPO_WRH_VRH_XRH_YRH_LTF_MTF_OPO_RRH_CUO_
	SET %mace OLH_JOH_URH_EMH_WTH_DMH_QMH_CFF_BFF_PMH_GMH_ZPF_CQF_FFF_RPF_TRH_YSF_XSF_FBG_VTH_AUH_ZTH_UOH_VOH_AUO_MPO_BUH_CUH_DMH_GFF_QOH_HFF_TLH_GBG_
	SET %bow WOH_TOH_QPO_SPO_WOH_TOH_JSF_USF_LMH_MMH_QPO_SPO_PPO_RPO_JSF_USF_LMH_MMH_PPO_RPO_JSF_
	;Samurai Empire Weapons---
	SET %se_sword CSF_ZRF_MPH_LPH_MSF_BSF_KPO_GUO_KTF_JTF_BPH_EPH_ZSF_ATF_JPH_SOH_POH_NMH_OMH_LPO_ZTO_XTH_YTH_HSF_KSF_NPO_XPO_FUO_JPO_QPF_NPF_KPH_FYG_RHM_JDP_EDP_YCP_KDP_
	SET %se_fencing LDP_HDP_ZCP_FDP_
	SET %se_mace DDP_ADP_IDP_
	SET %se_bow XCP_
	;Mondain's Legacy Weapons---
	SET %ml_sword RFR_NFR_SFR_OFR_EFR_YFR_XFR_DFR_
	SET %ml_fencing KFR_GFR_LFR_HFR_IFR_MFR_FFR_JFR_
	SET %ml_mace VFR_BFR_WFR_CFR_
	SET %ml_bow VER_PFR_wer_QFR_

	SET %shield LIK_MIK_CLK_AIK_CIK_LYD_GIK_NIK_ZHK_HLK_BLK_FIK_BIK_OIK_

	;ARMOR TYPES================================================================
	;Medable first----
	SET %leatherarmor VKH_NJL_QJL_JKH_QKH_DLH_HKH_PKH_SKH_KKH_ELH_PSF_QSK_ATK_ZSK_OSK_YSK_NSK_
	SET %medhelmet NJL_QJL_NZH_TDI_VVI_KZH_PZH_DZH_
	SET %medgorget JKH_RJG_SJG_FJG_MWL_
	SET %medarm HKH_PKH_
	SET %medtunic QKH_DLH_
	SET %femalemedtunic ATK_ZSK_OSK_NSK_
	SET %medglove KKH_SKH_WTO_
	SET %medlegging VKH_ELH_
	SET %femalemedlegging QSK_YSK_PSK_
	;Hats---
	SET %hats CZH_HZH_GZH_BWI_WVI_XVI_YVI_IWI_HWI_KWI_JWI_EWI_JJN_OZH_DZH_BZH_EZH_VSH_QSH_VSH_NWL_
	;Samurai Empire Medable Armor---
	SET %se_leatherarmor RWO_GCP_UBP_OWO_LCP_SWO_GWO_CCP_NCP_OCP_UCP
	SET %se_medhelmet GWO_CCP_
	SET %se_medgorget SWO_
	SET %se_medarm OWO_
	SET %se_medtunic RWO_NCP_
	SET %se_medglove OCP_
	SET %se_medleggins GCP_UBP_LCP_ECP_
	SET %se_medarmor %se_leatherarmor , %se_medhelmet , %se_medgorget , %se_medarm , %se_medtunic , %se_medglove , %se_medleggins
	;Mondain's Legacy Medable Armor---
	SET %ml_medhelmet IJQ_
	SET %ml_medgorget BAS_
	SET %ml_medarm MAS_
	SET %ml_medtunic ZZR_
	SET %ml_medglove CAS_
	SET %ml_medleggings OAS_LAS_
	SET %ml_medarmor %ml_medhelmet , %ml_medgorget , %ml_medarm , %ml_medtunic , %ml_medglove , %ml_medleggings
	;---
	SET %medarmor %leatherarmor , %medHelmet , %hats , %medGorget , %medArm , %medTunic , %femaleMedTunic , %medGlove , %medLegging , %femaleMedLegging , %se_medarmor , %ml_medarmor
	;Non medable---
	SET %studdedarmor ALH_ULH_LLH_YKH_GLH_XKH_FLH_RLH_MLH_SSK_RSK_
	SET %bonearmor ZPH_YPH_WPH_PPH_BQH_QPH_VPH_XPH_CQH_AQH_
	SET %nonmedhelmet DSH_ASH_TSH_ESH_YPH_GSH_OSH_ZPH_ZRH_IWL_CSH_CSH_BSH_FSH_DPH_LKO_NWL_MKH_
	SET %nonmedgorget NSH_ALH_
	SET %nonmedarm BQH_JSH_XLH_GLH_MSH_YLH_QPH_YKH_DLO_
	SET %nonmedtunic HSH_ULH_LLH_ZOH_IKH_VLH_KSH_WLH_WPH_PKO_PPH_RSK_
	SET %femalenonmedtunic USK_SSK_MSK_TSK_LSK_USK_
	SET %nonmedglove VPH_ISH_AQH_BMH_USH_XKH_FLH_KMH_RKO_
	SET %nonmedlegging HMH_LSH_IMH_CQH_WSH_APH_MLH_RLH_NKH_XPH_NKO_
	;Samurai Empire Non Medable Armor---
	SET %se_studdedarmor MWO_NWO_FCP_PCP_TBP_
	SET %se_nonmedhelmet RBP_DCP_QWO_DWO_SBP_VBP_FWO_
	SET %se_nonmedgorget PCP_
	SET %se_nonmedarm NWO_WBP_
	SET %se_nonmedtunic MWO_LWO
	SET %se_nonmedleggins FCP_TBP_ZBP_
	SET %se_nonmedarmor %se_studdedarmor , %se_nonmedhelmet , %se_nonmedgorget , %se_nonmedarm , %se_nonmedtunic , %se_nonmedleggins
	;Mondain's Legacy Non Medable Armor---
	SET %ml_nonmedhelmet RJQ_UJQ_TJQ_
	SET %ml_nonmedgorget QJQ_JJQ_
	SET %ml_nonmedarm PJQ_GJQ_
	SET %ml_nonmedtunic OJQ_ZIQ_
	SET %ml_nonmedgloves NJQ_MJQ_
	SET %ml_nonmedleggings AKQ_LJQ_
	SET %ml_nonmedarmor %ml_nonmedhelmet , %ml_nonmedgorget , %ml_nonmedarm , %ml_nonmedtunic , %ml_nonmedgloves , %ml_nonmedleggings
	;---
	SET %nonmedarmor %studdedarmor , %bonearmor , %nonmedHelmet , %nonmedGorget , %nonmedArm , %nonmedTunic , %femaleNonMedTunic , %nonmedGlove , %nonmedLegging , %se_nonmedarmor , %ml_nonmedarmor
	SET %jewelery CWL_LWL_UJG_IJG_


	SET %loot %shield , %jewelery , %medarmor , %nonmedarmor , %sword , %fencing , %mace , %bow , %se_sword , %se_fencing , %se_mace , %se_bow , %ml_sword , %ml_fencing , %ml_mace , %ml_bow

return
    
    
    
    
    
    
    
    
    
    
    
    
    
    
    








