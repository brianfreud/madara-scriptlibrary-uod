;===============================================================
; Script Name: Zagos Pro Finder
; Author: Zagos
; Version: 1.02
; Client Tested with: 6.0.13.0
; EUO version tested with: 1.5 Version 169
; Shard: FS UODreams
; Revision Date: 06/17/2010
; Public Release: 06/15/2010
; Global Variables Used: None
; Purpose: Automatic search in container / vendor and buy/drag item into backpack
;===============================================================

;===============================================================
;Change Logs
;v1.01 add: better menu to choose property intensity/values
;v1.02 fix: bug in drag&drop item / buy item from vendor
;===============================================================
gosub creamenuscelta
if ( %type <> 0 && %metodo <> 0 )
   gosub cercanpc
else
   gosub targetnpc

gosub creaattesa
gosub loopnpc
gosub creacompleta
halt
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Wait Menu
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
sub creaattesa
    menu clear
    menu window size 40 50
    menu Text text_search 10 10 Searching...
return
sub creacompleta
    menu clear
    menu window size 40 50
    menu Text text_search 10 10 Terminate!
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Starting Menu
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

sub creamenuscelta
    menu window title Zagos Pro Finder
    for  %i 1 15
    {
      set %altezza %i * 25
      menu Text text_ . %i 10 %altezza Property %i :
      menu Edit edit_ . %i 100 %altezza 150
      menu Combo create combo_ . %i 265 %altezza 30
      menu combo add combo_ . %i <
      menu combo add combo_ . %i >
      menu combo add combo_ . %i =
      menu combo select combo_ . %i 0
      menu Edit value_ . %i 310 %altezza 100
    }
    set %altezza %i * 25
    menu Text text_ . %i 10 %altezza Automatic???
    menu check Automatic 125 %altezza 60 20 #false
    set %i %i + 1
    set %altezza %i * 25
    menu Text text_ . %i 10 %altezza NPC???
    menu check NPCCONT 125 %altezza 60 20 #false
    set %i %i + 1
    set %altezza %i * 25
    menu Text text_ . %i 10 %altezza Hide After???
    menu check HIDEAFTER 125 %altezza 60 20 #false
    set %i %i + 1
    set %altezza %i * 25
    menu Button button_1 200 %altezza 50 25 Start!
    set %dimy %altezza + 50
    menu window size  430 %dimy
    menu show

    set #menubutton null
          while #menubutton <> button_1
          wait 1
    set %numprops 0
    for  %i 1 15
    {
         menu get edit_ . %i
         str len #menures
         if ( #strres > 0 )
         {
            set %numprops %numprops + 1
            set %proprieta . %numprops #menures
            ; check if selected zone
            menu get combo_ . %i
            if ( #menures > 0 )
            {
               if ( #menures = 1 )
                   set %direction . %i <
               if ( #menures = 2 )
                   set %direction . %i >
               if ( #menures = 3 )
                   set %direction . %i =
               menu get value_ . %i
               set %value . %i #menures
            }
            else
            {
                set %direction . %i 0
                set %value . %i null
            }
         }
    }
    menu get Automatic
    set %metodo #menures
    menu get NPCCONT
    set %type #menures
    menu get HIDEAFTER
    set %hideafter #menures
    if ( %numprops = 0 )
    {
       Display ok Insert at least 1 property!
       halt
    }
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Find All Npc In the Area
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
sub cercanpc
    set %numnpc 0
    finditem _HS_IS_AV_XU_ G_8
    loopnpc:
    if ( #findindex < #findcnt + 1 )
    {
      event property #findid
      if ( shop in #property )
      {
         set %numnpc %numnpc + 1
         set %npcc . %numnpc #findid
      }
    set #findindex #findindex + 1
    goto loopnpc
}
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Target Specified NPC / Container
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

sub targetNPC
    set #targcurs 1
    while #targcurs = 1
          wait 2
    set %npcc1 #Ltargetid
    set %numnpc 2
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Check All the NPC's looping it.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

sub loopnpc
    ;loop all the vendor / containers
loopnpc:
    set %numnpc %numnpc - 1
    set %npc %npcc . %numnpc ; save NPC
    set %numcont 0 ; container in first level
    ; open vendor/container and save container id  (first level)
    set #lobjectid %npc
    event macro 17
    wait 1s
    set %principale #contid
    ;first Level
      finditem * C_ , %principale
      ciclaprinc:
      if ( #findindex < #findcnt + 1 )
      {
         gosub checkcontainer #findid
         if #result = true
         {
            set %numcont %numcont + 1
            set %cont . %numcont #findid
         }
         else
         {
             gosub checkpezzo #findid
         }
         set #findindex #findindex + 1
         goto ciclaprinc
      }
      ; search in all the container (only in the first level)
      for %i 1 %numcont
      {
          gosub cercasotto %cont . %i
      }
; hide npc
if ( %hideafter <> 0 )
{
   hideitem %npc
}
if ( %numnpc > 1 )
{
   goto loopnpc
}
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Check second level containers
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

sub cercasotto
    set #lobjectid %1
    event macro 17
    wait 1s
    finditem * C_ , %1
    checksecondlevel:
    if ( #findindex < #findcnt + 1 )
    {
      gosub checkpezzo #findid
      set #findindex #findindex + 1
      goto checksecondlevel
    }
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Check if item is a container
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
sub checkcontainer
    event property %1
    if ( Contents in #property )
       return true
    else
        return false
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Check if items properties
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

sub checkpezzo
   set %itemid %1
   event property %1
   set %proptrovate 0
   for %j 1 %numprops
   {
      if ( %proprieta . %j in #property )
      {
         if ( %direction . %j <> 0 )
         {
            gosub checkvalue %proprieta . %j %value . %j %direction . %j
         }
         else
         {
                  set %proptrovate %proptrovate + 1
         }
      }
   }
    if ( %proptrovate = %numprops )
       {
       display YesNo #property $ Take/Buy????
           if #dispRes = yes
           {
              gosub take %itemid
           }
       }
return

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Parse Description
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
sub checkvalue
      set %props %1
      set %value %2
      set %direction %3
      ; save the property lenght
      str len %props
      set %lunghprop #strres
      ; save the item len
      str len #property
      set %lungtot #strres
      ; find position of property in item
      str Pos #property %props
      ; save difference from total lenght - ( property lenght + position where property was found )
      set %diff %lungtot - ( %lunghprop + #strres )
      ; delete useless  part
      str Right #property %diff
      ; save temporary str
      set %tempword #strres
      ; look for a termination
      str pos %tempword $
      set %lunghprop #strres - 1
      ; delete from $ to end of string
      str left %tempword %lunghprop
      set %tempword #strres
      str pos %tempword %
      if ( #strres > 0 )
      {
      set %lunghprop #strres - 1
      str left %tempword %lunghprop
      set %tempword #strres
      }
if ( %tempword %direction %value )
         set %proptrovate %proptrovate + 1
return
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Take Item (if in vendor it'll buy it)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
sub take
msg %type $
 if ( %type <> 0 )
 {
    exevent popup %1 1
    for %attesa 0 10
    {
      set %timeout 10 - %attesa
      event ExMsg #charID 3 0 %timeout Second Left To complete Buy.
      wait 1s
    }
 }
 else
 {
     exevent drag %1
     wait 10
     exevent dropc #backpackid
 }
return
