;==================================
; Script Name: Aegis' Trasher
; Author: Aegis
; Version: 1.2
; Client Tested with: 4.05b
; EUO version tested with: 1.5 Test Version 60
; Shard OSI / FS: FS
; Revision Date:
; Public Release: 11/10/05
; Purpose: Helps to organize/trash large amounts of items in containers.
;==================================
set %jewls _UJG_IJG_CWL_LWL_
set %weap FMH_GFF_EPH_LPH_BPH_CUO_TSF_LPO_JPH_ZTH_RMH_SOH_JOH_KSF_
+FUO_WSF_ATF_OMH_XPO_QPO_NMH_WOH_BNF_LTF_VPO_TOH_WTH_VRH_KTF_LMH_TLH_
+OLH_FFF_ZSF_YTH_BUO_ASF_ISF_BSF_QPF_KPH_CUH_NPO_MPH_UOH_TRH_XRH_CSF_
+KPO_POH_RRH_WPO_EMH_MSF_OPO_BUH_GUO_MMH_VTH_BFF_QMH_YSF_NSF_LSF_AQF_
+GMH_JTF_SRH_CNF_FBG_HNF_ZPF_GBG_OSF_INF_HSF_AUO_ZTO_FSF_WRH_MTF_JPO_
+RPF_HFF_XSF_YPO_JSF_DMH_XTH_PPO_QOH_URH_CQF_CFF_PMH_AUH_SPO_CPH_ZRF_
+MPO_YRH_USF
set %armors YVI_GZH_HZH_NJL_BWI_KWI_IWI_EWI_QJL_JWI_XVI_CZH_HWI_JJN_NZH_BZH_TDI_
+WVI_VVI_QSH_KZH_PZH_DZH_DSH_ASH_TSH_ESH_YPH_GSH_OSH_ZPH_ZRH_IWL_CSH_BSH_FSH_
+DPH_LKO_NWL_MKH_JKH_RJG_SJG_FJG_MWL_NSH_ALH_HKH_PKH_BQH_JSH_XLH_GLH_MSH_YLH_
+QPH_YKH_DLO_QKH_DLH_HSH_ULH_LLH_ZOH_IKH_VLH_KSH_WLH_WPH_PKO_PPH_RSK_ATK_ZSK_
+OSK_NSK_USK_SSK_MSK_TSK_LSK_KKH_SKH_WTO_VPH_ISH_AQH_BMH_USH_XKH_FLH_KMH_RKO_
+VKH_ELH_HMH_LSH_IMH_CQH_WSH_APH_MLH_RLH_NKH_XPH_NKO_QSK_YSK_PSK_OZH_VSH_
set %shields LIK_MIK_CLK_AIK_CIK_LYD_GIK_NIK_ZHK_HLK_BLK_FIK_BIK_OIK
set %targets %jewls , %weap , %armors , %shields
gosub ReadChecks

set %ContLockStat N/A;N/A= disabled
set %ignoreinsured #false ;#false= disabled #true=ignoring
set %securebag #BACKPACKID
set %trashcans IIF_BKF_HIF
finditem %trashcans G_2
if #FINDKIND = -1
{
finditem %trashcans G_18
if #FINDKIND <> -1
Display ok WARNING $ The Trash can is too far away. Manually select another one or a trash bag pressing the "Select Trasher" button.
else
Display ok WARNING $ No Trash can found $ Manually select one or a trash bag pressing the "Select Trasher" button.
}
set %trasher #FINDID

menu clear
menu Font Name MS Sans Serif
menu Font Color WindowText
menu Window Title Aegis' Trasher
menu Window Size 310 310
menu Font Size 8


menu Button trashbutton 160 0 150 50 TRASH!
menu Button keepbutton 160 50 150 50 KEEP!
menu Button ignorebutton 160 100 150 30 IGNORE
menu Button SelTrashButton 160 130 75 20 Select Trasher
menu Button SelSecureButton 235 130 75 20 Select Secure

menu Button ChangePauseButt 160 290 120 20 Pause/Resume
menu Button StopButt 290 290 20 20 X

menu text ListChecks 160 195 Select What to Examine:
menu Check ListCheckWeapon 240 210 70 20 #false Weapons
menu Check ListCheckArmor 160 210 60 20 #false Armors
menu Check ListCheckShield 160 230 60 20 #false Shields
menu Check ListCheckJewel 160 250 60 20 #false Jewels
menu Button ReadCheckbutton 160 270 120 17 Submit Changes

menu Check ListCheckAll 240 250 75 20 #false Everything
menu Check GumpCheckLock 160 150 100 20 #false Container Lock
menu Check InsuredCheckLock 160 170 100 20 #false Ignore Insured
menu Show 500 40


gosub ReadChecks
;==========Main Script Loop====================
displayloop:
wait 2
menu delete prop
set #MenuButton N/A

;======finds items in the container=============

if %ContLockStatID = N/A
finditem %targets C_ , #CONTID
else
finditem %targets C_ , %ContLockStatID

if #FINDKIND = -1 2
menu Text prop 10 10 No More items found $ Script Paused.
gosub PauseScript

if paperdoll in #CONTNAME
goto displayloop

event property #FINDID

if ( insured in #PROPERTY || blessed in #PROPERTY ) && %ignoreinsured = #true 2
ignoreitem #FINDID
goto displayloop

menu Text prop 10 10 #PROPERTY
set %currentExID #FINDID
set %currentExBag #FINDBAGID



;============menu Keyloop================
waitchoice:
wait 1
if #MenuButton = ReadCheckbutton 2
gosub ReadChecks
goto displayloop

if #MenuButton = ignorebutton 2
ignoreitem %currentExID
goto displayloop

if #MenuButton = SelTrashButton 2
gosub selectTrasher
set #MenuButton N/A

if #MenuButton = SelSecureButton 2
gosub selectSecure
set #MenuButton N/A

if #MenuButton = ChangePauseButt 2
set #MenuButton N/A
gosub PauseScript


if #MenuButton = StopButt 3
menu delete prop
menu Text prop 10 10 Script HALTED
halt

if #MenuButton = trashbutton 2
gosub moveitem %currentExID %currentExBag %trasher
goto displayloop

if #MenuButton = keepbutton 3
gosub moveitem %currentExID %currentExBag %securebag
ignoreitem %currentExID
goto displayloop

wait 1
goto waitchoice

;****************************************************
;==%%%%========###############============%%%%===
;===========&&&==#SUBROUTINES#==&&&================
;==%%%%========###############============%%%%===
;****************************************************

sub PauseScript
menu delete prop
menu Text prop 10 10 Script PAUSED $ Click the Pause/Resume $ botton to Resume
waitpause:

if #MenuButton = ReadCheckbutton 2
gosub ReadChecks
return

if #MenuButton = StopButt 3
menu delete prop
menu Text prop 10 10 Script HALTED
halt

wait 2
if #MenuButton <> ChangePauseButt
goto waitpause

set #MenuButton N/A
event property %currentExID
menu Text prop 10 10 #PROPERTY
return
;===============================
sub ReadChecks
set %targets N/A

menu get ListCheckWeapon
if #menures = -1
set %targets %targets , %weap

menu get ListCheckArmor
if #menures = -1
set %targets %targets , %armors

menu get ListCheckShield
if #menures = -1
set %targets %targets , %shields

menu get ListCheckJewel
if #menures = -1
set %targets %targets , %jewls

menu get ListCheckAll
if #menures = -1
set %targets *

menu get GumpCheckLock
if #menures = -1
set %ContLockStatID #CONTID
else
set %ContLockStatID N/A

menu get InsuredCheckLock
if #menures = -1
set %ignoreinsured #true
else
set %ignoreinsured #false

return
;=======================================
sub selectSecure
Display ok Please Select the Secure bag (default is backpack)
set #targcurs 1
while #targcurs = 1
wait 1
set %securebag #ltargetid
return
;=================================================
sub selectTrasher
Display ok Please Select the Trash can/bag
set #targcurs 1
while #targcurs = 1
wait 1
set %trasher #ltargetid
return

;====%1: ID o Type dell'item da muovere
;====%2: ID del container dell'oggetto da muovere
;============(all se è indefinito)
;====%3: Id o type del container di rilascio
sub moveitem
if %2 = all
finditem %1
else
finditem %1 C_ , %2
if #findkind <> -1
{
Exevent Drag #FINDID 1
wait 12
finditem %3
Exevent Dropc #FINDID
}
wait 10
return