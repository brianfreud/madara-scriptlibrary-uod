;================================== 
; Script Name:  My Looter 
; Author: Confucious, CEO, & Nilmer 
; Version: 3.05 Beta
; Client Tested with: 4.0.6a
; EUO version tested with: 1.42.009c
; Shard OSI / FS: OSI / FS 
; Revision Date: July 30, 2004 
; Public Release: July 5, 2004 
; Global Variables Used: 880 
; Purpose: Auto looter using a menu for item groups to loot. 
; Includes hides, bones, bags, turning on/off corpse carving, 
; pause, pick up off ground, and more! 
;==================================

; Changelog
;==================================
; 07/05/04 - Initial Public Release 
; 07/06/04 - Added variable %lootbagtypes to seperate them from
;            the bag types that can be looted. Hopefully helps 
;            with a random error of it not accepting bag types it
;            should for the loot bag.
; 07/07/04 - Adjusted the way bags and lootbags are handled. Now 
;            seperated. Added a bit of debug reports when pack 
;            checks fail for users to report to the forums with.
; 07/08/04 - Added Fletch Items (Feathers and Ammo) button. Added 
;            check for loot on ground within 1 range. Removed Min
;            Range and Max Range buttons. Changed button size and
;            text size. Open Corpse, Cut Hides, Cut Corpse settings
;            are now saved per character. When a Button option is 
;            disabled, or Pause is set to Resume, it will be highlighted
;            Red but no longer change the wording.
; 07/09/04 - Moved Options and What to loot to a Setup Menu. Accessed
;            via a new Setup Button. Main menu now holds Setup and
;            Pause buttons, weight block, and Status block. All 
;            settings are now saved, including the items to loot. Loot
;            List is built during the setup menu now instead of each
;            cycle of the looting checks thanks to the menu changes.
;            Loot Ground, Cut Corpse, and Open Corpse are now check
;            boxes instead of buttons. Added checks for the Open 
;            Corpse and Loot Ground support. If Loot Ground is enabled,
;            it will scan the ground for loot list items to pick up.
;            Cut and Carve includes both corpses and hides/bones/etc
;            now. If you do not have Open Corpse enabled it will not
;            carve it since you won't be looting it. Added Meats to
;            loot items. Will loot raw ribs and legs of lamb.
; 07/10/04 - Changed targeting method for loot bag to use Item ID
;            skill.
; 07/13/04 - Rewrote method handling the button settings being saved.
;            Altered order of init during script load. Backpack is now
;            loaded first, the initialize packs/loot bags/etc before
;            checking for scissors and knives.
; 07/17/04 - Altered targeting of loot bag yet again. Altered timers
;            as per example from CEO's Turbo Scripts.
; 07/18/04 - Added scales as own button. Added to bones list. Added a
;            new Option for Properties. When that is enabled, it will
;            check for buttons set up for "Properties". Such as, a box
;            for LRC items, or Slayers, etc. Those boxes are in their
;            own part of the window now. Moving to making this more
;            flexible and expandable. Hope it goes well :)
; 07/21/04 - Added option for Arcane Clothing/Gems. Adjusted code for
;            Property Looting. Added #contKind web service ability for
;            some checks being added in. Added new item draging code
;            for Looting ground, corpse, and prop checks. Uses some
;            new code to see if it's a stack of items or not. May
;            help speed up looting items that are not stacks. Property
;            Looting for corpses fixed up. Several bugs involved have
;            been resolved. Shields have their own box now, and added
;            to the itemscore_mylooter.txt file too.
; 07/23/04 - Added option to close corpse or not. Added journal check
;            for "You did not earn the right to loot this corpse".
;            Addressed an issue with stack looting. Fixed a bug in
;            Property looting.
; 07/27/04 - Adjustment on setting Backpack ID in script. Adjustment
;            on draging. Moved it to a single sub that handles all the
;            checks the others did themselves. Extended wait timer for
;            after opening corpse. Done for both not ignoring corpses
;            you don't have the right for, but for those on dialup with
;            lag possible. Began Debugging the itemscore script, it is
;            causing script lag issues at times. Unsure what is causing
;            it. Disabling property loot fixes it for now.
; 07/30/04 - Added new function for initGlobalVar. Mainly to compress the
;            code a bit, and help it be a bit more neat. 
; 11/21/04 - Few code syntax fixes. Adjusted Var use to check corpse gump.
;==================================

  initevents

  gosub StartupMenu
  event macro 8 7
  wait 1

  set %scissortypes KAG_JAG
  set %scissorid
  set %body
  set %corpsePack
  set %ScriptName AutoLoot
  set %VarNum 880
  gosub getGlobalVar 880 AutoLoot #charid backpackid NOLOCK
  gosub getGlobalVar 880 AutoLoot #charid lootbag NOLOCK

  gosub initGlobalVar loot -1
  gosub initGlobalVar keepvalue 5

  gosub initGlobalVar lootgold -1
  gosub initGlobalVar lootweapons -1
  gosub initGlobalVar lootarmor -1
  gosub initGlobalVar lootshields -1
  gosub initGlobalVar lootjewelery -1
  gosub initGlobalVar lootbags -1
  gosub initGlobalVar lootgems -1
  gosub initGlobalVar lootregs -1
  gosub initGlobalVar loothides -1
  gosub initGlobalVar lootscales -1
  gosub initGlobalVar lootbones -1
  gosub initGlobalVar lootammo -1
  gosub initGlobalVar lootfeathers -1
  gosub initGlobalVar lootmeats -1
  gosub initGlobalVar lootzoogi -1
  gosub initGlobalVar lootarcane -1

  gosub initGlobalVar lootlrc -1
  gosub initGlobalVar lootlmc -1
  gosub initGlobalVar lootstat -1
  gosub initGlobalVar lootdi -1
  gosub initGlobalVar lootmage -1
  gosub initGlobalVar lootslayer -1
  gosub initGlobalVar lootsc -1
  gosub initGlobalVar lootskill -1
  gosub initGlobalVar lootfcr -1
  gosub initGlobalVar lootnight -1
  gosub initGlobalVar lootrepair -1
  gosub initGlobalVar lootregen -1
  gosub initGlobalVar lootspell -1
  gosub initGlobalVar lootlower -1
  gosub initGlobalVar lootluck -1
  gosub initGlobalVar lootbest -1
  gosub initGlobalVar lootchance -1
  gosub initGlobalVar lootspeed -1
  gosub initGlobalVar lootleech -1
  gosub initGlobalVar lootresist -1

  gosub initGlobalVar ground -1
  gosub initGlobalVar cuthidessetting -1
  gosub initGlobalVar opencorpse -1
  gosub initGlobalVar itemloot -1
  gosub initGlobalVar proploot -1
  gosub initGlobalVar closeCorpse -1


  set %overweight 0
  set %knifetypes CNF_BNF_TSF_WSF_GMH_HNF_TRD_URD
  set %jewelery CWL_IJG_LWL_UJG_
  set %gold POF_
  set %meats VRD_VLI_
  set %gems UVF_VUF_GVF_EVF_HVF_OVF_FVF_BVF_RVF_
  set %regs KUF_JUF_KZF_JZF_MZF_WZF_RZF_SZF_IUF_DUF_TZF_UZF_YZF_
  set %feathers VLK_
  set %scales STO_
  set %ammo RWF_LNK_
  set %bags ZJF_CKF_LKF_
  set %lootbagtypes ZJF_CKF_LKF_
  set %hides EEG_GED_
  set %zoogi TTO_
  set %arcane MTO_JTO_JSL_WTO_LTO_
  set %cuttables OJK_XIK_SJK_IJK_TJK_BJK_UJK_DJK_MJK_AJK_LJK_FJK_RJK_
  +RJK_EJK_ZIK_YIK_JJK_GJK_KJK_HJK_JJK_KJK_EEG_GED_
  set %bones OJK_XIK_SJK_IJK_TJK_BJK_UJK_DJK_MJK_AJK_LJK_FJK_RJK_
  +RJK_EJK_ZIK_YIK_JJK_GJK_KJK_HJK_JJK_KJK_
  set %shields LIK_MIK_CLK_AIK_CIK_LYD_GIK_NIK_ZHK_HLK_BLK_FIK_BIK_OIK_
  set %weapon FMH_GFF_EPH_LPH_BPH_CUO_TSF_LPO_JPH_ZTH_MPF_RMH_SOH_TLH_
  +FUO_WSF_ATF_OMH_XPO_QPO_NMH_WOH_BNF_LTF_VPO_TOH_WTH_VRH_KTF_LMH_
  +OLH_FFF_ZSF_YTH_BUO_ASF_ISF_BSF_QPF_KPH_CUH_NPO_MPH_UOH_TRH_XRH_CSF_
  +KPO_POH_RRH_WPO_EMH_MSF_OPO_BUH_GUO_MMH_VTH_BFF_QMH_YSF_NSF_LSF_
  +GMH_JTF_SRH_CNF_FBG_HNF_ZPF_GBG_OSF_INF_HSF_AUO_ZTO_FSF_WRH_MTF_JPO_
  +RPF_HFF_XSF_YPO_JSF_DMH_XTH_PPO_QOH_URH_CQF_CFF_PMH_AUH_SPO_CPH_AQF_
  +MPO_YRH_USF_JOH_KSF_ZRF_RPO_
  set %armor GSH_OSH_HSH_CZH_JWI_GZH_EWI_BWI_JJN_VVI_WVI
  +_XVI_YVI_IWI_HWI_KWI_ASH_XVL_VJN_FCI_JKH_NJL_KKH_HKH
  +_VKH_QKH_ALH_XKH_YKH_MLH_LLH_QSK_YSK_ATK_USK_OSK_SSK
  +_YPH_VPH_BQH_CQH_PPH_TSH_ZSK_NSK_RSK_PSK_ULH_GLH_FLH
  +_RLH_QJL_DLH_PKH_SKH_ELH_ZPH_WPH_QPH_XPH_JSH_
  set %instruments QRF_NRF_MQF_PRF_LQF_OQF_

  if dmc notin #opts 2
    display ok Enable don't move cursor and hit play. Script Halted.
    halt
  gosub InitPacks
  if ! #result
  {
    display ok You need your backpack open and viewable.$Also need a pack or bag in your backpack to hold your loot. Script Halted.
    halt
  }
  gosub GetKnife
  if ! #result
  {
    display ok You need a knife in your backpack for cutting$up corpses. Script Halted.
    halt
  }
  gosub GetScissors
  if ! #result
  {
    display ok You need scissors in your backpack for cutting$up hides and bones. Script Halted.
    halt
  }

  gosub BackgroundInit

  Init:
  gosub InitMenu
  gosub WeightCheck

  top:
  if #menubutton = setup 2
    set #menubutton closed
    gosub SetupMenu
  if #menubutton = setupdone
  {
    set #menubutton closed
    goto Init
  }
  if #menubutton = pause 2
    set #menubutton closed
    gosub pause
  if #charghost = yes
    gosub pause
  if #weight <> %weight
    gosub WeightCheck

  gosub SetupLootPosition
  wait 5

  gosub CheckGround
  if #result
    gosub PickupLoot

; Check for corpses 
  gosub CheckCorpses
  if ! #result
  {
    goto top
  }

  openbody:
  set %body #findid
  set #lobjectid #findid
  nextCPos 10 10
  event macro 17 0
  set %nt2 #scnt2 + 15
  wait 20

  scanjournal 1
  if you_did_not_earn in #journal
  {
    wait 25
    event SysMessage You don't loot this corpse just yet
    deleteJournal
    goto top
  }

  set %corpsePack #CONTID
  if %itemloot = #true
  {
    gosub LootByItem
  }
  if %proploot = #true
  {
    gosub LootByProperties
  }

  ignoreitem %body
  ignoreitem reset 2
  if %closeCorpse = #true
  {
    click 82 60 r
  }
  event SysMessage finished Corpse.
  goto top

;######################### S U B S ######################### 

  sub StartupMenu
  menu Clear
  menu Window Title My Looter
  menu Window Color Black
  menu Window Size 100 50
  gosub SetMenuFont 14 Red Black #true Left -1
  menu Font Style
  menu Font Name MS Sans Serif
  menu Text ALInit 4 15 Initializing....
  menu Show 421 270 halt
  set #menubutton closed
  return #true

  sub InitPacks
; Opens your backpack then asks you to position it. Waits 5 seconds then it asks you to target the loot
; back you wish to use. If you don't target one then it halts the script. Simple nuff. 
  menu Hide
  GetBackpack:
  event macro 8 7
  wait 25
  contPos 270 518
  if ( #ContName <> Container_Gump ) && ( #ContSize <> 230_204 )
    goto GetBackpack
  if ( #ContName = Container_Gump ) && ( #ContSize = 230_204 )
  {
    set %backpackid #contid
    gosub putGlobalVar 880 AutoLoot #charid backpackid
  }
  display ok You will have 5 seconds to position your backpack on your screen
  wait 5s
  findItem %lootbag C_ , %backpackid
  if #findKind <> -1
  {
    gosub RecallLootBag
    return #true
  }
  display ok Please target the bag you wish to use for overall Loot.$Leather and Bones are put in your backpack's root level
  setlootbaginit:
  event macro 13 3
  WFT:
  wait 25
  if #targcurs = 1
  {
    goto WFT
  }
  wait 5
  findItem #LTARGETID C_ , %backpackid
  if #findType notin %lootbagtypes
  {
    display ok That is not the correct type of container $Container Type: #LTARGETID $Backpack Var: %backpackid $Internal Backpack ID: #BACKPACKID $Lootbag Types List: %lootbagtypes
    goto setlootbaginit
  }
  set %lootbag #LTARGETID
  gosub putGlobalVar 880 AutoLoot #charid lootbag
  gosub SetupLootPosition
  return #true

  sub RecallLootBag
  findItem %lootbag C_ , %backpackid
  if #findType notin %lootbagtypes
  {
    gosub cursor
    return #true
  }
  gosub SetupLootPosition
  return #true

  sub SetupLootPosition
  findItem %lootbag C_ , %backpackid
  if #findkind = -1
  {
    if #charghost = yes
    {
      gosub pause
      return
    }
    display ok Cannot find your loot bag in your backpack! Critical Error. Script Halted.
    halt
  }
  set %x #findX + 20
  set %y #findY + 15
  set %mainx #findX
  set %mainy #findY
  return #true

  sub BackgroundInit
  Set %_contKindInitialized #false
  set #sendHeader Content-type: , #spc , application/x-www-form-urlencoded$
  send HTTPPost www.easyuo.com /contkind.php cliVer= , #cliVer
  if ! %_contKindInitialized
  {
    Display ok There was an error contacting the web service. Make sure you have the$
    +'Allow Send' option enabled. The script requires this in order to work.
    halt
  }
  return #true

  sub InitMenu
  menu Clear
  menu Window Title My Looter
  menu Window Color BtnFace
  menu Window Size 108 155
  menu Font Transparent #true
  menu Font Align Right
  menu Font Name MS Sans Serif
  menu Font Size 10
  menu Font Style
  menu Font Color Maroon
  menu Font Align Left
  menu Font BGColor Red
  menu Text status 0 136 Status: Running
  menu Shape divone -8 132 125 1 3 7 1 Black 7 White
  menu Font Size 8
  menu Font Color WindowText
  menu Font Align Center
  menu Font BGColor BtnFace
  menu Text currweight 20 68 Current Weight
  menu Text weight 44 84 N/A
  menu Text permaxweight 24 100 % Max Weight
  menu Text per 44 116 N/A
  menu Shape divtwo -13 60 125 1 3 7 1 Black 7 White
  menu Button setup 16 4 75 25 Setup
  menu Button pause 16 32 75 25 Pause
  menu Show 421 270
  set #menubutton closed
  return #true

  sub SetupMenu

  menu Clear
  menu Window Title My Looter
  menu Window Color BtnFace
  menu Window Size 490 300
  menu Font Transparent #true
  menu Font Align Right
  menu Font Name MS Sans Serif
  menu Font Size 8
  menu Font Style bu
  menu Font Color WindowText
  menu Font Align Left
  menu Text itemstoloot 84 4 Items To Loot
  menu Text propertiestoloot 316 4 Properties To Loot
  menu Shape EUOShape1 250 -12 1 249 3 7 1 Black 7 White
  menu Shape EUOShape2 -4 236 501 1 3 7 1 Black 7 White
  menu Font Style
  menu Button done 412 270 75 25 Done
  menu Button cursor 332 270 75 25 Set Loot Bag
  menu Button keepval 332 245 75 25 Keep Value
  menu Edit valuefield 410 245 27 %keepvalue
  menu Check opencorpse 4 268 97 17 %opencorpse Open Corpse
  menu Check ground 4 248 97 17 %ground Loot Ground
  menu Check cuthides 108 248 97 17 %cuthidessetting Carve N Cut
  menu Check closeCorpse 108 268 97 17 %closeCorpse Close Corpse
  menu Check itemloot 212 248 97 17 %itemloot Item Loot
  menu Check proploot 212 268 97 17 %proploot Property Loot
  menu Check gold 4 24 97 17 %lootgold Gold
  menu Check weapons 4 44 97 17 %lootweapons Weapons
  menu Check armor 4 64 97 17 %lootarmor Armor
  menu Check shieldsi 4 84 97 17 %lootshields Shields
  menu Check gemsi 4 104 97 17 %lootgems Gems
  menu Check bagsi 4 124 97 17 %lootbags Bags
  menu Check regs 4 144 97 17 %lootregs Reagents
  menu Check ammo 4 164 97 17 %lootammo Ammo
  menu Check feathers 4 184 97 17 %lootfeathers Feathers
  menu Check meats 4 204 97 17 %lootmeats Meats
  menu Check hide 128 24 97 17 %loothides Hides
  menu Check bone 128 44 97 17 %lootbones Bones
  menu Check scales 128 64 97 17 %lootscales Scales
  menu Check zoogi 128 84 97 17 %lootzoogi Zoogi Fungus
  menu Check arcanei 128 104 97 17 %lootzoogi Arcane
  menu Check jewelery 128 124 97 17 %lootjewelery Jewelery
  menu Check lrcp 260 24 97 17 %lootlrc Lower Reagent
  menu Check lmcp 388 24 97 17 %lootlmc Lower Mana
  menu Check statincp 260 64 97 17 %lootstat Stat Increase
  menu Check skillincp 388 64 97 17 %lootskill Skill Increase
  menu Check dip 260 44 97 17 %lootdi Damage Inc.
  menu Check mageitemp 388 44 97 17 %lootmage Mage Item
  menu Check slayerp 260 84 97 17 %lootslayer Slayer
  menu Check scp 388 84 97 17 %lootsc Spell Channel
  menu Check nsp 260 104 97 17 %lootnight Night Sight
  menu Check fcrp 388 104 97 17 %lootfcr Faster Cast
  menu Check lrp 260 124 97 17 %lootrepair Self Repair
  menu Check regenp 388 124 97 17 %lootregen Regeneration
  menu Check spellp 260 144 97 17 %lootspell Spell Chance
  menu Check lowerp 388 144 97 17 %lootlower Hit Lower
  menu Check luckp 260 164 97 17 %lootluck Luck
  menu Check bestp 388 164 97 17 %lootbest Best Skill
  menu Check chancep 260 184 97 17 %lootchance Chance Inc
  menu Check speedp 388 184 97 17 %lootspeed Speed Inc
  menu Check leechp 260 204 97 17 %lootleech Hit Leech
  menu Check resistp 388 204 97 17 %lootresist Higher Resist

  menu Show 421 270
  set #menubutton closed

  WaitForButtons:
  if #menubutton = cursor 2
    set #menubutton closed
    gosub cursor
  if #menubutton = keepval 2
    set #menubutton closed
    gosub SetKeepValue
  if #menubutton = done 2
    goto HandleSetup

  wait 10
  goto WaitForButtons

  HandleSetup:
  set %loot ap_
  set %lootgold #false
  set %lootweapons #false
  set %lootarmor #false
  set %lootshields #false
  set %lootjewelery #false
  set %lootbags #false
  set %lootgems #false
  set %lootregs #false
  set %loothides #false
  set %lootscales #false
  set %lootbones #false
  set %lootammo #false
  set %lootfeathers #false
  set %lootmeats #false
  set %lootzoogi #false
  set %lootarcane #false
  set %ground #false
  set %opencorpse #false
  set %cuthidessetting #false
  set %closeCorpse #false
  set %itemloot #false
  set %proploot #false
  set %lootlrc #false
  set %lootlmc #false
  set %lootstat #false
  set %lootdi #false
  set %lootmage #false
  set %lootslayer #false
  set %lootsc #false
  set %lootskill #false
  set %lootfcr #false
  set %lootnight #false
  set %lootrepair #false
  set %lootregen #false
  set %lootspell #false
  set %lootlower #false
  set %lootluck #false
  set %lootbest #false
  set %lootchance #false
  set %lootspeed #false
  set %lootleech #false
  set %lootresist #false

  menu get gold
  gosub AdjustItemLootSetting lootgold gold
  menu get weapons
  gosub AdjustItemLootSetting lootweapons weapon
  menu get armor
  gosub AdjustItemLootSetting lootarmor armor
  menu get shieldsi
  gosub AdjustItemLootSetting lootshields shields
  menu get jewelery
  gosub AdjustItemLootSetting lootjewelery jewelery
  menu get bagsi
  gosub AdjustItemLootSetting lootbags bags
  menu get gemsi
  gosub AdjustItemLootSetting lootgems gems
  menu get regs
  gosub AdjustItemLootSetting lootregs regs
  menu get hide
  gosub AdjustItemLootSetting loothides hides
  menu get scales
  gosub AdjustItemLootSetting lootscales scales
  menu get bone
  gosub AdjustItemLootSetting lootbones bones
  menu get ammo
  gosub AdjustItemLootSetting lootammo ammo
  menu get feathers
  gosub AdjustItemLootSetting lootfeathers feathers
  menu get meats
  gosub AdjustItemLootSetting lootmeats meats
  menu get zoogi
  gosub AdjustItemLootSetting lootzoogi zoogi
  menu get arcanei
  gosub AdjustItemLootSetting lootarcane arcane
  menu get lrcp
  gosub AdjustPropLootSetting lootlrc
  menu get lmcp
  gosub AdjustPropLootSetting lootlmc
  menu get statincp
  gosub AdjustPropLootSetting lootstat
  menu get skillincp
  gosub AdjustPropLootSetting lootskill
  menu get dip
  gosub AdjustPropLootSetting lootdi
  menu get slayerp
  gosub AdjustPropLootSetting lootslayer
  menu get scp
  gosub AdjustPropLootSetting lootsc
  menu get mageitemp
  gosub AdjustPropLootSetting lootmage
  menu get fcrp
  gosub AdjustPropLootSetting lootfcr
  menu get nsp
  gosub AdjustPropLootSetting lootnight
  menu get lrp
  gosub AdjustPropLootSetting lootrepair
  menu get regenp
  gosub AdjustPropLootSetting lootregen
  menu get spellp
  gosub AdjustPropLootSetting lootspell
  menu get lowerp
  gosub AdjustPropLootSetting lootlower
  menu get luckp
  gosub AdjustPropLootSetting lootluck
  menu get bestp
  gosub AdjustPropLootSetting lootbest
  menu get chancep
  gosub AdjustPropLootSetting lootchance
  menu get speedp
  gosub AdjustPropLootSetting lootspeed
  menu get leechp
  gosub AdjustPropLootSetting lootleech
  menu get resistp
  gosub AdjustPropLootSetting lootresist

  menu get ground
  if #menures = -1
    set %ground #true
    gosub putGlobalVar 880 AutoLoot #charid ground
  menu get opencorpse
  if #menures = -1
    set %opencorpse #true
    gosub putGlobalVar 880 AutoLoot #charid opencorpse
  menu get cuthides
  if #menures = -1
    set %cuthidessetting #true
    gosub putGlobalVar 880 AutoLoot #charid cuthidessetting
  menu get closeCorpse
  if #menures = -1
    set %closeCorpse #true
    gosub putGlobalVar 880 AutoLoot #charid closeCorpse
  menu get itemloot
  if #menures = -1
    set %itemloot #true
    gosub putGlobalVar 880 AutoLoot #charid itemloot
  menu get proploot
  if #menures = -1
    set %proploot #true
    gosub putGlobalVar 880 AutoLoot #charid proploot

  set #menubutton setupdone
  gosub putGlobalVar 880 AutoLoot #charid loot
  return #true

  sub SetKeepValue
  menu GetNum valuefield 5
  set %keepvalue #menuRes
  return #true

  sub AdjustItemLootSetting
; %1 - the %loot,name that will be used to store the checkbox setting
; %2 - the variable name of the item list the button appends to the loot list
  if #menures = -1
  {
    set % . %1 #true
    set %loot %loot , % . %2
  }
  gosub putGlobalVar 880 AutoLoot #charid  %1
  return #true

  sub AdjustPropLootSetting
; %1 - the %lootprop name that will be used to store the checkbox setting
; %2 - the variable name of the item list the button appends to the lootprop list
  if #menures = -1
  {
    set % . %1 #true
  }
  gosub putGlobalVar 880 AutoLoot #charid  %1
  return #true

  sub GetKnife
  finditem %knifetypes C
  if #findkind <> -1
  {
    set %knifeid #findid
    return #true
  }
  return #false

  sub GetScissors
  finditem %scissortypes C
  if #findkind <> -1
  {
    set %scissorid #findid
    gosub getGlobalVar 880 AutoLoot #charid backpackid NOLOCK
    if ! %backpackid
    {
      set %backpackid #findbagid
      gosub putGlobalVar 880 AutoLoot #charid backpackid
      return #true
    }
  }
  return #false

  sub setground
  if %ground = 1
  {
    gosub SetMenuFont 1 Red $A9A9A9 -1 -1 -1
    menu Button ground 101 54 80 25 Loot Ground
    set %ground 0
    gosub putGlobalVar 880 AutoLoot #charid ground
    return
  }
  gosub SetMenuFont 2 Black $A9A9A9 -1 -1 -1
  menu Button ground 101 54 80 25 Loot Ground
  set %ground 1
  gosub putGlobalVar 880 AutoLoot #charid ground
  return

  sub setopencorpse
  if %opencorpse = 1
  {
    gosub SetMenuFont 1 Red $A9A9A9 -1 -1 -1
    menu Button opencorpse 101 78 80 25 Open Corpse
    set %opencorpse 0
    gosub putGlobalVar 880 AutoLoot #charid opencorpse
    return
  }
  gosub SetMenuFont 2 Black $A9A9A9 -1 -1 -1
  menu Button opencorpse 101 78 80 25 Open Corpse
  set %opencorpse 1
  gosub putGlobalVar 880 AutoLoot #charid opencorpse
  return

  sub CutResource
  CR1:
  finditem %hides C_ , %backpackid
  if #findkind <> -1
  {
    set %_findid #findid
    set #ltargetid %_findid
    set #lTargetKind 1
    set #lobjectid %scissorid
    wait 5
    event macro 17 0
    target 2s
    event macro 22 0
    wait  20
    goto CR1
  }
  finditem %bones C_ , %backpackid
  if #findkind <> -1
  {
    set %_findid #findid
    set #ltargetid %_findid
    set #lTargetKind 1
    set #lobjectid %scissorid
    wait 5
    event macro 17 0
    target 2s
    event macro 22 0
    wait  20
    goto CR1
  }
  return #true

  sub CorspeCutter
  set #lobjectid %knifeid
  set #ltargetid #findid
  set #lTargetKind 1
  event macro 17 0
  target 4s
  event macro 22 0
  wait 15
  return #true

  sub cursor
  set #menubutton closed
  display ok Target your loot bag within your backpack
  gosub SetMenuFont 10 Red -1 -1 -1 -1
  cursortop:
  event macro 13 3
  WTF:
  wait 5
  if #targcurs = 1
  {
    goto WTF
  }
  wait 5
  findItem #LTARGETID C_ , %backpackid
  if #findType notin %lootbagtypes
  {
    display ok That is not a valid loot bag type. Script Halted.
    halt
  }
  findItem #LTARGETID C_ , %backpackid
  if #findType notin %lootbagtypes
  {
    display ok That is not in your pack$Container Type: #LTARGETID$Backpack Var: %backpackid$Internal Backpack ID: #BACKPACKID$Lootbag Types List: %lootbagtypes
    goto cursortop
  }
  set %lootbag #LTARGETID
  gosub putGlobalVar 880 AutoLoot #charid lootbag
  gosub SetupLootPosition
  display ok Loot bag has been chosen. Press OK to return to Setup.
  return #true

  sub pause
  gosub SetMenuFont 8 Maroon BtnFace -1 -1 -1
  menu Button pause 16 32 75 25 Resume
  gosub SetMenuFont 10 Maroon -1 -1 -1 -1
  menu delete Status
  menu Text Status 0 136 Status: Paused
  c:
  if #menubutton <> pause
  {
    goto c
  }
  gosub SetMenuFont 8 Black BtnFace -1 -1 -1
  menu Button pause 16 32 75 25 Pause
  gosub SetMenuFont 10 Maroon -1 -1 -1 -1
  menu delete Status
  menu Text Status 0 136 Status: Running
  set #menubutton closed
  return

  sub WeightCheck
  set %overweight 0
  set %weight #weight
  set %perc #weight * 100 / #maxweight
  menu delete weight
  menu delete per
  gosub SetMenuFont 8 WindowText -1 -1 -1 -1
  if %perc > 80
  {
    gosub SetMenuFont 8 Yellow -1 -1 -1 -1
  }
  if %perc > 100
  {
    gosub SetMenuFont 8 Maroon -1 -1 -1 -1
  }
  menu Text weight 44 84 #weight
  menu Text per 44 116 %perc
  if %perc > 100
  {
    set %overweight 1
    gosub OverWeight
  }
  return #true

  sub OverWeight
  gosub SetMenuFont 8 Maroon BtnFace -1 -1 -1
  menu Button pause 16 32 75 25 Resume
  gosub SetMenuFont 10 Maroon -1 -1 -1 -1
  menu delete Status
  menu Text Status 0 136 Status: Overweight
  OWC:
  if %overweight <> 0
  {
    wait 2s
    gosub WeightCheck
    goto OWC
  }
  gosub SetMenuFont 8 Black BtnFace -1 -1 -1
  menu Button pause 16 32 75 25 Pause
  gosub SetMenuFont 10 Maroon -1 -1 -1 -1
  menu delete Status
  menu Text Status 0 136 Status: Running
  gosub WeightCheck
  return #true

  sub SetMenuFont
; %1 - Font Size 
; %2 - Font Color 
; %3 - Font Background Color 
; %4 - Font Transparent 
; %5 - Font Align 
; %6 - Font Style 
; If a param is sent as -1 it will not change it. 
; Example: GoSub SetMenuFont 8 Red -1 -1 -1 -1 -1 
; Explanation: Set Font Size 8, Set Font Color Red, and skil changing the BGColor. 
  if %1 <> -1
    menu Font Size %1
  if %2 <> -1
    menu Font Color %2
  if %3 <> -1
    menu Font BGColor %3
  if %4 <> -1
    menu Font Transparent %4
  if %5 <> -1
    menu Font Align %5
  if %6 <> -1
    menu Font Style %6
  return

  sub CheckGround
  if %ground = #false
    return #false
  finditem %loot G_1
  if #FINDKIND = -1
    return #false
  return #true

  sub PickupLoot
  GroundLoot:
  if #contKind = %amountContKind
  {
    event sleep 1
    click #cursorx #cursory r
    goto GroundLoot
  }
  if #LLIFTEDKIND = 1
  {
    click %x %y p
  }
  gosub GetTheLoot
  return #true

  sub CheckCorpses
  if %opencorpse = #false
    return #false
  finditem YFM G_1
  if #findkind = -1
     return #false
; Cut the corpse
  if %cuthidessetting = #true
    gosub Corspecutter
  return #true

  sub LootByItem
  ItemOQ:
  finditem %loot C_ , %corpsePack
  if #findkind = -1
  {
    if #scnt2 > %nt2
    {
      goto ItemOQ
    }
  }
  wait 15
  FindItemLoot:
  finditem %loot C_ , %corpsePack
  wait 5
  if #findkind = -1
  {
    return #true
  }
  if #contKind = %amountContKind
  {
    Event Sleep 1
    click #cursorx #cursory r
    goto FindItemLoot
  }
  gosub GetTheLoot
  goto FindItemLoot
  return #true

  sub LootByProperties
  set %cntcheck 0
  PropOQ:
  finditem * C_ , %corpsePack
  set %cntcheck %cntcheck + 1
  if #findkind = -1 && %nt2 < #scnt2 && %cntcheck < 3
  {
    wait 2
    goto PropOQ
  }
  if #findkind = -1
  {
    return #true
  }
  wait 8
  FindPropLoot:
  finditem * C_ , %corpsePack
  if #findkind = -1
  {
    wait 8
    return #true
  }
  set %object #findID
  gosub PropCheck #findID
  if %value < %keepvalue
  {
    ignoreitem %object 2
    goto FindPropLoot
  }
  PropLoot:
  if #contKind = %amountContKind
  {
    click mc
    click #cursorx #cursory r
    click dmc
    goto FindPropLoot
  }
  gosub GetTheLoot
  wait 10
  ignoreitem %object 2
  goto FindPropLoot
  return #true

  sub PropCheck
; %1 - ID of the Item to check
  if #findtype notin %armor && #findtype notin %jewelery && #findtype notin %weapon && #findtype notin %shields && #findtype notin %instruments
  {
    return #true
  }
  call #PATHSCR , itemscore_mylooter.txt
  return #true

  sub GetTheLoot
  set %dtlc 0
  DragMyLoot:
  if #contKind = %draggingContKind
    goto DropTheLoot
  if #findkind = -1
    return #false
  if #contKind = %amountContKind
    goto CheckForStackLoot
  Event Drag #FindID
  wait 5
  CheckForStackLoot:
  if #contKind <> %amountContKind && #findstack > 1
    goto CheckForStackLoot
  msg $
  wait 5
  DropTheLoot:
  set %dtlc %dtlc + 1
  if %dtlc > 10
    return
  if #contKind = %amountContKind
    goto CheckForStackLoot
  if #contKind <> %draggingContKind
    goto DropTheLoot
  if #findType notin %cuttables
  {
    click %x %y p
    ignoreitem #findid 2
    return #true
  }
  click %mainx %mainy p
  wait 10
  if %cuthidessetting = #true
    gosub CutResource
  ignoreItem #findID 2
  wait 3
  return #true

  sub initGlobalVar
  set %VarNameSet %1
  set %VarNameSetb %2
  gosub getGlobalVar %VarNum %ScriptName #charid %VarNameSet NOLOCK

  if % . %VarNameSet = N/A
    set % . %VarNameSet #false
  if %VarNameSetb <> -1
    set % . %VarNameSet %VarNameSetb
  return #true

;================================== 
; Script Name:  CEO's * variable management subs (pseudo filesystem) 
; Author: CEO 
; Version: 1.0 
; Client Tested with: 4.0.1b 
; EUO version tested with: 1_41_103 
; Shard OSI / FS: OSI 
; Revision Date: 040219 
; Public Release: 040219 
; Globals Used: User specified (2 total) 
; Purpose: Allows you to use a * variable as a pseudo-filesystem 
;======================================= 
; CEO's global variable management subs: 
;      getGlobalVar  . putGlobalVar  . delGlobalVar 
;======================================= 
Sub getGlobalVar
;ver 1.0 posted 19Feb04 by CEO 
;purpose: Allows you to store multiple variables in one global variable unique to 
;             each player and script. getGlobaVar retrieves a value from a global variable. 
;@returns: #result = #false if variable not found, #true if found 
;            % . %3 contains variable value 
;%1 is the global/persistant variable to use ( %1 + 1 is used for locking) 
;%2 is the script identifer or pseudo filename 
;%3 is #charid or pseudo filename 
;%4 is the variable to search for and return in %. %3  
;%5 is for lock control. By default getGlobalVar uses a lock to return a value. In some cases you may want to 
;       retrieve a variable (mostly for speed) without needing a lock as long as you are aware of  any potentional conflicts
;       that very rarely may occur. Set this param to NOLOCK to access  global storage without a lock. 
;sample usage: 
;               gosub getGlobalVar 50 MyScript #charid backpackid 
; Using global var *50 finds the variable labeled MyScript#charidbackpackid and returns it in %backpackid 
; 
;               gosub getGlobalVar 50 MyScript #charid clothstat NOLOCK 
; Using global var *50 find the variable labeled MyScript#charidclothstat and returns it in %clothstat without using a lock 
; Warning: do not use *1000. * %1 + 1 is used for locking and there is no *1001! 
nameSpace push
nameSpace local #systime , _ , %2 , _ , %3 , _ , %4 , _ , GET
set !lpc #lpc
set #lpc 1000
set !lock %1 + 1
if ( %0 = 5 ) && ( %5 = NOLOCK )
   goto getGlobalVar_skiplock
set !lockcount 0
getGlobalVar_waitforlock:
if * . !lock <> N/A && * . !lock <> #nsname
{
   wait 1 4
   set !lockcount !lockcount + 1
   if !lockcount < 10
      goto getGlobalVar_waitforlock
   if * . !lock <> N/A
   {
      set !currentlock * . !lock
      if !currentlock = N/A
         goto getGlobalVar_waitforlock
      str pos !currentlock _
      set !strres #strres - 1
      str left !currentlock !strres
      set !systime #systime - #strres
      if !systime < 10000 ; if over 10 seconds assume a broken lock and take it 
      {
         set !lockcount 0
         goto getGlobalVar_waitforlock
      }
   }
}
set * . !lock #nsname
wait 3 2
if  * . !lock <> #nsname
{
   set !lockcount 0
   goto getGlobalVar_waitforlock
}
getGlobalVar_skiplock:
set !global * . %1
set !varName %2 , ^ , %3 , ^ , %4 , |
str pos !global !varName
set #result #strres <> 0
if #result
{
   set !varNamePos #strres
   str len !varName
   set !delString !varNamePos + #strres - 1
   str del !global 1 !delString
   set !global #strres
   str pos !global |
   set !varNamePos #strres - 1
   str left !global !varNamePos
   set % . %4 #strres
}
if ( %0 < 5 || %5 <> NOLOCK ) && * . !lock = #nsname
   set * . !lock N/A
set #lpc !lpc
nameSpace Clear
nameSpace Pop
return #result

Sub putGlobalVar
;ver 1.0 posted 19Feb04 by CEO 
;purpose: Allows you to store multiple variables in one global variable unique to 
;             each player and script. putGlobaVar stores a value into a global variable. 
;
;@returns: #result = #false if variable not found, #true if found 
;            % . %3 contains variable 
;%1 is the global/persistant variable to use 
;%2 is the script identifer or pseudo filename 
;%3 #charid or other identifier 
;%4 is the variable name of the variable to save into the* variable identified in %1 
;sample usage: 
;               gosub putGlobalVar 50 MyScript #chardid backpackid 
; Using global var *50 stores/updates the variable labeled MyScript#charidbackpackid  with %backpackid 
; Warning: do not use *1000. * %1 + 1 is used for locking and there is no *1001! 
nameSpace push
nameSpace local #systime , _ , %2 , _ , %3 , _ , %4 , _  , PUT
set !lpc #lpc
set #lpc 1000
set !lock %1 + 1
set !lockcount 0
putGlobalVar_waitforlock:
if * . !lock <> N/A && * . !lock <> #nsname
{
   wait 1 4
   set !lockcount !lockcount + 1
   if !lockcount < 10
      goto putGlobalVar_waitforlock
   if * . !lock <> N/A
   {
      set !currentlock * . !lock
      if !currentlock = N/A
         goto putGlobalVar_waitforlock
      str pos !currentlock _
      set !strres #strres - 1
      str left !currentlock !strres
      set !systime #systime - #strres
      if !systime < 10000 ; if over 10 seconds assume a hung lock and take it 
      {
         set !lockcount 0
         goto putGlobalVar_waitforlock
      }
   }
}
set * . !lock #nsname
wait 4
set !global * . %1
set !varName %2 , ^ , %3 , ^ , %4 , |
str pos !global !varName
if #strres = 0
{
   if  * . !lock <> #nsname
   {
      set !lockcount 0
      goto putGlobalVar_waitforlock
   }
   if  |CEO*FILESYSTEM| notin !global
      set !global |CEO*FILESYSTEM|
   set * . %1 !global , !varName , % . %4 , |
   set * . !lock N/A
   set #lpc !lpc
   nameSpace clear
   nameSpace pop
   return #true
}
set !varNamePos #strres
str len !varName
set !splitString !varNamePos + #strres - 1
str left !global !splitstring
set !globalPart1 #strres
str del !global 1 !splitString
set !global #strres
str len !global
set !globalLen #strres
str pos !global |
set !splitString !globalLen - #strres + 1
str right !global !splitstring
set !global #strres
if  * . !lock <> #nsname
{
   set !lockcount 0
   goto putGlobalVar_waitforlock
}
set * . %1 !globalPart1 , % . %4 , !global
set * . !lock N/A
set #lpc !lpc
nameSpace clear
nameSpace pop
return #true


Sub delGlobalVar
;ver 1.0 posted 19Feb04 by CEO 
;purpose: Allows you to delete a variable out of the globalvar pool unique to 
;             each player and script. delGlobaVar deletes a global variable in a * variable. 
;
;@returns: #result = #false if variable not found, #true if  deleted 
; 
;%1 is the global/persistant variable to use 
;%2 is the script identifer or pseudo filename 
;%3 is #charid or other identifier 
;%4 is the variable name of the variable to save into the* variable identified in %1 
;sample usage: 
;               gosub delGlobalVar 50 MyScript #charid backpackid 
; Using global var *50 deletes  the variable label MyScript#charidbackpackid  and it's associated value. 
; Warning: do not use *1000. * %1 + 1 is used for locking and there is no *1001! 
nameSpace push
nameSpace local #systime , _ , %2 , _ , %3 , _ , %4 , _ , DEL
set !lpc #lpc
set #lpc 1000
set !lock %1 + 1
set !lockcount 0
delGlobalVar_waitforlock:
if * . !lock <> N/A && * . !lock <> #nsname
{
   wait 1 4
   set !lockcount !lockcount + 1
   if !lockcount <  10
      goto delGlobalVar_waitforlock

   if * . !lock <> N/A
   {
      set !currentlock * . !lock
      if !currentlock = N/A
         goto delGlobalVar_waitforlock
      str pos !currentlock _
      set !strres #strres - 1
      str left !currentlock !strres
      set !systime #systime - #strres
      if !systime < 10000 ; if over 10 seconds assume a broken lock and take it 
      {
         set !lockcount 0
         goto delGlobalVar_waitforlock
      }
   }

}
set * . !lock #nsname
wait 4
set !global * . %1
set !varName %2 , ^ , %3 , ^ , %4 , |
str pos !global !varName
set #result #strres <> 0
if #result
{
   set !varNamePos #strres
   str len !varName
   set !splitString !varNamePos + #strres - 1
   set !delString !varNamePos - 1
   str left !global !delString
   set !globalPart1 #strres
   str del !global 1 !splitString
   set !global #strres
   str pos !global |
   set !delString #strres
   str del !global 1 !delString
   if  * . !lock <> #nsname
   {
      set !lockcount 0
      goto delGlobalVar_waitforlock
   }
   set * . %1 !globalPart1 , #strres
}
if  * . !lock = #nsname
   set * . !lock N/A
set #lpc !lpc
nameSpace clear
nameSpace pop
return #result