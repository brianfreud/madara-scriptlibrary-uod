;================================== 
; Script Name: Taz Devil's Shadow Wisp Bone Collector 
; Author: Taz Devil 
; Version: 1.0 
; Client Tested with: 4.0.10b 
; EUO version tested with: EasyUO V1.42 
; Shard OSI / FS: OSI 
; Revision Date: 11/05/2005 
; Public Release: 11/05/2005
; Extra Help By: ScriptFellow  (Pathfind Routine)
; Purpose: 
; Kills Shadow Wisps (Lord Oaks Champ Spawn in Illsh is best spot to use this script)
; loots bones from the corpse, cuts them into usable bones. Sends the bones to the bank
; when you are over weight. 
; 
; Requirements:
; A Bag Of Sending
; A Pair Of Scissors
; A mage that can kill a shaddow wisp with 1 fireball
; LRC Suit (can use regs if need be)
; A good book to read while it runs.
;
; Alter the %CastWait higher if you keep getting the wait to cast spell message.
;================================== 

InitEvents
Set %CharStartPosX #CharPosX
Set %CharStartPosY #CharPosY
Set %CharStartPosZ #CharPosZ
Set %CastWait 25
Set %SafetyLimit 5 
Set %BoneTypes QNF_PNF_KNF_JNF_MNF_LNF_WNF_VNF_YNF_BJK_EJK_DJK_YIK_XIK_AJK_JJK_ZIK_KJK_MJK_LJK_GJK_FJK_IJK_RJK_SJK_UJK_HJK_TJK
Gosub Setup 

MainLoop: 

If #CharGhost = Yes 
{ 
Event SysMessage You are dead and unable to use a Bag of Sending, $ script will now halt. 
Halt 
} 

If #Weight > %MaxWeight 
{ 
FindItem GUF C_ , #BackPackID 

If #FindKind = -1 
{ 
Display OK You are close to becoming over weight and do not$have any bones in your backpack. Please call to$the bank and unload your loot then try again.$Script will now halt. 
Halt 
} 

Gosub SendGold 
} 

Wait 1 

findItem XI G_9 
if #FindKind = -1 
{ 
Gosub FindMoreWisps 
Goto Mainloop 
} 

If #FindKind <> -1 
{ 
Set %Enemy #FindID 
Event Property %Enemy 
If paragon in #Property 
{ 

Repeat: 
Set #LTargetID %enemy 
Set #TargetKind 1 
Event Macro 15 17 
target 
Event Macro 22 0 
wait %CastWait 
 
If #Hits < 70 
{ 
Set #LtargetID #CharID 
Set #LTargetKind 1 
Event Macro 15 28 
Target 
Event Macro 22 0 
Wait 20 
}

FindItem YFM G_10 
If #FindKind = -1 
Goto Repeat 
} 

Set #LTargetID %Enemy 
Set #TargetKind 1 
Event Macro 15 17 
target 
Event Macro 22 0 
Wait 30 
Gosub LootCorpse 
} 

If #Hits < 70 
{ 
Set #LtargetID #CharID 
Set #LTargetKind 1 
Event Macro 15 28 
Target 
Event Macro 22 0 
Wait 20 
} 
Goto MainLoop 

Sub LootCorpse 
FindItem YFM G_9 
if #FindKind <> -1 
{ 
Set %Body #FindID 
Gosub PathFind #FindX #FindY #FindZ 2 20 
If %Return = #False 
{ 
IgnoreItem #FindID 
Goto MainLoop 
} 

set #LObjectID #FindID 
Set #LTargetKind 1 
Event Macro 17 0 
Wait 20 

Set %CorpseID #ContID 
Set %CloseX ( #ContPosX + 50 ) 
Set %closeY ( #ContPosY + 50 ) 
} 
FindItem %BoneTypes C_ , %CorpseID 
If #FindKind = -1 
Click %closeX %CloseY r 

If #FindKind <> -1 
{ 
Set %Bones #FindID 
Event Drag #FindID 
Wait 10 
Click 931 318 p 
wait 10 
Click %closeX %CloseY r 
Wait 5 

set #LObjectID %Scissors 
Set #LTargetID %Bones 
Set #LTargetKind 1 
Event Macro 17 0 
Wait 2 
Event Macro 22 0 
} 

Wait 5 
HideItem %Body 
Return 

Sub SendGold 
RetrySending: 
FindItem GUF C_ , #BackPackID 
If #FindKind <> -1 
{ 
Set #LTargetID #FindID 
Set #LTargetKind 1 
Set #LObjectID %BagOfSending 
Event Macro 17 0 
ScanJournal 1 
If out_of_charges in #Journal 
{ 
Gosub ChargeBag 
Goto RetrySending 
} 

Target 
Event Macro 22 0 
Wait 10 
Return 

Sub ChargeBag 
FindItem EUO C_ , #BackPackId 
If #Findkind = -1 
{ 
Display ok Your bag of Sending is out of charges and$you do not have any Powder of Translocation$left in your backpack.$Script will now halt. 
Halt 
} 
Set #LTargetID %BagOfSending 
Set #LTargetKind 1 
Set #LObjectID #FindID 
Event Macro 17 0 
Target 
Event Macro 22 0 
Wait 10 
Return 

;******************************************************
; Pathfind Sub written by Scriptfellow
;******************************************************
sub pathFind 
  set %_x %1 
  set %_y %2 
  if %0 <= 2 || %2 = N/A 
    set %_z -1 
  else 
    set %_z %3 
  if %0 <= 3 || %3 = N/A 
    set %_tolerance 0 
  else 
    set %_tolerance %4 
  if %0 <= 4 
    set %_endTime #sCnt + 15 
  else 
    set %_endTime #sCnt + %5 
  set %return #false 
  deleteJournal 
  scanJournal 2 
  event PathFind %_x %_y %_z 
_pathFindScanAgain: 
  scanJournal 1 
  if pathfinding in #journal 
    goto _pathFindOkay 
  if can't_get_there in #journal || #sCnt > %_endTime 
    return 
  goto _pathFindScanAgain 
_pathFindOkay: 
  gosub _pathFindDist %_x %_y %_z #charPosX #charPosY #charPosZ 
  set %return %return <= %_tolerance 
  if %return 
  { 
    if %_tolerance > 0 
    { 
      set %1 #cliLeft + #cliXRes / 2 
      set %2 #cliTop + #cliYRes / 2 
      if #charDir < 3 
        set %1 %1 + 1 
      else 
      { 
        if #charDir > 3 && #charDir < 7 
          set %1 %1 - 1 
      } 
      if #charDir = 0 || #charDir = 6 
        set %2 %2 - 1 
      else 
      { 
        if #charDir > 1 && #charDir < 5 
          set %2 %2 + 1 
      } 
      click %1 %2 R 
    } 
    return 
  } 
  if #sCnt > %_endTime 
    return 
  goto _pathFindOkay 
 
sub _pathFindDist 
  set %1 %1 - %4 abs 
  set %2 %2 - %5 abs 
  set %3 %3 - %6 abs 
  gosub max %1 %2 %3 
return 
 
; to be defined 
sub max 
  set %return %1 
  for %_idx 2 %0 
  { 
    if % . %_idx > %return 
      set %return % . %_idx 
  } 
return 
;****************************************************** 
 
Sub FindMoreWisps
Set %SearchTries 0
RetrySearch:
findItem XI G_30 
If #FindKind = -1 && %SearchTries = 0
{ 
Gosub Pathfind %CharStartPosX %CharStartPosY %CharStartPosZ 2 20
Set %SearchTries 1
Goto RetrySearch
}
Else 
{
Display OK Cannot Find Any More Wisps To Kill$Script Will Now Halt....
Halt
}

Gosub PathFind #FindX #FindY #FindZ 4 20 
Return 
 
 
 
 
Sub Setup 
 
 
Event Macro 8 7 
Wait 5 
ContPos 790 380 
Wait 10 
FindBagOfSending: 
FindItem CKF C_ , #BackPackID 
 
If #FindKind = -1 
{ 
Display ok You do not appear to have a bag of sending$in your backpack. If it is inside another$container place it inyour backpack, or go$and get one and restart the script.$Script will now halt. 
Halt 
} 
 
if #FindKind <> -1 
{ 
Event Property #FindID 
 
If sending notin #Property 
{ 
IgnoreItem #FindID 
Goto FindBagOfSending 
} 
 
Set %BagOfSending #FindID 
} 
 
Set %MaxWeight ( #MaxWeight - %SafetyLimit ) 
 
FindItem KAG C_ , #BackPackID 
If #FindKind = -1 
{ 
Display ok you do not have any scissors. 
Halt 
} 
Set %Scissors #FindID 
Return
